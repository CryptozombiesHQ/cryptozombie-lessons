---
title: The setLatestEthPrice Function
actions: ['checkAnswer', 'hints']
requireLogin: true
material:
  editor:
    language: sol
    startingCode:
      "oracle/EthPriceOracle.sol": |
        pragma solidity 0.5.0;
        import "openzeppelin-solidity/contracts/ownership/Ownable.sol";
        import "./CallerContractInterface.sol";
        contract EthPriceOracle is Ownable {
          uint private randNonce = 0;
          uint private modulus = 1000;
          mapping(uint256=>bool) pendingRequests;
          event GetLatestEthPriceEvent(address callerAddress, uint id);
          event SetLatestEthPriceEvent(uint256 ethPrice, address callerAddress);
          function getLatestEthPrice() public returns (uint256) {
            randNonce++;
            uint id = uint(keccak256(abi.encodePacked(now, msg.sender, randNonce))) % modulus;
            pendingRequests[id] = true;
            emit GetLatestEthPriceEvent(msg.sender, id);
            return id;
          }
          // Start here
        }
      "caller/CallerContract.sol": |
        pragma solidity 0.5.0;
        import "./EthPriceOracleInterface.sol";
        import "openzeppelin-solidity/contracts/ownership/Ownable.sol";
        contract CallerContract is Ownable {
            uint256 private ethPrice;
            EthPriceOracleInterface private oracleInstance;
            address private oracleAddress;
            mapping(uint256=>bool) myRequests;
            event newOracleAddressEvent(address oracleAddress);
            event ReceivedNewRequestIdEvent(uint256 id);
            event PriceUpdatedEvent(uint256 ethPrice, uint256 id);
            function setOracleInstanceAddress (address _oracleInstanceAddress) public onlyOwner {
              oracleAddress = _oracleInstanceAddress;
              oracleInstance = EthPriceOracleInterface(oracleAddress);
              emit newOracleAddressEvent(oracleAddress);
            }
            function updateEthPrice() public {
              uint256 id = oracleInstance.getLatestEthPrice();
              myRequests[id] = true;
              emit ReceivedNewRequestIdEvent(id);
            }
            function callback(uint256 _ethPrice, uint256 _id) public onlyOracle {
              require(myRequests[_id], "This request is not in my pending list.");
              ethPrice = _ethPrice;
              delete myRequests[_id];
              emit PriceUpdatedEvent(_ethPrice, _id);
            }
            modifier onlyOracle() {
              require(msg.sender == oracleAddress, "You are not authorized to call this function.");
              _;
            }
        }
      "caller/EthPriceOracleInterface.sol": |
        pragma solidity 0.5.0;
        contract EthPriceOracleInterface {
          function getLatestEthPrice() public returns (uint256);
        }
      "oracle/CallerContractInterface.sol": |
        pragma solidity 0.5.0;
        contract CallerContractInterface {
          function callback(uint256 _ethPrice, uint256 id) public;
        }
    answer: |
      pragma solidity 0.5.0;
      import "openzeppelin-solidity/contracts/ownership/Ownable.sol";
      import "./CallerContractInterface.sol";
      contract EthPriceOracle is Ownable {
        uint private randNonce = 0;
        uint private modulus = 1000;
        mapping(uint256=>bool) pendingRequests;
        event GetLatestEthPriceEvent(address callerAddress, uint id);
        event SetLatestEthPriceEvent(uint256 ethPrice, address callerAddress);
        function getLatestEthPrice() public returns (uint256) {
          randNonce++;
          uint id = uint(keccak256(abi.encodePacked(now, msg.sender, randNonce))) % modulus;
          pendingRequests[id] = true;
          emit GetLatestEthPriceEvent(msg.sender, id);
          return id;
        }
        function setLatestEthPrice(uint256 _ethPrice, address _callerAddress, uint256 _id) public onlyOwner {
          require(pendingRequests[_id], "This request is not in my pending list.");
          delete pendingRequests[_id];
        }
      }
---

Awesome! In this chapter, you'll be putting together what you've learned so far to write the `setLatestEthPrice` function. This is going be a bit more complex, but there's nothing to be afraid of. I'll avoid big leaps of thought and make sure each step is well explained.

So, the JavaScript component of the oracle (the one we'll write in the next lesson) retrieves the ETH price from the Binance public API and then calls the `setLatestEthPrice`, passing it the following arguments:

* The ETH price,
* The address of the contract that initiated the request
* The `id` of the request.

First, your function must make sure that only the owner can call this function. Then, similarly to the code you've written in Chapter 6, your function should check whether the request `id` is valid. If so, it should remove it from `pendingRequests`.

## Put It to the Test

1. Create a `public function` named `setLatestEthPrice`. It should take three parameters: `_ethPrice` (a `uint256`), `_callerAddress` (an `address`), and `_id` (a `uint256`). Don't forget to make it so that only the owner could call it.
2. Use `require` to check if `pendingRequests[_id]` is `true`. The second parameter should be "This request is not in my pending list.". If you don't know how to do this, revisit Chapter 6 to refresh your memory.
3. Remove `id` from the `pendingRequests` mapping. In case you're stuck, here's how you can remove a key from a mapping:

 ```solidity
 delete myMapping[myId];
 ```

We're going to continue defining the `setLatestEthPrice` function in the next chapter.
