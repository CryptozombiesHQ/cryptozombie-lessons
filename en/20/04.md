---
title: Working with Tuples
actions: ['checkAnswer', 'hints']
material:
  editor:
    language: sol
    startingCode:
      "PriceConsumerV3.sol": |
        pragma solidity ^0.6.7;

        import "@chainlink/contracts/src/v0.6/interfaces/AggregatorV3Interface.sol";

        contract PriceConsumerV3 {
          AggregatorV3Interface public priceFeed;

          constructor() public {
            priceFeed = AggregatorV3Interface(0x8A753747A1Fa494EC906cE90E9f37563A8AF630e);
          }
        }
    answer: |
      pragma solidity ^0.6.7;

      import "@chainlink/contracts/src/v0.6/interfaces/AggregatorV3Interface.sol";

      contract PriceConsumerV3 {
        AggregatorV3Interface public priceFeed;

        constructor() public {
          priceFeed = AggregatorV3Interface(0x8A753747A1Fa494EC906cE90E9f37563A8AF630e);
        }

        function getLatestPrice() public view returns (int) {
          (,int price,,,) = priceFeed.latestRoundData();
          return price;
        }
      }
---

Now... all we have to do is call the function on the contract called `latestRoundData` which has all the information we are looking for, plus some!

- `roundId`: The round ID. Each price update gets a unique round ID. 
- `answer`: The current price.
- `startedAt`: Timestamp of when the round started.
- `updatedAt`: Timestamp of when the round was updated.
- `answeredInRound`: The round ID of the round in which the answer was computed.

So, let's create a `public view` function that returns a variable of the same type as the `answer` variable, since that's what we're looking for here!

## Tuples
Woah, MORE Solidity knowledge in a lesson about oracles? Amazing! What are tuples?

A `tuple` is a way in solidity to create a syntactic grouping of expressions. 

If a function returns multiple variables, such as our `latestRoundData`, we consider the return type to be a `tuple` of many types. 

```javascript
function latestRoundData()
    external
    view
    returns (
      uint80 roundId,
      int256 answer,
      uint256 startedAt,
      uint256 updatedAt,
      uint80 answeredInRound
    );
```

In order for us to assign variables to each return variable, we use the tuple syntax by grouping a list of variables in parentheses.

```javascript
 (uint80 roundId, int answer, uint startedAt, uint updatedAt, uint80 answeredInRound) = priceFeed.latestRoundData();
```

We can also rename the variables to anything that we please. For example, let's rename `answer` to `price`.

```javascript
 (uint80 roundId, int price, uint startedAt, uint updatedAt, uint80 answeredInRound) = priceFeed.latestRoundData();
```

Additionally, if there are variables that we are not going to use, it's often best practice to leave them as blanks, like so:

```javascript
 (,int price,,,) = priceFeed.latestRoundData();
```

This way, we can really easily see what variables are important to us, and not declare anything extra. 

## Putting it to the test

1. Create a function called `getLatestPrice` which calls the `priceFeed` contract's `latestRoundData` function.
   
2. Have the function return a variable of the same type as the `answer`, with the answer renamed to `price`. Exclude any other variables from being declared in our function call. 

3. Return the `price`. 
