---
title: Ownable Contracts
actions: ['checkAnswer', 'hints']
requireLogin: true
material:
  editor:
    language: sol
    startingCode:
      "contracts/src/Royalties.sol": |

        pragma solidity ^0.8.0;

        import "@openzeppelin/contracts/token/common/ERC2981.sol";
        import "@openzeppelin/contracts/token/ERC721/extensions/ERC721URIStorage.sol";
        import "@openzeppelin/contracts/utils/Counters.sol";

        // 1. Import `Ownable`

        // 2. Ensure that `Royalties` inherits from Ownable

        contract Royalties is ERC721URIStorage, ERC2981 {
          using Counters for Counters.Counter;
          Counters.Counter private _tokenIdsTracker;
          constructor(string memory _name, string memory _symbol, uint96 _feeNumerator) ERC721(_name, _symbol) {
            _setDefaultRoyalty(msg.sender, _feeNumerator);
          }

          // 3. Add the `onlyOwner` modifier to the mintNFT function

          function mintNFT(address recipient, string memory tokenURI) public returns (uint256) {
          _tokenIdsTracker.increment();
          uint256 tokenId = _tokenIdsTracker.current();
          _safeMint(recipient, tokenId);
          _setTokenURI(tokenId, tokenURI);
          return tokenId;
          }

        }

    answer: |

      pragma solidity ^0.8.0;

      import "@openzeppelin/contracts/token/common/ERC2981.sol";
      import "@openzeppelin/contracts/token/ERC721/extensions/ERC721URIStorage.sol";
      import "@openzeppelin/contracts/utils/Counters.sol";
      import "@openzeppelin/contracts/access/Ownable.sol";

      contract Royalties is ERC721URIStorage, ERC2981, Ownable {
        using Counters for Counters.Counter;
        Counters.Counter private _tokenIdsTracker;
        constructor(string memory _name, string memory _symbol, uint96 _feeNumerator) ERC721(_name, _symbol) {
          _setDefaultRoyalty(msg.sender, _feeNumerator);
        }

        function mintNFT(address recipient, string memory tokenURI) public onlyOwner returns (uint256) {
        _tokenIdsTracker.increment();
        uint256 tokenId = _tokenIdsTracker.current();
        _safeMint(recipient, tokenId);
        _setTokenURI(tokenId, tokenURI);
        return tokenId;
        }

      }
---

Did your keen eyes catch the subtle security loophole lurking in the previous chapter?

The `mintNFT` function is `public`, so anyone can call it! We do want the ability to mint NFTs, but we don't want everyone to be able to do it.

To handle cases like this, one common practice that has emerged is to make contracts `Ownable` — meaning they have an owner (you) who has special privileges.

To do this, we will use the `Ownable` contract provided by OpenZeppelin which basically does the following:

- When a contract is created, its constructor sets the `owner` to `msg.sender` (the person who deployed it)
- It adds an `onlyOwner` modifier, which can restrict access to certain functions to only the `owner`. A function modifier looks just like a function, but uses the keyword `modifier` instead of the keyword `function`. And it can't be called directly as a function can — instead, we can attach the modifier's name at the end of a function definition to change that function's behavior.
- It allows you to transfer the contract to a new `owner`.

**Example**:

```sol
import `@openzeppelin/contracts/access/Ownable.sol`;

contract MyContract Ownable {
  function myFunction() public onlyOwner {
    // Function logic that can only be executed by the contract owner
  }
```

## Putting it to the Test

1. Import the `@openzeppelin/contracts/access/Ownable.sol`.
2. Make it so that the `Royalties` contract inherits from `Ownable`.
3. Add the `onlyOwner` modifier to the `mintNFT` function.

