---
title: Contract Interfaces
actions: ['checkAnswer', 'hints']
requireLogin: true
material:
  editor:
    language: sol
    startingCode:
      "contracts/src/Royalties.sol": |

        pragma solidity ^0.8.0;

        import "@openzeppelin/contracts/token/common/ERC2981.sol";
        import "@openzeppelin/contracts/token/ERC721/extensions/ERC721URIStorage.sol";
        import "@openzeppelin/contracts/utils/Counters.sol";
        import "@openzeppelin/contracts/access/Ownable.sol";

        contract Royalties is ERC721URIStorage, ERC2981, Ownable {
          using Counters for Counters.Counter;
          Counters.Counter private _tokenIdsTracker;
          constructor(string memory _name, string memory _symbol, uint96 _feeNumerator) ERC721(_name, _symbol) {
            _setDefaultRoyalty(msg.sender, _feeNumerator);
          }

          function mintNFT(address recipient, string memory tokenURI) public onlyOwner returns (uint256) {
          _tokenIdsTracker.increment();
          uint256 tokenId = _tokenIdsTracker.current();
          _safeMint(recipient, tokenId);
          _setTokenURI(tokenId, tokenURI);
          return tokenId;
          }

          function mintNFTWithRoyalty(address recipient, string memory tokenURI, address royaltyReceiver, uint96 feeNumerator) public onlyOwner returns (uint256) {
            uint256 tokenId = mintNFT(recipient, tokenURI);
            _setTokenRoyalty(tokenId, royaltyReceiver, feeNumerator);
            return tokenId;
          }

          function supportsInterface(bytes4 interfaceId) public view virtual override(ERC721, ERC2981) returns (bool) {
            // Start here
          }

        }

    answer: |

      pragma solidity ^0.8.0;

      import "@openzeppelin/contracts/token/common/ERC2981.sol";
      import "@openzeppelin/contracts/token/ERC721/extensions/ERC721URIStorage.sol";
      import "@openzeppelin/contracts/utils/Counters.sol";
      import "@openzeppelin/contracts/access/Ownable.sol";

      contract Royalties is ERC721URIStorage, ERC2981, Ownable {
        using Counters for Counters.Counter;
        Counters.Counter private _tokenIdsTracker;
        constructor(string memory _name, string memory _symbol, uint96 _feeNumerator) ERC721(_name, _symbol) {
          _setDefaultRoyalty(msg.sender, _feeNumerator);
        }

        function mintNFT(address recipient, string memory tokenURI) public onlyOwner returns (uint256) {
        _tokenIdsTracker.increment();
        uint256 tokenId = _tokenIdsTracker.current();
        _safeMint(recipient, tokenId);
        _setTokenURI(tokenId, tokenURI);
        return tokenId;
        }

        function mintNFTWithRoyalty(address recipient, string memory tokenURI, address royaltyReceiver, uint96 feeNumerator) public onlyOwner returns (uint256) {
          uint256 tokenId = mintNFT(recipient, tokenURI);
          _setTokenRoyalty(tokenId, royaltyReceiver, feeNumerator);
          return tokenId;
        }

        function supportsInterface(bytes4 interfaceId) public view virtual override(ERC721, ERC2981) returns (bool) {
          return super.supportsInterface(interfaceId);
        }

      }
---

In Solidity, an interface is a way to define a contract's external-facing function signatures without implementing the full contract logic. Interfaces provide a standardized way to interact with other contracts, enabling contract-to-contract communication and interoperability. In other words, interfaces serve as a blueprint that specifies the function names, arguments, return types, and visibility, but they do not contain the actual implementation code.

## Introducing ERC-165

ERC-165 is a standard interface for contract-level support of interfaces that defines a standardized way to check if a contract implements a specific interface.

For example, if you send tokens to a contract that doesn't have a way to transfer them out, those tokens can get stuck there forever. In such situations, having a contract that declares its interface can be incredibly useful.

To help prevent such errors, the ERC-165 standard aims to provide clear definitions of how to interact with a contract and ensure that the necessary functions are available.

## The `supportsInterface` Funtion

To declare that a contract implements a specific interface, you must use the `supportsInterface` function. It takes an interface identifier (`InterfaceId`) as a parameter and returns a boolean value indicating whether the contract supports the specified interface:

```sol
function supportsInterface(bytes4 interfaceId) external view returns (bool);
```

Your contract inherits from both `ERC721URIStorage` and `ERC2981`, and both of these contracts override the `supportsInterface function`,  so you need to make sure that your contract provides a proper implementation of this function.

## Using the `super` Keyword

What you must do is override the `supportsInterface`  function and use the `super` keyword to call the function defined in the parent contract. In Solidity, the `super` keyword is used to invoke or access functions or variables from a parent contract within a derived contract. It allows derived contracts to call and interact with the functions and state variables defined in their parent contract.

**Example**:

```sol
contract Parent {
    function foo() virtual public {
        // Parent contract's logic
    }
}

contract Child is Parent {
    function foo() public override {
        // Extended logic in the derived contract
        super.foo(); // Call to the overridden function in the parent contract
        // Additional logic in the derived contract
    }
}
```

We went ahead and declared the `supportsInterface` function. Let's fill in its body.

## Putting it to the Test

1. Return the result of `super.supportsInterface(interfaceId)`. In other words, you're calling the `supportsInterface` function from the parent contract (`super`) and passing `interfaceId` as an argument. Whatever value it returns, your function should return the same.
