---
title: Keeping Track of Responses
actions: ['checkAnswer', 'hints']
requireLogin: true
material:
  editor:
    language: JavaScript
    startingCode:
      "oracle/EthPriceOracle.sol": |
        pragma solidity 0.5.0;
        import "openzeppelin-solidity/contracts/access/Roles.sol";
        import "./CallerContractInterface.sol";
        contract EthPriceOracle {
          using Roles for Roles.Role;
          Roles.Role private owners;
          Roles.Role private oracles;
          uint private randNonce = 0;
          uint private modulus = 1000;
          uint private numOracles = 0;
          mapping(uint256=>bool) pendingRequests;
          //1. Define `Response`
          mapping (uint256=>Response[]) public requestIdToResponse;
          event GetLatestEthPriceEvent(address callerAddress, uint id);
          event SetLatestEthPriceEvent(uint256 ethPrice, address callerAddress);
          event AddOracleEvent(address oracleAddress);
          event RemoveOracleEvent(address oracleAddress);
          constructor (address _owner) public {
            owners.add(_owner);
          }
          function addOracle (address _oracle) public {
            require(owners.has(msg.sender), "Not an owner!");
            require(!oracles.has(_oracle), "Already an oracle!");
            oracles.add(_oracle);
            numOracles++;
            emit AddOracleEvent(_oracle);
          }
          function removeOracle (address _oracle) public {
            require(owners.has(msg.sender), "Not an owner!");
            require(oracles.has(_oracle), "Not an oracle!");
            require (numOracles > 1, "Do not remove the last oracle!");
            oracles.remove(_oracle);
            numOracles--;
            emit RemoveOracleEvent(_oracle);
          }
          function getLatestEthPrice() public returns (uint256) {
            randNonce++;
            uint id = uint(keccak256(abi.encodePacked(now, msg.sender, randNonce))) % modulus;
            pendingRequests[id] = true;
            emit GetLatestEthPriceEvent(msg.sender, id);
            return id;
          }
          function setLatestEthPrice(uint256 _ethPrice, address _callerAddress, uint256 _id) public {
            require(oracles.has(msg.sender), "Not an oracle!");
            require(pendingRequests[_id], "This request is not in my pending list.");
            // 2. Continue here
            delete pendingRequests[_id];
            CallerContractInterface callerContractInstance;
            callerContractInstance = CallerContractInterface(_callerAddress);
            callerContractInstance.callback(_ethPrice, _id);
            emit SetLatestEthPriceEvent(_ethPrice, _callerAddress);
          }
        }
    answer: |
      pragma solidity 0.5.0;
      import "openzeppelin-solidity/contracts/access/Roles.sol";
      import "./CallerContractInterface.sol";
      contract EthPriceOracle {
        using Roles for Roles.Role;
        Roles.Role private owners;
        Roles.Role private oracles;
        uint private randNonce = 0;
        uint private modulus = 1000;
        uint private numOracles = 0;
        mapping(uint256=>bool) pendingRequests;
        struct Response {
          address oracleAddress;
          address callerAddress;
          uint256 ethPrice;
        }
        mapping (uint256=>Response[]) public requestIdToResponse;
        event GetLatestEthPriceEvent(address callerAddress, uint id);
        event SetLatestEthPriceEvent(uint256 ethPrice, address callerAddress);
        event AddOracleEvent(address oracleAddress);
        event RemoveOracleEvent(address oracleAddress);
        constructor (address _owner) public {
          owners.add(_owner);
        }
        function addOracle (address _oracle) public {
          require(owners.has(msg.sender), "Not an owner!");
          require(!oracles.has(_oracle), "Already an oracle!");
          oracles.add(_oracle);
          numOracles++;
          emit AddOracleEvent(_oracle);
        }
        function removeOracle (address _oracle) public {
          require(owners.has(msg.sender), "Not an owner!");
          require(oracles.has(_oracle), "Not an oracle!");
          require (numOracles > 1, "Do not remove the last oracle!");
          oracles.remove(_oracle);
          numOracles--;
          emit RemoveOracleEvent(_oracle);
        }
        function getLatestEthPrice() public returns (uint256) {
          randNonce++;
          uint id = uint(keccak256(abi.encodePacked(now, msg.sender, randNonce))) % modulus;
          pendingRequests[id] = true;
          emit GetLatestEthPriceEvent(msg.sender, id);
          return id;
        }
        function setLatestEthPrice(uint256 _ethPrice, address _callerAddress, uint256 _id) public {
          require(oracles.has(msg.sender), "Not an oracle!");
          require(pendingRequests[_id], "This request is not in my pending list.");
          Response memory resp;
          resp = Response(msg.sender, _callerAddress, _ethPrice);
          requestIdToResponse[_id].push(resp);
          delete pendingRequests[_id];
          CallerContractInterface callerContractInstance;
          callerContractInstance = CallerContractInterface(_callerAddress);
          callerContractInstance.callback(_ethPrice, _id);
          emit SetLatestEthPriceEvent(_ethPrice, _callerAddress);
        }
      }
---

In this chapter, you're going to start updating the `setLatestEthPrice` function to make the contract work in a more decentralized manner.

With more oracles added, your contract expects more than one response for each request. Thus, the way you're keeping track of responses (that is by adding them the `pendingRequests` mapping) has to change.

So how should you go about it?ðŸ¤”

To keep track of everything, for each response, you'll want to store the following:

* `oracleAddress`
* `callerAddress`
* `ethPrice`

Then, you'll want to associate these variables with the identifier of the request.

And one more thing. It'd be nice if you could do this without throwing away all your work and starting overðŸ¤“.

To accomplish this, you're going to use a `mapping` that'll associate each request id to an array of `struct`s containing `oracleAddress`, `callerAddress`, and `ethPrice` variables.

>Note: In Solidity, you can define a `struct` using something like the following:

  ```Solidity
  struct MyStruct {
    address anAddress;
    uint256 aNumber;
  }
  ```

  Then, you can instantiate `MyStruct` like so:

  ```Solidity
  MyStruct memory myStructInstance; // declare the struct
  myStructInstance = new MyStruct(msg.sender, 200); // initialize it
  ```

  â˜ž Did you notice the `memory` keyword? **_Starting with Solidity 5.0, it is mandatory to specify the data location for every reference type!_**

  Of course, you can change the value of an object's member using an assignment statement, the same way you'd assign a value to any other plain variable. Just keep in mind that, in Solidity, we refer to structs and their members are using the dot notation:

  ```Solidity
  myStructInstance.anAddress = otherAddress
  ```

## Put It to the Test

We've gone ahead and removed the `onlyOwner` modifier from the `setLatestEthPrice` function definition, and added a `require` statement that makes sure `oracles.has` `(msg.sender)` (once again, please excuse the grammar!). We've also added the line of code that defines the `requestIdToResponse` mapping. Make sure you browse through the code before moving on.

1. Let's start by defining a `struct` named `Response` with the following properties inside of it: `oracleAddress` (an `address`), `callerAddress` (an `address`), and `ethPrice` (a `uint256`).
2. Now let's move to the `setLatestEthPrice`. Declare a `Response` variable called `resp`. It should be stored in `memory`.
3. Initialize `resp` with the following values `msg.sender`, `_callerAddress`, and `_ethPrice`.
4. Push `resp` to the array stored at `requestIdToResponse[_id]`.
