---
title: Commenti
actions: ['checkAnswer', 'hints']
requireLogin: true
material:
  editor:
    language: sol
    startingCode:
      "zombieownership.sol": |
        pragma solidity ^0.4.25;

        import "./zombieattack.sol";
        import "./erc721.sol";
        import "./safemath.sol";

        /// TODO: sostituiscilo con le descrizioni di natspec
        contract ZombieOwnership is ZombieAttack, ERC721 {

          using SafeMath for uint256;

          mapping (uint => address) zombieApprovals;

          function balanceOf(address _owner) external view returns (uint256) {
            return ownerZombieCount[_owner];
          }

          function ownerOf(uint256 _tokenId) external view returns (address) {
            return zombieToOwner[_tokenId];
          }

          function _transfer(address _from, address _to, uint256 _tokenId) private {
            ownerZombieCount[_to] = ownerZombieCount[_to].add(1);
            ownerZombieCount[msg.sender] = ownerZombieCount[msg.sender].sub(1);
            zombieToOwner[_tokenId] = _to;
            emit Transfer(_from, _to, _tokenId);
          }

          function transferFrom(address _from, address _to, uint256 _tokenId) external payable {
            require (zombieToOwner[_tokenId] == msg.sender || zombieApprovals[_tokenId] == msg.sender);
            _transfer(_from, _to, _tokenId);
          }

          function approve(address _approved, uint256 _tokenId) external payable onlyOwnerOf(_tokenId) {
            zombieApprovals[_tokenId] = _approved;
            emit Approval(msg.sender, _approved, _tokenId);
          }

        }
      "zombieattack.sol": |
        pragma solidity ^0.4.25;

        import "./zombiehelper.sol";

        contract ZombieAttack is ZombieHelper {
          uint randNonce = 0;
          uint attackVictoryProbability = 70;

          function randMod(uint _modulus) internal returns(uint) {
            randNonce = randNonce.add(1);
            return uint(keccak256(abi.encodePacked(now, msg.sender, randNonce))) % _modulus;
          }

          function attack(uint _zombieId, uint _targetId) external onlyOwnerOf(_zombieId) {
            Zombie storage myZombie = zombies[_zombieId];
            Zombie storage enemyZombie = zombies[_targetId];
            uint rand = randMod(100);
            if (rand <= attackVictoryProbability) {
              myZombie.winCount = myZombie.winCount.add(1);
              myZombie.level = myZombie.level.add(1);
              enemyZombie.lossCount = enemyZombie.lossCount.add(1);
              feedAndMultiply(_zombieId, enemyZombie.dna, "zombie");
            } else {
              myZombie.lossCount = myZombie.lossCount.add(1);
              enemyZombie.winCount = enemyZombie.winCount.add(1);
              _triggerCooldown(myZombie);
            }
          }
        }
      "zombiehelper.sol": |
        pragma solidity ^0.4.25;

        import "./zombiefeeding.sol";

        contract ZombieHelper is ZombieFeeding {

          uint levelUpFee = 0.001 ether;

          modifier aboveLevel(uint _level, uint _zombieId) {
            require(zombies[_zombieId].level >= _level);
            _;
          }

          function withdraw() external onlyOwner {
            address _owner = owner();
            _owner.transfer(address(this).balance);
          }

          function setLevelUpFee(uint _fee) external onlyOwner {
            levelUpFee = _fee;
          }

          function levelUp(uint _zombieId) external payable {
            require(msg.value == levelUpFee);
            zombies[_zombieId].level = zombies[_zombieId].level.add(1);
          }

          function changeName(uint _zombieId, string _newName) external aboveLevel(2, _zombieId) onlyOwnerOf(_zombieId) {
            zombies[_zombieId].name = _newName;
          }

          function changeDna(uint _zombieId, uint _newDna) external aboveLevel(20, _zombieId) onlyOwnerOf(_zombieId) {
            zombies[_zombieId].dna = _newDna;
          }

          function getZombiesByOwner(address _owner) external view returns(uint[]) {
            uint[] memory result = new uint[](ownerZombieCount[_owner]);
            uint counter = 0;
            for (uint i = 0; i < zombies.length; i++) {
              if (zombieToOwner[i] == _owner) {
                result[counter] = i;
                counter++;
              }
            }
            return result;
          }

        }
      "zombiefeeding.sol": |
        pragma solidity ^0.4.25;

        import "./zombiefactory.sol";

        contract KittyInterface {
          function getKitty(uint256 _id) external view returns (
            bool isGestating,
            bool isReady,
            uint256 cooldownIndex,
            uint256 nextActionAt,
            uint256 siringWithId,
            uint256 birthTime,
            uint256 matronId,
            uint256 sireId,
            uint256 generation,
            uint256 genes
          );
        }

        contract ZombieFeeding is ZombieFactory {

          KittyInterface kittyContract;

          modifier onlyOwnerOf(uint _zombieId) {
            require(msg.sender == zombieToOwner[_zombieId]);
            _;
          }

          function setKittyContractAddress(address _address) external onlyOwner {
            kittyContract = KittyInterface(_address);
          }

          function _triggerCooldown(Zombie storage _zombie) internal {
            _zombie.readyTime = uint32(now + cooldownTime);
          }

          function _isReady(Zombie storage _zombie) internal view returns (bool) {
              return (_zombie.readyTime <= now);
          }

          function feedAndMultiply(uint _zombieId, uint _targetDna, string _species) internal onlyOwnerOf(_zombieId) {
            Zombie storage myZombie = zombies[_zombieId];
            require(_isReady(myZombie));
            _targetDna = _targetDna % dnaModulus;
            uint newDna = (myZombie.dna + _targetDna) / 2;
            if (keccak256(abi.encodePacked(_species)) == keccak256(abi.encodePacked("kitty"))) {
              newDna = newDna - newDna % 100 + 99;
            }
            _createZombie("NoName", newDna);
            _triggerCooldown(myZombie);
          }

          function feedOnKitty(uint _zombieId, uint _kittyId) public {
            uint kittyDna;
            (,,,,,,,,,kittyDna) = kittyContract.getKitty(_kittyId);
            feedAndMultiply(_zombieId, kittyDna, "kitty");
          }
        }
      "zombiefactory.sol": |
        pragma solidity ^0.4.25;

        import "./ownable.sol";
        import "./safemath.sol";

        contract ZombieFactory is Ownable {

          using SafeMath for uint256;
          using SafeMath32 for uint32;
          using SafeMath16 for uint16;

          event NewZombie(uint zombieId, string name, uint dna);

          uint dnaDigits = 16;
          uint dnaModulus = 10 ** dnaDigits;
          uint cooldownTime = 1 days;

          struct Zombie {
            string name;
            uint dna;
            uint32 level;
            uint32 readyTime;
            uint16 winCount;
            uint16 lossCount;
          }

          Zombie[] public zombies;

          mapping (uint => address) public zombieToOwner;
          mapping (address => uint) ownerZombieCount;

          function _createZombie(string _name, uint _dna) internal {
            uint id = zombies.push(Zombie(_name, _dna, 1, uint32(now + cooldownTime), 0, 0)) - 1;
            zombieToOwner[id] = msg.sender;
            ownerZombieCount[msg.sender] = ownerZombieCount[msg.sender].add(1);
            emit NewZombie(id, _name, _dna);
          }

          function _generateRandomDna(string _str) private view returns (uint) {
            uint rand = uint(keccak256(abi.encodePacked(_str)));
            return rand % dnaModulus;
          }

          function createRandomZombie(string _name) public {
            require(ownerZombieCount[msg.sender] == 0);
            uint randDna = _generateRandomDna(_name);
            randDna = randDna - randDna % 100;
            _createZombie(_name, randDna);
          }

        }
      "ownable.sol": |
        pragma solidity ^0.4.25;

        /**
        * @title Ownable
        * @dev Il contratto di proprietÃ  ha un indirizzo del proprietario e fornisce funzioni di controllo
        * delle autorizzazioni di base, ciÃ² semplifica l'implementazione delle "autorizzazioni dell'utente".
        */
        contract Ownable {
          address private _owner;

          event OwnershipTransferred(
            address indexed previousOwner,
            address indexed newOwner
          );

          /**
          * @dev Il costruttore di proprietÃ  imposta il `proprietario` originale del contratto sull'account del mittente.
          */
          constructor() internal {
            _owner = msg.sender;
            emit OwnershipTransferred(address(0), _owner);
          }

          /**
          * @return l'indirizzo del proprietario.
          */
          function owner() public view returns(address) {
            return _owner;
          }

          /**
          * @dev Genera se chiamato da qualsiasi account diverso dal proprietario.
          */
          modifier onlyOwner() {
            require(isOwner());
            _;
          }

          /**
          * @return vero se `msg.sender` Ã¨ il proprietario del contratto.
          */
          function isOwner() public view returns(bool) {
            return msg.sender == _owner;
          }

          /**
          * @dev Consente all'attuale proprietario di rinunciare al controllo del contratto.
          * @notice La rinuncia alla proprietÃ  lascerÃ  il contratto senza un proprietario
          * Non sarÃ  piÃ¹ possibile chiamare le funzioni con il modificatore `onlyOwner`.
          */
          function renounceOwnership() public onlyOwner {
            emit OwnershipTransferred(_owner, address(0));
            _owner = address(0);
          }

          /**
          * @dev Consente all'attuale proprietario di trasferire il controllo del contratto a un nuovo proprietario.
          * @param newOwner L'indirizzo a cui trasferire la proprietÃ .
          */
          function transferOwnership(address newOwner) public onlyOwner {
            _transferOwnership(newOwner);
          }

          /**
          * @dev Trasferisce il controllo del contratto a un nuovo proprietario.
          * @param newOwner L'indirizzo a cui trasferire la proprietÃ .
          */
          function _transferOwnership(address newOwner) internal {
            require(newOwner != address(0));
            emit OwnershipTransferred(_owner, newOwner);
            _owner = newOwner;
          }
        }
      "safemath.sol": |
        pragma solidity ^0.4.25;

        /**
         * @title SafeMath
         * @dev Math operations with safety checks that throw on error
         */
        library SafeMath {

          /**
          * @dev Multiplies two numbers, throws on overflow.
          */
          function mul(uint256 a, uint256 b) internal pure returns (uint256) {
            if (a == 0) {
              return 0;
            }
            uint256 c = a * b;
            assert(c / a == b);
            return c;
          }

          /**
          * @dev Integer division of two numbers, truncating the quotient.
          */
          function div(uint256 a, uint256 b) internal pure returns (uint256) {
            // assert(b > 0); // Solidity lo lancia automaticamente quando si divide per 0
            uint256 c = a / b;
            // assert(a == b * c + a % b); // Non esiste un caso in cui ciÃ² non valga
            return c;
          }

          /**
          * @dev Subtracts two numbers, throws on overflow (i.e. if subtrahend is greater than minuend).
          */
          function sub(uint256 a, uint256 b) internal pure returns (uint256) {
            assert(b <= a);
            return a - b;
          }

          /**
          * @dev Adds two numbers, throws on overflow.
          */
          function add(uint256 a, uint256 b) internal pure returns (uint256) {
            uint256 c = a + b;
            assert(c >= a);
            return c;
          }
        }

        /**
         * @title SafeMath32
         * @dev SafeMath library implemented for uint32
         */
        library SafeMath32 {

          function mul(uint32 a, uint32 b) internal pure returns (uint32) {
            if (a == 0) {
              return 0;
            }
            uint32 c = a * b;
            assert(c / a == b);
            return c;
          }

          function div(uint32 a, uint32 b) internal pure returns (uint32) {
            // assert(b > 0); // Solidity lo lancia automaticamente quando si divide per 0
            uint32 c = a / b;
            // assert(a == b * c + a % b); // Non esiste un caso in cui ciÃ² non valga
            return c;
          }

          function sub(uint32 a, uint32 b) internal pure returns (uint32) {
            assert(b <= a);
            return a - b;
          }

          function add(uint32 a, uint32 b) internal pure returns (uint32) {
            uint32 c = a + b;
            assert(c >= a);
            return c;
          }
        }

        /**
         * @title SafeMath16
         * @dev SafeMath library implemented for uint16
         */
        library SafeMath16 {

          function mul(uint16 a, uint16 b) internal pure returns (uint16) {
            if (a == 0) {
              return 0;
            }
            uint16 c = a * b;
            assert(c / a == b);
            return c;
          }

          function div(uint16 a, uint16 b) internal pure returns (uint16) {
            // assert(b > 0); // Solidity lo lancia automaticamente quando si divide per 0
            uint16 c = a / b;
            // assert(a == b * c + a % b); // Non esiste un caso in cui ciÃ² non valga
            return c;
          }

          function sub(uint16 a, uint16 b) internal pure returns (uint16) {
            assert(b <= a);
            return a - b;
          }

          function add(uint16 a, uint16 b) internal pure returns (uint16) {
            uint16 c = a + b;
            assert(c >= a);
            return c;
          }
        }
      "erc721.sol": |
        pragma solidity ^0.4.25;

        contract ERC721 {
          event Transfer(address indexed _from, address indexed _to, uint256 indexed _tokenId);
          event Approval(address indexed _owner, address indexed _approved, uint256 indexed _tokenId);

          function balanceOf(address _owner) external view returns (uint256);
          function ownerOf(uint256 _tokenId) external view returns (address);
          function transferFrom(address _from, address _to, uint256 _tokenId) external payable;
          function approve(address _approved, uint256 _tokenId) external payable;
        }
    answer: |
      pragma solidity ^0.4.25;

      import "./zombieattack.sol";
      import "./erc721.sol";
      import "./safemath.sol";

      contract ZombieOwnership is ZombieAttack, ERC721 {

        using SafeMath for uint256;

        mapping (uint => address) zombieApprovals;

        function balanceOf(address _owner) external view returns (uint256) {
          return ownerZombieCount[_owner];
        }

        function ownerOf(uint256 _tokenId) external view returns (address) {
          return zombieToOwner[_tokenId];
        }

        function _transfer(address _from, address _to, uint256 _tokenId) private {
          ownerZombieCount[_to] = ownerZombieCount[_to].add(1);
          ownerZombieCount[msg.sender] = ownerZombieCount[msg.sender].sub(1);
          zombieToOwner[_tokenId] = _to;
          emit Transfer(_from, _to, _tokenId);
        }

        function transferFrom(address _from, address _to, uint256 _tokenId) external payable {
            require (zombieToOwner[_tokenId] == msg.sender || zombieApprovals[_tokenId] == msg.sender);
            _transfer(_from, _to, _tokenId);
          }

        function approve(address _approved, uint256 _tokenId) external payable onlyOwnerOf(_tokenId) {
            zombieApprovals[_tokenId] = _approved;
            emit Approval(msg.sender, _approved, _tokenId);
          }

      }
---

Il codice di Solidity per il nostro gioco di zombi Ã¨ finito finalmente!

Nelle prossime lezioni vedremo come distribuire il codice su Ethereum e come interagire con esso tramite Web3.js.

Ma un'ultima cosa prima di lasciarti andare nella Lezione 5: parliamo di come **commentare il tuo codice**.

## Sintassi per i commenti

Commentare in Solidity Ã¨ proprio come in JavaScript. Hai giÃ  visto alcuni esempi di commenti a riga singola durante le lezioni di CryptoZombies:

```
// Questo Ã¨ un commento a riga singola. Ãˆ un po' come una nota per sÃ© (o per gli altri)
```

Aggiungi il doppio `//` ovunque vuoi commentare. Ãˆ cosÃ¬ facile che dovresti farlo sempre.

Ma ti capisco â€” a volte una sola riga non Ã¨ sufficiente. Sei nato scrittore, dopo tutto!

Quindi abbiamo anche commenti su piÃ¹ righe:

```
contract CryptoZombies {
  /* Questo Ã¨ un commento a piÃ¹ righe. Vorrei ringraziare tutti voi
     che avete impiegato del tempo per provare questo corso di programmazione.
     So che Ã¨ gratuito e rimarrÃ  gratuito per sempre.

     Sappi che questo Ã¨ ancora l'inizio dello sviluppo di Blockchain.
     Siamo andati molto lontano, ma ci sono molti modi per farlo
     Se abbiamo fatto un errore da qualche parte, puoi
     aiutarci ed aprire una richiesta pull qui:
     https://github.com/loomnetwork/cryptozombie-lessons

     O se hai idee, commenti o vuoi semplicemente dire
     Salve alla nostra community di Telegram su https://t.me/loomnetwork
  */
}
```

In particolare Ã¨ buona norma commentare il codice per spiegare il comportamento previsto di ogni funzione del contratto. In questo modo un altro sviluppatore (o tu, dopo una pausa di 6 mesi da un progetto!) potrÃ  rapidamente scremare e capire ad alto livello cosa fa il tuo codice senza dover leggere il codice stesso.

Lo standard nella comunitÃ  di Solidity Ã¨ quello di usare un formato chiamato **_natspec_**, che assomiglia a questo:

```
/// @title Un contratto per operazioni matematiche di base
/// @author H4XF13LD MORRIS ðŸ’¯ðŸ’¯ðŸ˜ŽðŸ’¯ðŸ’¯
/// @notice Per ora, questo contratto aggiunge solo una funzione di moltiplicazione
contract Math {
  /// @notice Multiplies 2 numbers together
  /// @param x il primo uint.
  /// @param y il secondo uint.
  /// @return z il prodotto di (x * y)
  /// @dev Questa funzione non controlla attualmente la presenza di overflow
  function multiply(uint x, uint y) returns (uint z) {
    // Questo Ã¨ solo un normale commento e non verrÃ  raccolto da natspec
    z = x * y;
  }
}
```

`@title` e `@author` sono semplici.

`@notice` spiega a un **utente** che cosa fa il contratto/la funzione. `@dev` Ã¨ per spiegare ulteriori dettagli agli sviluppatori.

`@param` e `@return` servono per descrivere a cosa serve ogni parametro e qual'Ã¨ il valore di ritorno di una funzione.

Nota che non devi sempre usare tutti questi tag per ogni funzione: tutti i tag sono opzionali. Ma almeno, lascia una nota `@dev` che spieghi cosa fa ogni funzione.

## Facciamo una prova

Se ancora non lo hai notato, il controllo delle risposte di CryptoZombies ignora i commenti quando controlla le tue risposte. Quindi non possiamo effettivamente controllare il tuo codice natspec per questo capitolo ;)

Tuttavia, ormai sei un mago di Solidity â€” supponiamo che questo sia nelle tue corde!

Prova comunque ad aggiungere alcuni tag natspec a `ZombieOwnership`:

1. `@title` â€” Es. Un contratto che gestisce il trasferimento della proprietÃ  degli zombi

2. `@author` â€”Â Il tuo nome!

3. `@dev` â€” Es. Conforme all'implementazione di OpenZeppelin della bozza di specifica ERC721
