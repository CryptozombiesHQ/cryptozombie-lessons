---
title: Tokens su Ethereum
actions: ['checkAnswer', 'hints']
requireLogin: true
material:
  editor:
    language: sol
    startingCode:
      "zombieownership.sol": |
        // Start here
      "zombieattack.sol": |
        pragma solidity ^0.4.25;

        import "./zombiehelper.sol";

        contract ZombieAttack is ZombieHelper {
          uint randNonce = 0;
          uint attackVictoryProbability = 70;

          function randMod(uint _modulus) internal returns(uint) {
            randNonce++;
            return uint(keccak256(abi.encodePacked(now, msg.sender, randNonce))) % _modulus;
          }

          function attack(uint _zombieId, uint _targetId) external ownerOf(_zombieId) {
            Zombie storage myZombie = zombies[_zombieId];
            Zombie storage enemyZombie = zombies[_targetId];
            uint rand = randMod(100);
            if (rand <= attackVictoryProbability) {
              myZombie.winCount++;
              myZombie.level++;
              enemyZombie.lossCount++;
              feedAndMultiply(_zombieId, enemyZombie.dna, "zombie");
            } else {
              myZombie.lossCount++;
              enemyZombie.winCount++;
              _triggerCooldown(myZombie);
            }
          }
        }

      "zombiehelper.sol": |
        pragma solidity ^0.4.25;

        import "./zombiefeeding.sol";

        contract ZombieHelper is ZombieFeeding {

          uint levelUpFee = 0.001 ether;

          modifier aboveLevel(uint _level, uint _zombieId) {
            require(zombies[_zombieId].level >= _level);
            _;
          }

          function withdraw() external onlyOwner {
            address _owner = owner();
            _owner.transfer(address(this).balance);
          }

          function setLevelUpFee(uint _fee) external onlyOwner {
            levelUpFee = _fee;
          }

          function levelUp(uint _zombieId) external payable {
            require(msg.value == levelUpFee);
            zombies[_zombieId].level++;
          }

          function changeName(uint _zombieId, string _newName) external aboveLevel(2, _zombieId) ownerOf(_zombieId) {
            zombies[_zombieId].name = _newName;
          }

          function changeDna(uint _zombieId, uint _newDna) external aboveLevel(20, _zombieId) ownerOf(_zombieId) {
            zombies[_zombieId].dna = _newDna;
          }

          function getZombiesByOwner(address _owner) external view returns(uint[]) {
            uint[] memory result = new uint[](ownerZombieCount[_owner]);
            uint counter = 0;
            for (uint i = 0; i < zombies.length; i++) {
              if (zombieToOwner[i] == _owner) {
                result[counter] = i;
                counter++;
              }
            }
            return result;
          }

        }

      "zombiefeeding.sol": |
        pragma solidity ^0.4.25;

        import "./zombiefactory.sol";

        contract KittyInterface {
          function getKitty(uint256 _id) external view returns (
            bool isGestating,
            bool isReady,
            uint256 cooldownIndex,
            uint256 nextActionAt,
            uint256 siringWithId,
            uint256 birthTime,
            uint256 matronId,
            uint256 sireId,
            uint256 generation,
            uint256 genes
          );
        }

        contract ZombieFeeding is ZombieFactory {

          KittyInterface kittyContract;

          modifier ownerOf(uint _zombieId) {
            require(msg.sender == zombieToOwner[_zombieId]);
            _;
          }

          function setKittyContractAddress(address _address) external onlyOwner {
            kittyContract = KittyInterface(_address);
          }

          function _triggerCooldown(Zombie storage _zombie) internal {
            _zombie.readyTime = uint32(now + cooldownTime);
          }

          function _isReady(Zombie storage _zombie) internal view returns (bool) {
              return (_zombie.readyTime <= now);
          }

          function feedAndMultiply(uint _zombieId, uint _targetDna, string _species) internal ownerOf(_zombieId) {
            Zombie storage myZombie = zombies[_zombieId];
            require(_isReady(myZombie));
            _targetDna = _targetDna % dnaModulus;
            uint newDna = (myZombie.dna + _targetDna) / 2;
            if (keccak256(abi.encodePacked(_species)) == keccak256(abi.encodePacked("kitty"))) {
              newDna = newDna - newDna % 100 + 99;
            }
            _createZombie("NoName", newDna);
            _triggerCooldown(myZombie);
          }

          function feedOnKitty(uint _zombieId, uint _kittyId) public {
            uint kittyDna;
            (,,,,,,,,,kittyDna) = kittyContract.getKitty(_kittyId);
            feedAndMultiply(_zombieId, kittyDna, "kitty");
          }
        }

      "zombiefactory.sol": |
        pragma solidity ^0.4.25;

        import "./ownable.sol";

        contract ZombieFactory is Ownable {

            event NewZombie(uint zombieId, string name, uint dna);

            uint dnaDigits = 16;
            uint dnaModulus = 10 ** dnaDigits;
            uint cooldownTime = 1 days;

            struct Zombie {
              string name;
              uint dna;
              uint32 level;
              uint32 readyTime;
              uint16 winCount;
              uint16 lossCount;
            }

            Zombie[] public zombies;

            mapping (uint => address) public zombieToOwner;
            mapping (address => uint) ownerZombieCount;

            function _createZombie(string _name, uint _dna) internal {
                uint id = zombies.push(Zombie(_name, _dna, 1, uint32(now + cooldownTime), 0, 0)) - 1;
                zombieToOwner[id] = msg.sender;
                ownerZombieCount[msg.sender]++;
                emit NewZombie(id, _name, _dna);
            }

            function _generatePseudoRandomDna(string _str) private view returns (uint) {
                uint rand = uint(keccak256(abi.encodePacked(_str)));
                return rand % dnaModulus;
            }

            function createPseudoRandomZombie(string _name) public {
                require(ownerZombieCount[msg.sender] == 0);
                uint randDna = _generatePseudoRandomDna(_name);
                randDna = randDna - randDna % 100;
                _createZombie(_name, randDna);
            }

        }

      "ownable.sol": |
        pragma solidity ^0.4.25;

        /**
        * @title Ownable
        * @dev Il contratto di proprietÃ  ha un indirizzo del proprietario e fornisce funzioni di controllo
        * delle autorizzazioni di base, ciÃ² semplifica l'implementazione delle "autorizzazioni dell'utente".
        */
        contract Ownable {
          address private _owner;

          event OwnershipTransferred(
            address indexed previousOwner,
            address indexed newOwner
          );

          /**
          * @dev Il costruttore di proprietÃ  imposta il `proprietario` originale del contratto sull'account del mittente.
          */
          constructor() internal {
            _owner = msg.sender;
            emit OwnershipTransferred(address(0), _owner);
          }

          /**
          * @return l'indirizzo del proprietario.
          */
          function owner() public view returns(address) {
            return _owner;
          }

          /**
          * @dev Genera se chiamato da qualsiasi account diverso dal proprietario.
          */
          modifier onlyOwner() {
            require(isOwner());
            _;
          }

          /**
          * @return vero se `msg.sender` Ã¨ il proprietario del contratto.
          */
          function isOwner() public view returns(bool) {
            return msg.sender == _owner;
          }

          /**
          * @dev Consente all'attuale proprietario di rinunciare al controllo del contratto.
          * @notice La rinuncia alla proprietÃ  lascerÃ  il contratto senza un proprietario
          * Non sarÃ  piÃ¹ possibile chiamare le funzioni con il modificatore `onlyOwner`.
          */
          function renounceOwnership() public onlyOwner {
            emit OwnershipTransferred(_owner, address(0));
            _owner = address(0);
          }

          /**
          * @dev Consente all'attuale proprietario di trasferire il controllo del contratto a un nuovo proprietario.
          * @param newOwner L'indirizzo a cui trasferire la proprietÃ .
          */
          function transferOwnership(address newOwner) public onlyOwner {
            _transferOwnership(newOwner);
          }

          /**
          * @dev Trasferisce il controllo del contratto a un nuovo proprietario.
          * @param newOwner L'indirizzo a cui trasferire la proprietÃ .
          */
          function _transferOwnership(address newOwner) internal {
            require(newOwner != address(0));
            emit OwnershipTransferred(_owner, newOwner);
            _owner = newOwner;
          }
        }

    answer: |
      pragma solidity ^0.4.25;

      import "./zombieattack.sol";

      contract ZombieOwnership is ZombieAttack {

      }
---

Parliamo di **_tokens_**.

Se sei stato nello spazio di Ethereum per un certo periodo di tempo, probabilmente hai sentito persone parlare di token, in particolare **_token ERC20_**.

A **_token_** on Ethereum is basically just a smart contract that follows some common rules â€”Â namely it implements a standard set of functions that all other token contracts share, such as `transferFrom(address _from, address _to, uint256 _tokenId)` and `balanceOf(address _owner)`.
Un **_token_** su Ethereum Ã¨ fondamentalmente solo un contratto intelligente che segue alcune regole comuni â€” ovvero implementa un insieme standard di funzioni condivise da tutti gli altri contratti di token, come `transferFrom(address _from, address _to, uint256 _tokenId)` e `balanceOf(address _owner)`.

Internamente il contratto intelligente di solito ha una mappatura, `mapping(address => uint256) balances`, che tiene traccia di quanto saldo ha ciascun indirizzo.

Quindi in pratica un token Ã¨ solo un contratto che tiene traccia di chi possiede la quantitÃ  di quel token e alcune funzioni in modo che tali utenti possano trasferire i loro token ad altri indirizzi.

### PerchÃ¨ Ã¨ importante?

PoichÃ© tutti i token ERC20 condividono lo stesso set di funzioni con gli stessi nomi, tutti possono interagire allo stesso modo.

CiÃ² significa che se si crea un'applicazione in grado di interagire con un token ERC20, Ã¨ anche in grado di interagire con qualsiasi token ERC20. In questo modo Ã¨ possibile aggiungere facilmente altri token alla tua app in futuro senza la necessitÃ  di essere codificati in modo personalizzato. Puoi semplicemente inserire il nuovo indirizzo del contratto token e, boom, la tua app ha un altro token che puÃ² usare.

Un esempio di questo sarebbe un exchange. Quando un exchange aggiunge un nuovo token ERC20, in realtÃ  deve solo aggiungere un altro contratto intelligente con cui parla. Gli utenti possono dire a quel contratto di inviare token all'indirizzo del portafoglio dell'exchange, e l'exchange puÃ² dire al contratto di rispedire i token agli utenti quando richiedono un prelievo.

L'exchange deve solo implementare questa logica di trasferimento una volta, quindi quando vuole aggiungere un nuovo token ERC20, Ã¨ semplicemente una questione di aggiungere il nuovo indirizzo di contratto al suo database.

### Altri token standard

I token ERC20 sono davvero fantastici per i token che agiscono come valute. Ma non sono particolarmente utili per rappresentare gli zombi nel nostro gioco di zombi.

Prima di tutto gli zombi non sono divisibili come le valute: posso inviarti 0,237 ETH, ma il trasferimento di 0,237 di uno zombi non ha davvero senso.

In secondo luogo, tutti gli zombi non sono uguali. Il tuo zombi di livello 2 "**Steve**" non Ã¨ del tutto uguale al mio zombi di livello 732 "**H4XF13LD MORRIS ðŸ’¯ðŸ’¯ðŸ˜ŽðŸ’¯ðŸ’¯**". (Nemmeno vicino, *Steve*).

C'Ã¨ un altro token standard che si adatta molto meglio ai cripto-collezionabili come CryptoZombies â€” e si chiama **_token ERC721._**

**_I token ERC721_** sono **non** intercambiabili poichÃ© ognuno Ã¨ considerato unico e non divisibile. Puoi scambiarle solo in unitÃ  intere e ognuna ha un ID univoco. Quindi sono perfetti per rendere negoziabili i nostri zombi.

> Nota che l'utilizzo di uno standard come ERC721 ha il vantaggio di non dover implementare la logica dell'asta o dell'impegno all'interno del nostro contratto che determina come i giocatori possono scambiare / vendere i nostri zombi. Se ci conformiamo alle specifiche, qualcun altro potrebbe costruire una piattaforma di scambio per asset ERC721 cripto-negoziabili, e i nostri zombie ERC721 sarebbero utilizzabili su quella piattaforma. Quindi ci sono chiari vantaggi nell'utilizzare uno standard token invece di implementare la propria logica di trading.

## Facciamo una prova

Analizzeremo l'implementazione di ERC721 nel prossimo capitolo. Ma prima impostiamo la nostra struttura di file per questa lezione.

Conserveremo tutta la logica ERC721 in un contratto chiamato `ZombieOwnership`.

1. Dichiara la nostra versione `pragma` nella parte superiore del file (controlla i file delle lezioni precedenti per la sintassi).

2. Questo file dovrebbe essere importato da `zombieattack.sol`.

3. Dichiara un nuovo contratto, `ZombieOwnership`, che eredita da `ZombieAttack`. Lascia il corpo del contratto vuoto per ora.
