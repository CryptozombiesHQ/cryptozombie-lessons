---
title: Provider Web3
actions: ['checkAnswer', 'hints']
requireLogin: true
material:
  editor:
    language: html
    startingCode:
      "index.html": |
        <!DOCTYPE html>
        <html lang="en">
          <head>
            <meta charset="UTF-8">
            <title>CryptoZombies front-end</title>
            <script language="javascript" type="text/javascript" src="https://cdnjs.cloudflare.com/ajax/libs/jquery/3.3.1/jquery.min.js"></script>
            <script language="javascript" type="text/javascript" src="web3.min.js"></script>
          </head>
          <body>

            <script>
              // Inizia qui
            </script>
          </body>
        </html>
      "zombieownership.sol": |
        pragma solidity ^0.4.25;

        import "./zombieattack.sol";
        import "./erc721.sol";
        import "./safemath.sol";

        contract ZombieOwnership is ZombieAttack, ERC721 {

          using SafeMath for uint256;

          mapping (uint => address) zombieApprovals;

          function balanceOf(address _owner) external view returns (uint256) {
            return ownerZombieCount[_owner];
          }

          function ownerOf(uint256 _tokenId) external view returns (address) {
            return zombieToOwner[_tokenId];
          }

          function _transfer(address _from, address _to, uint256 _tokenId) private {
            ownerZombieCount[_to] = ownerZombieCount[_to].add(1);
            ownerZombieCount[msg.sender] = ownerZombieCount[msg.sender].sub(1);
            zombieToOwner[_tokenId] = _to;
            emit Transfer(_from, _to, _tokenId);
          }

          function transferFrom(address _from, address _to, uint256 _tokenId) external payable {
              require (zombieToOwner[_tokenId] == msg.sender || zombieApprovals[_tokenId] == msg.sender);
              _transfer(_from, _to, _tokenId);
            }

          function approve(address _approved, uint256 _tokenId) external payable onlyOwnerOf(_tokenId) {
              zombieApprovals[_tokenId] = _approved;
              emit Approval(msg.sender, _approved, _tokenId);
            }

        }
      "zombieattack.sol": |
        pragma solidity ^0.4.25;

        import "./zombiehelper.sol";

        contract ZombieAttack is ZombieHelper {
          uint randNonce = 0;
          uint attackVictoryProbability = 70;

          function randMod(uint _modulus) internal returns(uint) {
            randNonce = randNonce.add(1);
            return uint(keccak256(abi.encodePacked(now, msg.sender, randNonce))) % _modulus;
          }

          function attack(uint _zombieId, uint _targetId) external onlyOwnerOf(_zombieId) {
            Zombie storage myZombie = zombies[_zombieId];
            Zombie storage enemyZombie = zombies[_targetId];
            uint rand = randMod(100);
            if (rand <= attackVictoryProbability) {
              myZombie.winCount = myZombie.winCount.add(1);
              myZombie.level = myZombie.level.add(1);
              enemyZombie.lossCount = enemyZombie.lossCount.add(1);
              feedAndMultiply(_zombieId, enemyZombie.dna, "zombie");
            } else {
              myZombie.lossCount = myZombie.lossCount.add(1);
              enemyZombie.winCount = enemyZombie.winCount.add(1);
              _triggerCooldown(myZombie);
            }
          }
        }
      "zombiehelper.sol": |
        pragma solidity ^0.4.25;

        import "./zombiefeeding.sol";

        contract ZombieHelper is ZombieFeeding {

          uint levelUpFee = 0.001 ether;

          modifier aboveLevel(uint _level, uint _zombieId) {
            require(zombies[_zombieId].level >= _level);
            _;
          }

          function withdraw() external onlyOwner {
            address _owner = owner();
            _owner.transfer(address(this).balance);
          }

          function setLevelUpFee(uint _fee) external onlyOwner {
            levelUpFee = _fee;
          }

          function levelUp(uint _zombieId) external payable {
            require(msg.value == levelUpFee);
            zombies[_zombieId].level = zombies[_zombieId].level.add(1);
          }

          function changeName(uint _zombieId, string _newName) external aboveLevel(2, _zombieId) onlyOwnerOf(_zombieId) {
            zombies[_zombieId].name = _newName;
          }

          function changeDna(uint _zombieId, uint _newDna) external aboveLevel(20, _zombieId) onlyOwnerOf(_zombieId) {
            zombies[_zombieId].dna = _newDna;
          }

          function getZombiesByOwner(address _owner) external view returns(uint[]) {
            uint[] memory result = new uint[](ownerZombieCount[_owner]);
            uint counter = 0;
            for (uint i = 0; i < zombies.length; i++) {
              if (zombieToOwner[i] == _owner) {
                result[counter] = i;
                counter++;
              }
            }
            return result;
          }

        }
      "zombiefeeding.sol": |
        pragma solidity ^0.4.25;

        import "./zombiefactory.sol";

        contract KittyInterface {
          function getKitty(uint256 _id) external view returns (
            bool isGestating,
            bool isReady,
            uint256 cooldownIndex,
            uint256 nextActionAt,
            uint256 siringWithId,
            uint256 birthTime,
            uint256 matronId,
            uint256 sireId,
            uint256 generation,
            uint256 genes
          );
        }

        contract ZombieFeeding is ZombieFactory {

          KittyInterface kittyContract;

          modifier onlyOwnerOf(uint _zombieId) {
            require(msg.sender == zombieToOwner[_zombieId]);
            _;
          }

          function setKittyContractAddress(address _address) external onlyOwner {
            kittyContract = KittyInterface(_address);
          }

          function _triggerCooldown(Zombie storage _zombie) internal {
            _zombie.readyTime = uint32(now + cooldownTime);
          }

          function _isReady(Zombie storage _zombie) internal view returns (bool) {
              return (_zombie.readyTime <= now);
          }

          function feedAndMultiply(uint _zombieId, uint _targetDna, string _species) internal onlyOwnerOf(_zombieId) {
            Zombie storage myZombie = zombies[_zombieId];
            require(_isReady(myZombie));
            _targetDna = _targetDna % dnaModulus;
            uint newDna = (myZombie.dna + _targetDna) / 2;
            if (keccak256(abi.encodePacked(_species)) == keccak256(abi.encodePacked("kitty"))) {
              newDna = newDna - newDna % 100 + 99;
            }
            _createZombie("NoName", newDna);
            _triggerCooldown(myZombie);
          }

          function feedOnKitty(uint _zombieId, uint _kittyId) public {
            uint kittyDna;
            (,,,,,,,,,kittyDna) = kittyContract.getKitty(_kittyId);
            feedAndMultiply(_zombieId, kittyDna, "kitty");
          }
        }
      "zombiefactory.sol": |
        pragma solidity ^0.4.25;

        import "./ownable.sol";
        import "./safemath.sol";

        contract ZombieFactory is Ownable {

          using SafeMath for uint256;
          using SafeMath32 for uint32;
          using SafeMath16 for uint16;

          event NewZombie(uint zombieId, string name, uint dna);

          uint dnaDigits = 16;
          uint dnaModulus = 10 ** dnaDigits;
          uint cooldownTime = 1 days;

          struct Zombie {
            string name;
            uint dna;
            uint32 level;
            uint32 readyTime;
            uint16 winCount;
            uint16 lossCount;
          }

          Zombie[] public zombies;

          mapping (uint => address) public zombieToOwner;
          mapping (address => uint) ownerZombieCount;

          function _createZombie(string _name, uint _dna) internal {
            uint id = zombies.push(Zombie(_name, _dna, 1, uint32(now + cooldownTime), 0, 0)) - 1;
            zombieToOwner[id] = msg.sender;
            ownerZombieCount[msg.sender] = ownerZombieCount[msg.sender].add(1);
            emit NewZombie(id, _name, _dna);
          }

          function _generatePseudoRandomDna(string _str) private view returns (uint) {
            uint rand = uint(keccak256(abi.encodePacked(_str)));
            return rand % dnaModulus;
          }

          function createPseudoRandomZombie(string _name) public {
            require(ownerZombieCount[msg.sender] == 0);
            uint randDna = _generatePseudoRandomDna(_name);
            randDna = randDna - randDna % 100;
            _createZombie(_name, randDna);
          }

        }
      "ownable.sol": |
        pragma solidity ^0.4.25;

        /**
        * @title Ownable
        * @dev Il contratto di proprietà ha un indirizzo del proprietario e fornisce funzioni di controllo
        * delle autorizzazioni di base, ciò semplifica l'implementazione delle "autorizzazioni dell'utente".
        */
        contract Ownable {
          address private _owner;

          event OwnershipTransferred(
            address indexed previousOwner,
            address indexed newOwner
          );

          /**
          * @dev Il costruttore di proprietà imposta il `proprietario` originale del contratto sull'account del mittente.
          */
          constructor() internal {
            _owner = msg.sender;
            emit OwnershipTransferred(address(0), _owner);
          }

          /**
          * @return the address of the owner.
          */
          function owner() public view returns(address) {
            return _owner;
          }

          /**
          * @dev Throws if called by any account other than the owner.
          */
          modifier onlyOwner() {
            require(isOwner());
            _;
          }

          /**
          * @return true if `msg.sender` is the owner of the contract.
          */
          function isOwner() public view returns(bool) {
            return msg.sender == _owner;
          }

          /**
          * @dev Allows the current owner to relinquish control of the contract.
          * @notice Renouncing to ownership will leave the contract without an owner.
          * It will not be possible to call the functions with the `onlyOwner`
          * modifier anymore.
          */
          function renounceOwnership() public onlyOwner {
            emit OwnershipTransferred(_owner, address(0));
            _owner = address(0);
          }

          /**
          * @dev Allows the current owner to transfer control of the contract to a newOwner.
          * @param newOwner The address to transfer ownership to.
          */
          function transferOwnership(address newOwner) public onlyOwner {
            _transferOwnership(newOwner);
          }

          /**
          * @dev Transfers control of the contract to a newOwner.
          * @param newOwner The address to transfer ownership to.
          */
          function _transferOwnership(address newOwner) internal {
            require(newOwner != address(0));
            emit OwnershipTransferred(_owner, newOwner);
            _owner = newOwner;
          }
        }
      "safemath.sol": |
        pragma solidity ^0.4.25;

        /**
         * @title SafeMath
         * @dev Math operations with safety checks that throw on error
         */
        library SafeMath {

          /**
          * @dev Multiplies two numbers, throws on overflow.
          */
          function mul(uint256 a, uint256 b) internal pure returns (uint256) {
            if (a == 0) {
              return 0;
            }
            uint256 c = a * b;
            assert(c / a == b);
            return c;
          }

          /**
          * @dev Integer division of two numbers, truncating the quotient.
          */
          function div(uint256 a, uint256 b) internal pure returns (uint256) {
            // assert(b > 0); // Solidity lo lancia automaticamente quando si divide per 0
            uint256 c = a / b;
            // assert(a == b * c + a % b); // Non esiste un caso in cui ciò non valga
            return c;
          }

          /**
          * @dev Subtracts two numbers, throws on overflow (i.e. if subtrahend is greater than minuend).
          */
          function sub(uint256 a, uint256 b) internal pure returns (uint256) {
            assert(b <= a);
            return a - b;
          }

          /**
          * @dev Adds two numbers, throws on overflow.
          */
          function add(uint256 a, uint256 b) internal pure returns (uint256) {
            uint256 c = a + b;
            assert(c >= a);
            return c;
          }
        }

        /**
         * @title SafeMath32
         * @dev SafeMath library implemented for uint32
         */
        library SafeMath32 {

          function mul(uint32 a, uint32 b) internal pure returns (uint32) {
            if (a == 0) {
              return 0;
            }
            uint32 c = a * b;
            assert(c / a == b);
            return c;
          }

          function div(uint32 a, uint32 b) internal pure returns (uint32) {
            // assert(b > 0); // Solidity lo lancia automaticamente quando si divide per 0
            uint32 c = a / b;
            // assert(a == b * c + a % b); // Non esiste un caso in cui ciò non valga
            return c;
          }

          function sub(uint32 a, uint32 b) internal pure returns (uint32) {
            assert(b <= a);
            return a - b;
          }

          function add(uint32 a, uint32 b) internal pure returns (uint32) {
            uint32 c = a + b;
            assert(c >= a);
            return c;
          }
        }

        /**
         * @title SafeMath16
         * @dev SafeMath library implemented for uint16
         */
        library SafeMath16 {

          function mul(uint16 a, uint16 b) internal pure returns (uint16) {
            if (a == 0) {
              return 0;
            }
            uint16 c = a * b;
            assert(c / a == b);
            return c;
          }

          function div(uint16 a, uint16 b) internal pure returns (uint16) {
            // assert(b > 0); // Solidity lo lancia automaticamente quando si divide per 0
            uint16 c = a / b;
            // assert(a == b * c + a % b); // Non esiste un caso in cui ciò non valga
            return c;
          }

          function sub(uint16 a, uint16 b) internal pure returns (uint16) {
            assert(b <= a);
            return a - b;
          }

          function add(uint16 a, uint16 b) internal pure returns (uint16) {
            uint16 c = a + b;
            assert(c >= a);
            return c;
          }
        }
      "erc721.sol": |
       pragma solidity ^0.4.25;

        contract ERC721 {
          event Transfer(address indexed _from, address indexed _to, uint256 indexed _tokenId);
          event Approval(address indexed _owner, address indexed _approved, uint256 indexed _tokenId);

          function balanceOf(address _owner) external view returns (uint256);
          function ownerOf(uint256 _tokenId) external view returns (address);
          function transferFrom(address _from, address _to, uint256 _tokenId) external payable;
          function approve(address _approved, uint256 _tokenId) external payable;
        }
    answer: |
      <!DOCTYPE html>
      <html lang="en">
        <head>
          <meta charset="UTF-8">
          <title>CryptoZombies front-end</title>
          <script language="javascript" type="text/javascript" src="https://cdnjs.cloudflare.com/ajax/libs/jquery/3.3.1/jquery.min.js"></script>
          <script language="javascript" type="text/javascript" src="web3.min.js"></script>
        </head>
        <body>

          <script>
            window.addEventListener('load', function() {

              // Verifica se Web3 è stato iniettato dal browser (Mist/MetaMask)
              if (typeof web3 !== 'undefined') {
                // Usa il provider Mist/MetaMask
                web3js = new Web3(web3.currentProvider);
              } else {
                // Gestire il caso in cui l'utente non ha installato Metamask
                // Probabilmente mostrare loro un messaggio che richiede di installare Metamask
              }

              // Ora puoi avviare la tua app ed accedere a web3 liberamente:
              startApp()

            })
          </script>
        </body>
      </html>
---

Grande! Ora che abbiamo Web3.js nel nostro progetto, inizializziamolo e parliamo con la blockchain.

La prima cosa di cui abbiamo bisogno è un **_Provider Web3_**.

Ricorda, Ethereum è composto da **_nodi_** che condividono tutti una copia degli stessi dati. L'impostazione di un provider Web3 in Web3.js indica al nostro codice **con quale nodo** dovremmo parlare per gestire le nostre letture e scritture. È un po' come impostare l'URL del server Web remoto per le chiamate API in una WebApp tradizionale.

È possibile ospitare il proprio nodo Ethereum come provider. Tuttavia, esiste un servizio di terze parti che semplifica la vita, quindi non è necessario mantenere il proprio nodo Ethereum per fornire una DApp ai tuoi utenti  — **_Infura_**.

## Infura

<a href="https://infura.io/" target=_blank>Infura</a> è un servizio che mantiene un insieme di nodi Ethereum con un livello di cache per letture veloci, a cui puoi accedere gratuitamente attraverso le loro API. Utilizzando Infura come provider, puoi inviare e ricevere in modo affidabile messaggi da/verso la blockchain di Ethereum senza dover configurare e mantenere il tuo nodo.

È possibile configurare Web3 per utilizzare Infura come provider Web3 come segue:

```
var web3 = new Web3(new Web3.providers.WebsocketProvider("wss://mainnet.infura.io/ws"));
```

Tuttavia poiché la nostra DApp verrà utilizzata da molti utenti — e questi utenti scriveranno sulla blockchain e non solo leggeranno — avremo bisogno di un modo per permettere a questi utenti di firmare le transazioni con la loro chiave privata.

> Nota: Ethereum (e blockchain in generale) utilizzano una coppia di chiavi pubblica/privata per firmare digitalmente le transazioni. Pensalo come una password estremamente sicura per una firma digitale. In questo modo, se modifico alcuni dati sulla blockchain, posso **dimostrare** tramite la mia chiave pubblica di essere stato colui che l'ha firmata, ma poiché nessuno conosce la mia chiave privata, nessuno può creare una transazione per me.

La crittografia è complicata, quindi se non sei un esperto di sicurezza e non sai davvero cosa stai facendo, probabilmente non è una buona idea provare a gestire le chiavi private degli utenti nel front-end della nostra app.

Ma per fortuna non è necessario — ci sono già servizi che gestiscono questo per te. Il più popolare di questi è **_Metamask_**.

## Metamask

<a href="https://metamask.io/" target=_blank>Metamask</a> è un'estensione del browser per Chrome e Firefox che consente agli utenti di gestire in sicurezza i propri account Ethereum e le chiavi private e di utilizzare questi account per interagire con i siti Web che utilizzano Web3.js. (Se non l'hai mai usato prima, vorrai sicuramente installarlo — il tuo browser è abilitato per Web3 e ora puoi interagire con qualsiasi sito Web che comunica con la blockchain di Ethereum!).

E come sviluppatore, se desideri che gli utenti interagiscano con la tua DApp attraverso un sito Web nel loro browser Web (come stiamo facendo con il nostro gioco CryptoZombies), vorrai sicuramente renderlo compatibile con Metamask.

> **Nota**: Metamask utilizza i server Infura come provider web3, proprio come abbiamo fatto sopra, ma offre anche all'utente la possibilità di scegliere il proprio provider web3. Quindi, usando il provider web3 di Metamask, stai dando all'utente una scelta, ed è una cosa in meno di cui devi preoccuparti nella tua app.

## Utilizzo del provider web3 di Metamask

Metamask inserisce il proprio provider web3 nel browser nell'oggetto JavaScript globale `web3`. Quindi la tua app può verificare se esiste `web3` e se usa` web3.currentProvider` come provider.

Ecco alcuni codici modello forniti da Metamask per sapere come possiamo rilevare se l'utente ha Metamask installato e, in caso contrario, dire loro che dovranno installarlo per usare la nostra app:

```
window.addEventListener('load', function() {

  // Verifica se Web3 è stato iniettato dal browser (Mist/MetaMask)
  if (typeof web3 !== 'undefined') {
    // Usa il provider Mist/MetaMask
    web3js = new Web3(web3.currentProvider);
  } else {
    // Gestisci il caso in cui l'utente non ha web3. Probabilmente
    // mostra loro un messaggio dove si dice di installare Metamask in
    // per utilizzare la nostra app.
  }

  // Ora puoi avviare la tua app e accedere a web3js liberamente:
  startApp()

})
```

Puoi utilizzare questo codice boilerplate in tutte le app che crei per richiedere agli utenti di avere Metamask per utilizzare la tua DApp.

> Nota: ci sono altri programmi di gestione delle chiavi private che gli utenti potrebbero utilizzare oltre a MetaMask, come il browser Web **Mist**. Tuttavia, entrambi implementano un modello comune di iniezione della variabile `web3`, quindi il metodo che descriviamo qui per rilevare il provider web3 dell'utente funzionerà per entrambi.

## Facciamo una prova

Abbiamo creato alcuni tag script vuoti prima del tag di chiusura `</body>` nel nostro file HTML. Possiamo scrivere qui il nostro codice JavaScript per questa lezione.

1. Procedi e copia/incolla il codice template da sopra per rilevare Metamask. È il blocco che inizia con `window.addEventListener`.
