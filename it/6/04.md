---
title: Chiama le funzioni di un contratto
actions: ['checkAnswer', 'hints']
requireLogin: true
material:
  editor:
    language: html
    startingCode:
      "index.html": |
        <!DOCTYPE html>
        <html lang="en">
          <head>
            <meta charset="UTF-8">
            <title>CryptoZombies front-end</title>
            <script language="javascript" type="text/javascript" src="https://cdnjs.cloudflare.com/ajax/libs/jquery/3.3.1/jquery.min.js"></script>
            <script language="javascript" type="text/javascript" src="web3.min.js"></script>
            <script language="javascript" type="text/javascript" src="cryptozombies_abi.js"></script>
          </head>
          <body>

            <script>
              var cryptoZombies;

              function startApp() {
                var cryptoZombiesAddress = "YOUR_CONTRACT_ADDRESS";
                cryptoZombies = new web3js.eth.Contract(cryptoZombiesABI, cryptoZombiesAddress);
              }

              function getZombieDetails(id) {
                return cryptoZombies.methods.zombies(id).call()
              }

              // 1. Definisci `zombieToOwner` qui

              // 2. Definisci `getZombiesByOwner` qui

              window.addEventListener('load', function() {

                // Verifica se Web3 è stato iniettato dal browser (Mist/MetaMask)
                if (typeof web3 !== 'undefined') {
                  // Usa il provider Mist/MetaMask
                  web3js = new Web3(web3.currentProvider);
                } else {
                // Gestire il caso in cui l'utente non ha installato Metamask
                // Probabilmente mostrare loro un messaggio che richiede di installare Metamask
                }

                // Ora puoi avviare la tua app e accedere a web3 liberamente:
                startApp()

              })
            </script>
          </body>
        </html>
      "zombieownership.sol": |
        pragma solidity ^0.4.25;

        import "./zombieattack.sol";
        import "./erc721.sol";
        import "./safemath.sol";

        contract ZombieOwnership is ZombieAttack, ERC721 {

          using SafeMath for uint256;

          mapping (uint => address) zombieApprovals;

          function balanceOf(address _owner) external view returns (uint256) {
            return ownerZombieCount[_owner];
          }

          function ownerOf(uint256 _tokenId) external view returns (address) {
            return zombieToOwner[_tokenId];
          }

          function _transfer(address _from, address _to, uint256 _tokenId) private {
            ownerZombieCount[_to] = ownerZombieCount[_to].add(1);
            ownerZombieCount[msg.sender] = ownerZombieCount[msg.sender].sub(1);
            zombieToOwner[_tokenId] = _to;
            emit Transfer(_from, _to, _tokenId);
          }

          function transferFrom(address _from, address _to, uint256 _tokenId) external payable {
              require (zombieToOwner[_tokenId] == msg.sender || zombieApprovals[_tokenId] == msg.sender);
              _transfer(_from, _to, _tokenId);
            }

          function approve(address _approved, uint256 _tokenId) external payable onlyOwnerOf(_tokenId) {
              zombieApprovals[_tokenId] = _approved;
              emit Approval(msg.sender, _approved, _tokenId);
            }

        }
      "zombieattack.sol": |
        pragma solidity ^0.4.25;

        import "./zombiehelper.sol";

        contract ZombieAttack is ZombieHelper {
          uint randNonce = 0;
          uint attackVictoryProbability = 70;

          function randMod(uint _modulus) internal returns(uint) {
            randNonce = randNonce.add(1);
            return uint(keccak256(abi.encodePacked(now, msg.sender, randNonce))) % _modulus;
          }

          function attack(uint _zombieId, uint _targetId) external onlyOwnerOf(_zombieId) {
            Zombie storage myZombie = zombies[_zombieId];
            Zombie storage enemyZombie = zombies[_targetId];
            uint rand = randMod(100);
            if (rand <= attackVictoryProbability) {
              myZombie.winCount = myZombie.winCount.add(1);
              myZombie.level = myZombie.level.add(1);
              enemyZombie.lossCount = enemyZombie.lossCount.add(1);
              feedAndMultiply(_zombieId, enemyZombie.dna, "zombie");
            } else {
              myZombie.lossCount = myZombie.lossCount.add(1);
              enemyZombie.winCount = enemyZombie.winCount.add(1);
              _triggerCooldown(myZombie);
            }
          }
        }
      "zombiehelper.sol": |
        pragma solidity ^0.4.25;

        import "./zombiefeeding.sol";

        contract ZombieHelper is ZombieFeeding {

          uint levelUpFee = 0.001 ether;

          modifier aboveLevel(uint _level, uint _zombieId) {
            require(zombies[_zombieId].level >= _level);
            _;
          }

          function withdraw() external onlyOwner {
            address _owner = owner();
            _owner.transfer(address(this).balance);
          }

          function setLevelUpFee(uint _fee) external onlyOwner {
            levelUpFee = _fee;
          }

          function levelUp(uint _zombieId) external payable {
            require(msg.value == levelUpFee);
            zombies[_zombieId].level = zombies[_zombieId].level.add(1);
          }

          function changeName(uint _zombieId, string _newName) external aboveLevel(2, _zombieId) onlyOwnerOf(_zombieId) {
            zombies[_zombieId].name = _newName;
          }

          function changeDna(uint _zombieId, uint _newDna) external aboveLevel(20, _zombieId) onlyOwnerOf(_zombieId) {
            zombies[_zombieId].dna = _newDna;
          }

          function getZombiesByOwner(address _owner) external view returns(uint[]) {
            uint[] memory result = new uint[](ownerZombieCount[_owner]);
            uint counter = 0;
            for (uint i = 0; i < zombies.length; i++) {
              if (zombieToOwner[i] == _owner) {
                result[counter] = i;
                counter++;
              }
            }
            return result;
          }
        }
      "zombiefeeding.sol": |
        pragma solidity ^0.4.25;

        import "./zombiefactory.sol";

        contract KittyInterface {
          function getKitty(uint256 _id) external view returns (
            bool isGestating,
            bool isReady,
            uint256 cooldownIndex,
            uint256 nextActionAt,
            uint256 siringWithId,
            uint256 birthTime,
            uint256 matronId,
            uint256 sireId,
            uint256 generation,
            uint256 genes
          );
        }

        contract ZombieFeeding is ZombieFactory {

          KittyInterface kittyContract;

          modifier onlyOwnerOf(uint _zombieId) {
            require(msg.sender == zombieToOwner[_zombieId]);
            _;
          }

          function setKittyContractAddress(address _address) external onlyOwner {
            kittyContract = KittyInterface(_address);
          }

          function _triggerCooldown(Zombie storage _zombie) internal {
            _zombie.readyTime = uint32(now + cooldownTime);
          }

          function _isReady(Zombie storage _zombie) internal view returns (bool) {
              return (_zombie.readyTime <= now);
          }

          function feedAndMultiply(uint _zombieId, uint _targetDna, string _species) internal onlyOwnerOf(_zombieId) {
            Zombie storage myZombie = zombies[_zombieId];
            require(_isReady(myZombie));
            _targetDna = _targetDna % dnaModulus;
            uint newDna = (myZombie.dna + _targetDna) / 2;
            if (keccak256(abi.encodePacked(_species)) == keccak256(abi.encodePacked("kitty"))) {
              newDna = newDna - newDna % 100 + 99;
            }
            _createZombie("NoName", newDna);
            _triggerCooldown(myZombie);
          }

          function feedOnKitty(uint _zombieId, uint _kittyId) public {
            uint kittyDna;
            (,,,,,,,,,kittyDna) = kittyContract.getKitty(_kittyId);
            feedAndMultiply(_zombieId, kittyDna, "kitty");
          }
        }
      "zombiefactory.sol": |
        pragma solidity ^0.4.25;

        import "./ownable.sol";
        import "./safemath.sol";

        contract ZombieFactory is Ownable {

          using SafeMath for uint256;
          using SafeMath32 for uint32;
          using SafeMath16 for uint16;

          event NewZombie(uint zombieId, string name, uint dna);

          uint dnaDigits = 16;
          uint dnaModulus = 10 ** dnaDigits;
          uint cooldownTime = 1 days;

          struct Zombie {
            string name;
            uint dna;
            uint32 level;
            uint32 readyTime;
            uint16 winCount;
            uint16 lossCount;
          }

          Zombie[] public zombies;

          mapping (uint => address) public zombieToOwner;
          mapping (address => uint) ownerZombieCount;

          function _createZombie(string _name, uint _dna) internal {
            uint id = zombies.push(Zombie(_name, _dna, 1, uint32(now + cooldownTime), 0, 0)) - 1;
            zombieToOwner[id] = msg.sender;
            ownerZombieCount[msg.sender] = ownerZombieCount[msg.sender].add(1);
            emit NewZombie(id, _name, _dna);
          }

          function _generatePseudoRandomDna(string _str) private view returns (uint) {
            uint rand = uint(keccak256(abi.encodePacked(_str)));
            return rand % dnaModulus;
          }

          function createPseudoRandomZombie(string _name) public {
            require(ownerZombieCount[msg.sender] == 0);
            uint randDna = _generatePseudoRandomDna(_name);
            randDna = randDna - randDna % 100;
            _createZombie(_name, randDna);
          }
        }
      "ownable.sol": |
        pragma solidity ^0.4.25;

        /**
        * @title Ownable
        * @dev Il contratto di proprietà ha un indirizzo del proprietario e fornisce funzioni di controllo
        * delle autorizzazioni di base, ciò semplifica l'implementazione delle "autorizzazioni dell'utente".
        */
        contract Ownable {
          address private _owner;

          event OwnershipTransferred(
            address indexed previousOwner,
            address indexed newOwner
          );

          /**
          * @dev Il costruttore di proprietà imposta il `proprietario` originale del contratto sull'account del mittente.
          */
          constructor() internal {
            _owner = msg.sender;
            emit OwnershipTransferred(address(0), _owner);
          }

          /**
          * @return the address of the owner.
          */
          function owner() public view returns(address) {
            return _owner;
          }

          /**
          * @dev Throws if called by any account other than the owner.
          */
          modifier onlyOwner() {
            require(isOwner());
            _;
          }

          /**
          * @return true if `msg.sender` is the owner of the contract.
          */
          function isOwner() public view returns(bool) {
            return msg.sender == _owner;
          }

          /**
          * @dev Allows the current owner to relinquish control of the contract.
          * @notice Renouncing to ownership will leave the contract without an owner.
          * It will not be possible to call the functions with the `onlyOwner`
          * modifier anymore.
          */
          function renounceOwnership() public onlyOwner {
            emit OwnershipTransferred(_owner, address(0));
            _owner = address(0);
          }

          /**
          * @dev Allows the current owner to transfer control of the contract to a newOwner.
          * @param newOwner The address to transfer ownership to.
          */
          function transferOwnership(address newOwner) public onlyOwner {
            _transferOwnership(newOwner);
          }

          /**
          * @dev Transfers control of the contract to a newOwner.
          * @param newOwner The address to transfer ownership to.
          */
          function _transferOwnership(address newOwner) internal {
            require(newOwner != address(0));
            emit OwnershipTransferred(_owner, newOwner);
            _owner = newOwner;
          }
        }
      "safemath.sol": |
        pragma solidity ^0.4.25;

        /**
         * @title SafeMath
         * @dev Math operations with safety checks that throw on error
         */
        library SafeMath {

          /**
          * @dev Multiplies two numbers, throws on overflow.
          */
          function mul(uint256 a, uint256 b) internal pure returns (uint256) {
            if (a == 0) {
              return 0;
            }
            uint256 c = a * b;
            assert(c / a == b);
            return c;
          }

          /**
          * @dev Integer division of two numbers, truncating the quotient.
          */
          function div(uint256 a, uint256 b) internal pure returns (uint256) {
            // assert(b > 0); // Solidity lo lancia automaticamente quando si divide per 0
            uint256 c = a / b;
            // assert(a == b * c + a % b); // Non esiste un caso in cui ciò non valga
            return c;
          }

          /**
          * @dev Subtracts two numbers, throws on overflow (i.e. if subtrahend is greater than minuend).
          */
          function sub(uint256 a, uint256 b) internal pure returns (uint256) {
            assert(b <= a);
            return a - b;
          }

          /**
          * @dev Adds two numbers, throws on overflow.
          */
          function add(uint256 a, uint256 b) internal pure returns (uint256) {
            uint256 c = a + b;
            assert(c >= a);
            return c;
          }
        }

        /**
         * @title SafeMath32
         * @dev SafeMath library implemented for uint32
         */
        library SafeMath32 {

          function mul(uint32 a, uint32 b) internal pure returns (uint32) {
            if (a == 0) {
              return 0;
            }
            uint32 c = a * b;
            assert(c / a == b);
            return c;
          }

          function div(uint32 a, uint32 b) internal pure returns (uint32) {
            // assert(b > 0); // Solidity lo lancia automaticamente quando si divide per 0
            uint32 c = a / b;
            // assert(a == b * c + a % b); // Non esiste un caso in cui ciò non valga
            return c;
          }

          function sub(uint32 a, uint32 b) internal pure returns (uint32) {
            assert(b <= a);
            return a - b;
          }

          function add(uint32 a, uint32 b) internal pure returns (uint32) {
            uint32 c = a + b;
            assert(c >= a);
            return c;
          }
        }

        /**
         * @title SafeMath16
         * @dev SafeMath library implemented for uint16
         */
        library SafeMath16 {

          function mul(uint16 a, uint16 b) internal pure returns (uint16) {
            if (a == 0) {
              return 0;
            }
            uint16 c = a * b;
            assert(c / a == b);
            return c;
          }

          function div(uint16 a, uint16 b) internal pure returns (uint16) {
            // assert(b > 0); // Solidity lo lancia automaticamente quando si divide per 0
            uint16 c = a / b;
            // assert(a == b * c + a % b); // Non esiste un caso in cui ciò non valga
            return c;
          }

          function sub(uint16 a, uint16 b) internal pure returns (uint16) {
            assert(b <= a);
            return a - b;
          }

          function add(uint16 a, uint16 b) internal pure returns (uint16) {
            uint16 c = a + b;
            assert(c >= a);
            return c;
          }
        }
      "erc721.sol": |
        pragma solidity ^0.4.25;

        contract ERC721 {
          event Transfer(address indexed _from, address indexed _to, uint256 indexed _tokenId);
          event Approval(address indexed _owner, address indexed _approved, uint256 indexed _tokenId);

          function balanceOf(address _owner) external view returns (uint256);
          function ownerOf(uint256 _tokenId) external view returns (address);
          function transferFrom(address _from, address _to, uint256 _tokenId) external payable;
          function approve(address _approved, uint256 _tokenId) external payable;
        }
    answer: |
      <!DOCTYPE html>
      <html lang="en">
        <head>
          <meta charset="UTF-8">
          <title>CryptoZombies front-end</title>
          <script language="javascript" type="text/javascript" src="https://cdnjs.cloudflare.com/ajax/libs/jquery/3.3.1/jquery.min.js"></script>
          <script language="javascript" type="text/javascript" src="web3.min.js"></script>
          <script language="javascript" type="text/javascript" src="cryptozombies_abi.js"></script>
        </head>
        <body>

          <script>
            var cryptoZombies;

            function startApp() {
              var cryptoZombiesAddress = "YOUR_CONTRACT_ADDRESS";
              cryptoZombies = new web3js.eth.Contract(cryptoZombiesABI, cryptoZombiesAddress);
            }

            function getZombieDetails(id) {
              return cryptoZombies.methods.zombies(id).call()
            }

            function zombieToOwner(id) {
              return cryptoZombies.methods.zombieToOwner(id).call()
            }

            function getZombiesByOwner(owner) {
              return cryptoZombies.methods.getZombiesByOwner(owner).call()
            }

            window.addEventListener('load', function() {

              // Verifica se Web3 è stato iniettato dal browser (Mist/MetaMask)
              if (typeof web3 !== 'undefined') {
                // Usa il provider Mist/MetaMask
                web3js = new Web3(web3.currentProvider);
              } else {
                // Gestire il caso in cui l'utente non ha installato Metamask
                // Probabilmente mostrare loro un messaggio che richiede di installare Metamask
              }

              // Ora puoi avviare la tua app e accedere a web3 liberamente:
              startApp()

            })
          </script>
        </body>
      </html>
---

Il nostro contratto è pronto! Ora possiamo usare Web3.js per parlarci.

Web3.js ha due metodi che possiamo usare per chiamare le funzioni nel nostro contratto: `call` e `send`.

### Call

`call` è usato per le funzioni `view` e `pure`. Funziona solo sul nodo locale e non creerà una transazione sulla blockchain.

> **Revisione:** le funzioni `view` e `pure` sono di sola lettura e non cambiano stato sulla blockchain. Inoltre non costano alcun gas e all'utente non verrà richiesto di firmare una transazione con MetaMask.

Usando Web3.js dovresti usare `call` per una funzione chiamata `myMethod` con il parametro `123` come segue:

```
myContract.methods.myMethod(123).call()
```

### Send

`send` creerà una transazione e cambierà i dati sulla blockchain. Dovrai usare `send` per tutte le funzioni che non sono `view` o `pure`.

> **Nota:** il `send` (invio) di una transazione richiederà all'utente di pagare il gas e farà apparire Metamask che chiederà di firmare la transazione stessa. Quando usiamo Metamask come nostro provider web3, tutto questo sarà automatico e non abbiamo bisogno di fare nulla di speciale nel nostro codice. Abbastanza bello!

Usando Web3.js dovresti inviare una transazione che richiama la funzione chiamata `myMethod` con il parametro `123` come segue:

```
myContract.methods.myMethod(123).send()
```

La sintassi è quasi identica a `call()`.

## Ottenere i Dati degli zombi

Ora diamo un'occhiata ad un esempio reale dell'utilizzo di `call` per accedere ai dati del nostro contratto.

Ricordiamoci che abbiamo reso la nostra serie di zombi `public`:

```
Zombie[] public zombies;
```

In Solidity quando dichiari una variabile `public`, si creerà automaticamente una funzione pubblica "getter" con lo stesso nome. Quindi se volessi cercare lo zombi con id `15`, lo richiameresti come se fosse una funzione: `zombies(15)`.

Ecco come scriveremo una funzione JavaScript nel nostro front-end per richiede uno zombi tramite il suo ID, farà una query (richiesta) sul nostro contratto per quello zombi e ne restituirà il risultato:

> Nota: tutti gli esempi di codice che utilizziamo in questa lezione utilizzano la **versione 1.0** di Web3.js, che utilizza promises anziché callbacks. Molti altri tutorial che vedrai online utilizzano una versione precedente di Web3.js. La sintassi è cambiata molto con la versione 1.0, quindi se stai copiando il codice da altri tutorial, assicurati che stiano usando la tua stessa versione!

```
function getZombieDetails(id) {
  return cryptoZombies.methods.zombies(id).call()
}

// Chiama la funzione e fai qualcosa con il risultato:
getZombieDetails(15)
.then(function(result) {
  console.log("Zombie 15: " + JSON.stringify(result));
});
```

Vediamo cosa sta succedendo qui.

`cryptoZombies.methods.zombies(id).call()` comunicherà con il nodo del provider Web3 e gli dirà di restituire lo zombi con indice `id` da `Zombie[] public zombies` del nostro contratto.

Si noti che questo è **asincrono**, come una chiamata API a un server esterno. Quindi Web3 restituisce una promise (promessa) qui. (Se non hai familiarità con le promise di JavaScript... È tempo di fare alcuni esercizi prima di continuare!)

Una volta che la promise si risolve (il che significa che abbiamo ricevuto una risposta dal provider web3), il nostro codice di esempio continua con l'istruzione `then` che registra `result` sulla console.

`result` sarà un oggetto JavaScript che assomiglia a questo:

```
{
  "name": "H4XF13LD MORRIS'S COOLER OLDER BROTHER",
  "dna": "1337133713371337",
  "level": "9999",
  "readyTime": "1522498671",
  "winCount": "999999999",
  "lossCount": "0" // Obviously.
}
```

Potremmo quindi avere una logica di front-end per analizzare questo oggetto e visualizzarlo in modo significativo sul front-end.

## Facciamo una prova

Siamo andati avanti ed abbiamo copiato `getZombieDetails` nel codice per te.

1. Creiamo una funzione simile per `zombieToOwner`. Se ricordi da `ZombieFactory.sol`, avevamo una mappatura simile a:

  ```
  mapping (uint => address) public zombieToOwner;
  ```

  Definisci una funzione JavaScript chiamata `zombieToOwner`. Simile a `getZombieDetails` presente sopra, prenderà un `id` come parametro e restituirà (return) una `call` Web3.js a `zombieToOwner` sul nostro contratto.

2. Sotto, crea una terza funzione per `getZombiesByOwner`. Se ricordi da `ZombieHelper.sol`, la definizione della funzione appariva così:

  ```
  function getZombiesByOwner(address _owner)
  ```

  La nostra funzione `getZombiesByOwner` prenderà `owner` come parametro e restituirà (return) una `call` Web3.js a `getZombiesByOwner`.
