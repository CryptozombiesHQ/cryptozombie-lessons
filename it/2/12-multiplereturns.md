---
title: Gestire valori di ritorno multipli
actions: ['checkAnswer', 'hints']
material:
  editor:
    language: sol
    startingCode:
      "zombiefeeding.sol": |
        pragma solidity ^0.4.25;

        import "./zombiefactory.sol";

        contract KittyInterface {
          function getKitty(uint256 _id) external view returns (
            bool isGestating,
            bool isReady,
            uint256 cooldownIndex,
            uint256 nextActionAt,
            uint256 siringWithId,
            uint256 birthTime,
            uint256 matronId,
            uint256 sireId,
            uint256 generation,
            uint256 genes
          );
        }

        contract ZombieFeeding is ZombieFactory {

          address ckAddress = 0x06012c8cf97BEaD5deAe237070F9587f8E7A266d;
          KittyInterface kittyContract = KittyInterface(ckAddress);

          function feedAndMultiply(uint _zombieId, uint _targetDna) public {
            require(msg.sender == zombieToOwner[_zombieId]);
            Zombie storage myZombie = zombies[_zombieId];
            _targetDna = _targetDna % dnaModulus;
            uint newDna = (myZombie.dna + _targetDna) / 2;
            _createZombie("NoName", newDna);
          }

          // definisci qui la funzione

        }
      "zombiefactory.sol": |
        pragma solidity ^0.4.25;

        contract ZombieFactory {

            event NewZombie(uint zombieId, string name, uint dna);

            uint dnaDigits = 16;
            uint dnaModulus = 10 ** dnaDigits;

            struct Zombie {
                string name;
                uint dna;
            }

            Zombie[] public zombies;

            mapping (uint => address) public zombieToOwner;
            mapping (address => uint) ownerZombieCount;

            function _createZombie(string _name, uint _dna) internal {
                uint id = zombies.push(Zombie(_name, _dna)) - 1;
                zombieToOwner[id] = msg.sender;
                ownerZombieCount[msg.sender]++;
                emit NewZombie(id, _name, _dna);
            }

            function _generateRandomDna(string _str) private view returns (uint) {
                uint rand = uint(keccak256(abi.encodePacked(_str)));
                return rand % dnaModulus;
            }

            function createRandomZombie(string _name) public {
                require(ownerZombieCount[msg.sender] == 0);
                uint randDna = _generateRandomDna(_name);
                _createZombie(_name, randDna);
            }

        }
    answer: >
      pragma solidity ^0.4.25;

      import "./zombiefactory.sol";

      contract KittyInterface {
        function getKitty(uint256 _id) external view returns (
          bool isGestating,
          bool isReady,
          uint256 cooldownIndex,
          uint256 nextActionAt,
          uint256 siringWithId,
          uint256 birthTime,
          uint256 matronId,
          uint256 sireId,
          uint256 generation,
          uint256 genes
        );
      }

      contract ZombieFeeding is ZombieFactory {

        address ckAddress = 0x06012c8cf97BEaD5deAe237070F9587f8E7A266d;
        KittyInterface kittyContract = KittyInterface(ckAddress);

        function feedAndMultiply(uint _zombieId, uint _targetDna) public {
          require(msg.sender == zombieToOwner[_zombieId]);
          Zombie storage myZombie = zombies[_zombieId];
          _targetDna = _targetDna % dnaModulus;
          uint newDna = (myZombie.dna + _targetDna) / 2;
          _createZombie("NoName", newDna);
        }

        function feedOnKitty(uint _zombieId, uint _kittyId) public {
          uint kittyDna;
          (,,,,,,,,,kittyDna) = kittyContract.getKitty(_kittyId);
          feedAndMultiply(_zombieId, kittyDna);
        }

      }
---

La funzione `getKitty` è il primo esempio che abbiamo visto che restituisce più valori. Diamo un'occhiata su come gestirli:

```
function multipleReturns() internal returns(uint a, uint b, uint c) {
  return (1, 2, 3);
}

function processMultipleReturns() external {
  uint a;
  uint b;
  uint c;
  // Ecco come fare assegnazioni multiple:
  (a, b, c) = multipleReturns();
}

// O se ci preoccupassimo solo di uno dei valori:
function getLastReturnValue() external {
  uint c;
  // Possiamo semplicemente lasciare vuoti gli altri campi:
  (,,c) = multipleReturns();
}
```

# Facciamo una prova

È ora di interagire con il contratto CryptoKitties!

Facciamo una funzione che ottiene i geni del gattino dal contratto:

1. Crea una funzione chiamata `feedOnKitty`. Ci vorranno 2 parametri `uint`: `_zombieId` e `_kittyId`, e dovrebbe essere una funzione `public`.

2. La funzione dovrebbe prima dichiarare un `uint` chiamato `kittyDna`.

> Nota: Nel nostro `KittyInterface`, `genes` è un `uint256` — ma se ricordi alla lezione 1, `uint` è un alias per `uint256` — sono la stessa cosa.

3. La funzione dovrebbe quindi richiamare la funzione `kittyContract.getKitty` con `_kittyId` e memorizzare `genes` dentro `kittyDna`. Ricorda: `getKitty` restituisce un sacco di variabili. (10 per l'esattezza, sono gentile, li ho contati per te!). Ma tutto ciò che ci interessa è l'ultimo, i `genes`. Conta molto attentamente le virgole!

4. Infine la funzione dovrebbe richiamare `feedAndMultiply` e passare sia `_zombieId` che `kittyDna`.
