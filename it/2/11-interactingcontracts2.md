---
title: Utilizzare un'interfaccia
actions: ['checkAnswer', 'hints']
material:
  editor:
    language: sol
    startingCode:
      "zombiefeeding.sol": |
        pragma solidity ^0.4.25;

        import "./zombiefactory.sol";

        contract KittyInterface {
          function getKitty(uint256 _id) external view returns (
            bool isGestating,
            bool isReady,
            uint256 cooldownIndex,
            uint256 nextActionAt,
            uint256 siringWithId,
            uint256 birthTime,
            uint256 matronId,
            uint256 sireId,
            uint256 generation,
            uint256 genes
          );
        }

        contract ZombieFeeding is ZombieFactory {

          address ckAddress = 0x06012c8cf97BEaD5deAe237070F9587f8E7A266d;
          // Inizializza kittyContract qui usando `ckAddress` da sopra

          function feedAndMultiply(uint _zombieId, uint _targetDna) public {
            require(msg.sender == zombieToOwner[_zombieId]);
            Zombie storage myZombie = zombies[_zombieId];
            _targetDna = _targetDna % dnaModulus;
            uint newDna = (myZombie.dna + _targetDna) / 2;
            _createZombie("NoName", newDna);
          }

        }
      "zombiefactory.sol": |
        pragma solidity ^0.4.25;

        contract ZombieFactory {

            event NewZombie(uint zombieId, string name, uint dna);

            uint dnaDigits = 16;
            uint dnaModulus = 10 ** dnaDigits;

            struct Zombie {
                string name;
                uint dna;
            }

            Zombie[] public zombies;

            mapping (uint => address) public zombieToOwner;
            mapping (address => uint) ownerZombieCount;

            function _createZombie(string _name, uint _dna) internal {
                uint id = zombies.push(Zombie(_name, _dna)) - 1;
                zombieToOwner[id] = msg.sender;
                ownerZombieCount[msg.sender]++;
                emit NewZombie(id, _name, _dna);
            }

            function _generatePseudoRandomDna(string _str) private view returns (uint) {
                uint rand = uint(keccak256(abi.encodePacked(_str)));
                return rand % dnaModulus;
            }

            function createPseudoRandomZombie(string _name) public {
                require(ownerZombieCount[msg.sender] == 0);
                uint randDna = _generatePseudoRandomDna(_name);
                _createZombie(_name, randDna);
            }

        }
    answer: >
      pragma solidity ^0.4.25;

      import "./zombiefactory.sol";

      contract KittyInterface {
        function getKitty(uint256 _id) external view returns (
          bool isGestating,
          bool isReady,
          uint256 cooldownIndex,
          uint256 nextActionAt,
          uint256 siringWithId,
          uint256 birthTime,
          uint256 matronId,
          uint256 sireId,
          uint256 generation,
          uint256 genes
        );
      }

      contract ZombieFeeding is ZombieFactory {

        address ckAddress = 0x06012c8cf97BEaD5deAe237070F9587f8E7A266d;
        KittyInterface kittyContract = KittyInterface(ckAddress);

        function feedAndMultiply(uint _zombieId, uint _targetDna) public {
          require(msg.sender == zombieToOwner[_zombieId]);
          Zombie storage myZombie = zombies[_zombieId];
          _targetDna = _targetDna % dnaModulus;
          uint newDna = (myZombie.dna + _targetDna) / 2;
          _createZombie("NoName", newDna);
        }

      }
---

Continuando il nostro esempio precedente con `NumberInterface`, una volta definita l'interfaccia come:

```
contract NumberInterface {
  function getNum(address _myAddress) public view returns (uint);
}
```

Possiamo usarlo in un contratto come segue:

```
contract MyContract {
  address NumberInterfaceAddress = 0xab38... 
  // ^ L'indirizzo del contratto FavoriteNumber su Ethereum
  NumberInterface numberContract = NumberInterface(NumberInterfaceAddress);
  // Ora `numberContract` punta all'altro contratto

  function someFunction() public {
    // Ora possiamo chiamare `getNum` da quel contratto:
    uint num = numberContract.getNum(msg.sender);
    // ...e fai qualcosa con `num` qui
  }
}
```

In questo modo, il tuo contratto può interagire con qualsiasi altro contratto sulla blockchain di Ethereum, purché espongano tali funzioni come `public` o `external`.

# Facciamo una prova

Impostiamo il nostro contratto per leggere dal contratto intelligente di CryptoKitties!

1. Ho salvato l'indirizzo del contratto CryptoKitties nel codice per te, sotto una variabile chiamata `ckAddress`. Nella riga successiva crea un `KittyInterface` chiamato `kittyContract`, ed inizializzalo con `ckAddress` — proprio come abbiamo fatto con `numberContract` sopra.
