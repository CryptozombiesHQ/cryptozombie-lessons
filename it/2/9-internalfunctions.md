---
title: Altro sulla Visibilità delle Funzioni
actions: ['checkAnswer', 'hints']
material:
  editor:
    language: sol
    startingCode:
      "zombiefactory.sol": |
        pragma solidity ^0.4.25;

        contract ZombieFactory {

            event NewZombie(uint zombieId, string name, uint dna);

            uint dnaDigits = 16;
            uint dnaModulus = 10 ** dnaDigits;

            struct Zombie {
                string name;
                uint dna;
            }

            Zombie[] public zombies;

            mapping (uint => address) public zombieToOwner;
            mapping (address => uint) ownerZombieCount;

            // modifica la definizione della funzione di seguito
            function _createZombie(string _name, uint _dna) private {
                uint id = zombies.push(Zombie(_name, _dna)) - 1;
                zombieToOwner[id] = msg.sender;
                ownerZombieCount[msg.sender]++;
                emit NewZombie(id, _name, _dna);
            }

            function _generatePseudoRandomDna(string _str) private view returns (uint) {
                uint rand = uint(keccak256(abi.encodePacked(_str)));
                return rand % dnaModulus;
            }

            function createPseudoRandomZombie(string _name) public {
                require(ownerZombieCount[msg.sender] == 0);
                uint randDna = _generatePseudoRandomDna(_name);
                _createZombie(_name, randDna);
            }

        }
      "zombiefeeding.sol": |
        pragma solidity ^0.4.25;

        import "./zombiefactory.sol";

        contract ZombieFeeding is ZombieFactory {

          function feedAndMultiply(uint _zombieId, uint _targetDna) public {
            require(msg.sender == zombieToOwner[_zombieId]);
            Zombie storage myZombie = zombies[_zombieId];
            _targetDna = _targetDna % dnaModulus;
            uint newDna = (myZombie.dna + _targetDna) / 2;
            _createZombie("NoName", newDna);
          }

        }
    answer: >
      pragma solidity ^0.4.25;

      contract ZombieFactory {

          event NewZombie(uint zombieId, string name, uint dna);

          uint dnaDigits = 16;
          uint dnaModulus = 10 ** dnaDigits;

          struct Zombie {
              string name;
              uint dna;
          }

          Zombie[] public zombies;

          mapping (uint => address) public zombieToOwner;
          mapping (address => uint) ownerZombieCount;

          function _createZombie(string _name, uint _dna) internal {
              uint id = zombies.push(Zombie(_name, _dna)) - 1;
              zombieToOwner[id] = msg.sender;
              ownerZombieCount[msg.sender]++;
              emit NewZombie(id, _name, _dna);
          }

          function _generatePseudoRandomDna(string _str) private view returns (uint) {
              uint rand = uint(keccak256(abi.encodePacked(_str)));
              return rand % dnaModulus;
          }

          function createPseudoRandomZombie(string _name) public {
              require(ownerZombieCount[msg.sender] == 0);
              uint randDna = _generatePseudoRandomDna(_name);
              _createZombie(_name, randDna);
          }

      }
---

**Il codice nella nostra lezione precedente ha un errore!**

Se provi a compilarlo il compilatore genererà un errore.

Il problema è che abbiamo provato a chiamare la funzione `_createZombie` all'interno di `ZombieFeeding` ma `_createZombie` è una funzione `private` all'interno di `ZombieFactory`. Ciò significa che nessuno dei contratti ereditati da `ZombieFactory` può accedervi.

## Interno ed Esterno

Oltre a `public` e `private`, Solidity ha altri due tipi di visibilità per le funzioni: `internal` ed `external`.

`internal` equivale a `private`, tranne che è anche accessibile ai contratti che ereditano da questo contratto. **(Ehi, sembra proprio quello che vogliamo qui!)**.

`external` è simile a `public`, tranne che queste funzioni possono essere chiamate SOLO al di fuori del contratto — non possono essere chiamate da altre funzioni all'interno di quel contratto. Parleremo del motivo per cui potresti voler usare `external` o `public` in seguito.

Per dichiarare le funzioni `internal` o `external`, la sintassi è la stessa di `private` e `public`:

```
contract Sandwich {
  uint private sandwichesEaten = 0;

  function eat() internal {
    sandwichesEaten++;
  }
}

contract BLT is Sandwich {
  uint private baconSandwichesEaten = 0;

  function eatWithBacon() public returns (string) {
    baconSandwichesEaten++;
    // Possiamo chiamarlo qui perché è interno
    eat();
  }
}
```

# Facciamo una prova

1. Cambia `_createZombie()` da `private` ad `internal` in modo che il nostro altro contratto possa accedervi.

Ti abbiamo già riportato alla scheda corretta `zombiefactory.sol`.
