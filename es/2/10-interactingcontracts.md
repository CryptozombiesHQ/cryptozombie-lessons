---
title: ¬øQu√© Comen Los Zombis?
actions: ['checkAnswer', 'hints']
material:
  editor:
    language: sol
    startingCode:
      "zombiefeeding.sol": |
        pragma solidity ^0.4.25;

        import "./zombiefactory.sol";

        // Crea la KittyInterface aqu√≠

        contract ZombieFeeding is ZombieFactory {

          function feedAndMultiply(uint _zombieId, uint _targetDna) public {
            require(msg.sender == zombieToOwner[_zombieId]);
            Zombie storage myZombie = zombies[_zombieId];
            _targetDna = _targetDna % dnaModulus;
            uint newDna = (myZombie.dna + _targetDna) / 2;
            _createZombie("NoName", newDna);
          }

        }
      "zombiefactory.sol": |
        pragma solidity ^0.4.25;

        contract ZombieFactory {

            event NewZombie(uint zombieId, string name, uint dna);

            uint dnaDigits = 16;
            uint dnaModulus = 10 ** dnaDigits;

            struct Zombie {
                string name;
                uint dna;
            }

            Zombie[] public zombies;

            mapping (uint => address) public zombieToOwner;
            mapping (address => uint) ownerZombieCount;

            function _createZombie(string _name, uint _dna) internal {
                uint id = zombies.push(Zombie(_name, _dna)) - 1;
                zombieToOwner[id] = msg.sender;
                ownerZombieCount[msg.sender]++;
                emit NewZombie(id, _name, _dna);
            }

            function _generateRandomDna(string _str) private view returns (uint) {
                uint rand = uint(keccak256(abi.encodePacked(_str)));
                return rand % dnaModulus;
            }

            function createRandomZombie(string _name) public {
                require(ownerZombieCount[msg.sender] == 0);
                uint randDna = _generateRandomDna(_name);
                _createZombie(_name, randDna);
            }

        }
    answer: >
      pragma solidity ^0.4.25;

      import "./zombiefactory.sol";

      contract KittyInterface {
        function getKitty(uint256 _id) external view returns (
          bool isGestating,
          bool isReady,
          uint256 cooldownIndex,
          uint256 nextActionAt,
          uint256 siringWithId,
          uint256 birthTime,
          uint256 matronId,
          uint256 sireId,
          uint256 generation,
          uint256 genes
        );
      }

      contract ZombieFeeding is ZombieFactory {

        function feedAndMultiply(uint _zombieId, uint _targetDna) public {
          require(msg.sender == zombieToOwner[_zombieId]);
          Zombie storage myZombie = zombies[_zombieId];
          _targetDna = _targetDna % dnaModulus;
          uint newDna = (myZombie.dna + _targetDna) / 2;
          _createZombie("NoName", newDna);
        }

      }
---

¬°Es hora de alimentar a nuestros zombis! Y ¬øQu√© es lo que m√°s les gusta comer?

Bueno lo que pasa es que a los CryptoZombies lo que les encanta comer es...

**¬°CryptoKitties!** üò±üò±üò±

(S√≠, es enserio üòÜ )

Para hacer esto necesitamos leer el kittyDna del contrato inteligente CryptoKitties. Podemos hacer eso debido a que los datos de los CryptoKitties guardados en la blockchain son p√∫blicos. ¬°¬øNo es blockchain genial?!

No te preocupes - nuestro juego a√∫n no va a hacer da√±or a ning√∫n CryptoKitty. Solo vamos a *leer* los datos de los CryptoKitties, no podemos borrarlos üòâ

## Interactuando con otros contratos

Para que nuestro contrato pueda hablar a otro contrato de la blockchain que no poseemos, necesitamos definir una **_interfaz_**.

Vamos a ver un simple ejemplo. Digamos que hay un contrato en la blockchain tal que as√≠:

```
contract LuckyNumber {
  mapping(address => uint) numbers;

  function setNum(uint _num) public {
    numbers[msg.sender] = _num;
  }

  function getNum(address _myAddress) public view returns (uint) {
    return numbers[_myAddress];
  }
}
```

Este seria un simple contrato donde cualquiera puede guardar su n√∫mero de la suerte, y este estar√° asociado a su direcci√≥n de Ethereum. De esta forma cualquiera podr√≠a ver el n√∫mero de la suerte de una persona usando su direcci√≥n.

Ahora digamos que tenemos un contrato externo que quiere leer la informaci√≥n de este contrato usando la funci√≥n `getNum`. 

Primero debemos usar una **_interfaz_** del contrato `LuckyNumber`:

```
contract NumberInterface {
  function getNum(address _myAddress) public view returns (uint);
}
```

Ten en cuenta que esto se asemeja a definir un contrato, con alguna diferencia. Primero, solo declaramos las funciones con las que queremos interactuar - en este caso `getNum` ‚Äî¬†y no mencionamos ninguna otra funci√≥n o variables de estado.

Segundo, no definimos el cuerpo de la funci√≥n. En vez de usar las llaves (`{` y `}`), solamente terminaremos la funci√≥n a√±adiendo un punto y coma al final de la declaraci√≥n (`;`).

Ser√≠a como definir el esqueleto del contrato. As√≠ es como conoce el compilador a las interfaces.

Incluyendo esta interfaz en el c√≥digo de tu dapp nuestro contrato sabe como son las funciones de otro contrato, como llamarlas, y que tipo de respuesta recibiremos.

Entraremos en como llamar a las funciones de otros contratos en la siguiente lecci√≥n, por ahora vamos a declarar nuestra interfaz para el contrato de CryptoKitties.

# Vamos a probarlo

Hemos mirado el c√≥digo fuente de CryptoKitties por t√≠, y hemos encontrado una funci√≥n llamada `getKitty` que devuelve todos los datos de un kitty, incluyendo sus "genes" (¬°qu√© es lo que nuestro juego de zombis necesita para crear un nuevo zombi!).

La funci√≥n es as√≠:

```
function getKitty(uint256 _id) external view returns (
    bool isGestating,
    bool isReady,
    uint256 cooldownIndex,
    uint256 nextActionAt,
    uint256 siringWithId,
    uint256 birthTime,
    uint256 matronId,
    uint256 sireId,
    uint256 generation,
    uint256 genes
) {
    Kitty storage kit = kitties[_id];

    // si esta variable es 0 entonces no se esta gestando
    isGestating = (kit.siringWithId != 0);
    isReady = (kit.cooldownEndBlock <= block.number);
    cooldownIndex = uint256(kit.cooldownIndex);
    nextActionAt = uint256(kit.cooldownEndBlock);
    siringWithId = uint256(kit.siringWithId);
    birthTime = uint256(kit.birthTime);
    matronId = uint256(kit.matronId);
    sireId = uint256(kit.sireId);
    generation = uint256(kit.generation);
    genes = kit.genes;
}
```

La funci√≥n parece algo diferente de las que hemos usado. Puedes ver que devuelve... una lista de diferentes valores. Si vienes de un lenguaje de programaci√≥n como JavaScript, esto es diferente - en Solidity puedes devolver m√°s de un valor en una funci√≥n.

Ahora que sabemos como es esta funci√≥n, podemos usarla para crear una interfaz:

1. Define una interfaz llamada `KittyInterface`. Recuerda, es como crear un nuevo contrato - usamos la palabra clave `contract`.

2. Dentro de la interfaz, define la funci√≥n `getKitty` (que deber√≠a ser un copia/pega de la funci√≥n de arriba, pero con un punto y coma despu√©s de los par√°metros, en vez de todo lo que hay dentro de las llaves.
