---
title: トランザクションの送信
actions: ['答え合わせ', 'ヒント']
requireLogin: true
material:
  editor:
    language: html
    startingCode:
      "index.html": |
        <!DOCTYPE html>
        <html lang="en">
          <head>
            <meta charset="UTF-8">
            <title>CryptoZombies front-end</title>
            <script language="javascript" type="text/javascript" src="https://cdnjs.cloudflare.com/ajax/libs/jquery/3.3.1/jquery.min.js"></script>
            <script language="javascript" type="text/javascript" src="web3.min.js"></script>
            <script language="javascript" type="text/javascript" src="cryptozombies_abi.js"></script>
          </head>
          <body>
            <div id="txStatus"></div>
            <div id="zombies"></div>

            <script>
              var cryptoZombies;
              var userAccount;

              function startApp() {
                var cryptoZombiesAddress = "YOUR_CONTRACT_ADDRESS";
                cryptoZombies = new web3js.eth.Contract(cryptoZombiesABI, cryptoZombiesAddress);

                var accountInterval = setInterval(function() {
                  // アカウントが変更されているかチェック
                  if (web3.eth.accounts[0] !== userAccount) {
                    userAccount = web3.eth.accounts[0];
                    // 新アカウントでUIをアップデートするように関数コール
                    updateInterface();
                  }
                }, 100);
              }

              function displayZombies(ids) {
                $("#zombies").empty();
                for (id of ids) {
                  // コントラクトからゾンビ詳細を探し、`zombie`オブジェクトを返す。
                  getZombieDetails(id)
                  .then(function(zombie) {
                    // ES6の「テンプレート文字列」を使い、HTMLに変数をインジェクト。
                    // それぞれを #zombies div に追加
                    $("#zombies").append(`<div class="zombie">
                      <ul>
                        <li>Name: ${zombie.name}</li>
                        <li>DNA: ${zombie.dna}</li>
                        <li>Level: ${zombie.level}</li>
                        <li>Wins: ${zombie.winCount}</li>
                        <li>Losses: ${zombie.lossCount}</li>
                        <li>Ready Time: ${zombie.readyTime}</li>
                      </ul>
                    </div>`);
                  });
                }
              }

              // ここから始めよ

              function getZombieDetails(id) {
                return cryptoZombies.methods.zombies(id).call();
              }

              function zombieToOwner(id) {
                return cryptoZombies.methods.zombieToOwner(id).call();
              }

              function getZombiesByOwner(owner) {
                return cryptoZombies.methods.getZombiesByOwner(owner).call();
              }

              window.addEventListener('load', function() {

                // Web3がブラウザにインジェクトされているかチェック (Mist/MetaMask)
                if (typeof web3 !== 'undefined') {
                  // Mist/MetaMaskのプロバイダの使用
                  web3js = new Web3(web3.currentProvider);
                } else {
                  // ユーザーがweb3を持たない場合の対処。
                  // アプリを使用するためにMetamaskをインストールするよう
                  // 伝えるメッセージを表示。
                }

                // アプリのスタート＆Web3.jsへの自由なアクセスが可能に:
                startApp()
              })
            </script>
          </body>
        </html>
      "zombieownership.sol": |
        pragma solidity ^0.4.19;

        import "./zombieattack.sol";
        import "./erc721.sol";
        import "./safemath.sol";

        contract ZombieOwnership is ZombieAttack, ERC721 {

          using SafeMath for uint256;

          mapping (uint => address) zombieApprovals;

          function balanceOf(address _owner) public view returns (uint256 _balance) {
            return ownerZombieCount[_owner];
          }

          function ownerOf(uint256 _tokenId) public view returns (address _owner) {
            return zombieToOwner[_tokenId];
          }

          function _transfer(address _from, address _to, uint256 _tokenId) private {
            ownerZombieCount[_to] = ownerZombieCount[_to].add(1);
            ownerZombieCount[msg.sender] = ownerZombieCount[msg.sender].sub(1);
            zombieToOwner[_tokenId] = _to;
            Transfer(_from, _to, _tokenId);
          }

          function transfer(address _to, uint256 _tokenId) public onlyOwnerOf(_tokenId) {
            _transfer(msg.sender, _to, _tokenId);
          }

          function approve(address _to, uint256 _tokenId) public onlyOwnerOf(_tokenId) {
            zombieApprovals[_tokenId] = _to;
            Approval(msg.sender, _to, _tokenId);
          }

          function takeOwnership(uint256 _tokenId) public {
            require(zombieApprovals[_tokenId] == msg.sender);
            address owner = ownerOf(_tokenId);
            _transfer(owner, msg.sender, _tokenId);
          }
        }
      "zombieattack.sol": |
        pragma solidity ^0.4.19;

        import "./zombiehelper.sol";

        contract ZombieAttack is ZombieHelper {
          uint randNonce = 0;
          uint attackVictoryProbability = 70;

          function randMod(uint _modulus) internal returns(uint) {
            randNonce++;
            return uint(keccak256(now, msg.sender, randNonce)) % _modulus;
          }

          function attack(uint _zombieId, uint _targetId) external onlyOwnerOf(_zombieId) {
            Zombie storage myZombie = zombies[_zombieId];
            Zombie storage enemyZombie = zombies[_targetId];
            uint rand = randMod(100);
            if (rand <= attackVictoryProbability) {
              myZombie.winCount++;
              myZombie.level++;
              enemyZombie.lossCount++;
              feedAndMultiply(_zombieId, enemyZombie.dna, "zombie");
            } else {
              myZombie.lossCount++;
              enemyZombie.winCount++;
              _triggerCooldown(myZombie);
            }
          }
        }
      "zombiehelper.sol": |
        pragma solidity ^0.4.19;

        import "./zombiefeeding.sol";

        contract ZombieHelper is ZombieFeeding {

          uint levelUpFee = 0.001 ether;

          modifier aboveLevel(uint _level, uint _zombieId) {
            require(zombies[_zombieId].level >= _level);
            _;
          }

          function withdraw() external onlyOwner {
            owner.transfer(this.balance);
          }

          function setLevelUpFee(uint _fee) external onlyOwner {
            levelUpFee = _fee;
          }

          function levelUp(uint _zombieId) external payable {
            require(msg.value == levelUpFee);
            zombies[_zombieId].level++;
          }

          function changeName(uint _zombieId, string _newName) external aboveLevel(2, _zombieId) onlyOwnerOf(_zombieId) {
            zombies[_zombieId].name = _newName;
          }

          function changeDna(uint _zombieId, uint _newDna) external aboveLevel(20, _zombieId) onlyOwnerOf(_zombieId) {
            zombies[_zombieId].dna = _newDna;
          }

          function getZombiesByOwner(address _owner) external view returns(uint[]) {
            uint[] memory result = new uint[](ownerZombieCount[_owner]);
            uint counter = 0;
            for (uint i = 0; i < zombies.length; i++) {
              if (zombieToOwner[i] == _owner) {
                result[counter] = i;
                counter++;
              }
            }
            return result;
          }

        }
      "zombiefeeding.sol": |
        pragma solidity ^0.4.19;

        import "./zombiefactory.sol";

        contract KittyInterface {
          function getKitty(uint256 _id) external view returns (
            bool isGestating,
            bool isReady,
            uint256 cooldownIndex,
            uint256 nextActionAt,
            uint256 siringWithId,
            uint256 birthTime,
            uint256 matronId,
            uint256 sireId,
            uint256 generation,
            uint256 genes
          );
        }

        contract ZombieFeeding is ZombieFactory {

          KittyInterface kittyContract;

          modifier onlyOwnerOf(uint _zombieId) {
            require(msg.sender == zombieToOwner[_zombieId]);
            _;
          }

          function setKittyContractAddress(address _address) external onlyOwner {
            kittyContract = KittyInterface(_address);
          }

          function _triggerCooldown(Zombie storage _zombie) internal {
            _zombie.readyTime = uint32(now + cooldownTime);
          }

          function _isReady(Zombie storage _zombie) internal view returns (bool) {
              return (_zombie.readyTime <= now);
          }

          function feedAndMultiply(uint _zombieId, uint _targetDna, string _species) internal onlyOwnerOf(_zombieId) {
            Zombie storage myZombie = zombies[_zombieId];
            require(_isReady(myZombie));
            _targetDna = _targetDna % dnaModulus;
            uint newDna = (myZombie.dna + _targetDna) / 2;
            if (keccak256(_species) == keccak256("kitty")) {
              newDna = newDna - newDna % 100 + 99;
            }
            _createZombie("NoName", newDna);
            _triggerCooldown(myZombie);
          }

          function feedOnKitty(uint _zombieId, uint _kittyId) public {
            uint kittyDna;
            (,,,,,,,,,kittyDna) = kittyContract.getKitty(_kittyId);
            feedAndMultiply(_zombieId, kittyDna, "kitty");
          }
        }
      "zombiefactory.sol": |
        pragma solidity ^0.4.19;

        import "./ownable.sol";
        import "./safemath.sol";

        contract ZombieFactory is Ownable {

          using SafeMath for uint256;

          event NewZombie(uint zombieId, string name, uint dna);

          uint dnaDigits = 16;
          uint dnaModulus = 10 ** dnaDigits;
          uint cooldownTime = 1 days;

          struct Zombie {
            string name;
            uint dna;
            uint32 level;
            uint32 readyTime;
            uint16 winCount;
            uint16 lossCount;
          }

          Zombie[] public zombies;

          mapping (uint => address) public zombieToOwner;
          mapping (address => uint) ownerZombieCount;

          function _createZombie(string _name, uint _dna) internal {
            uint id = zombies.push(Zombie(_name, _dna, 1, uint32(now + cooldownTime), 0, 0)) - 1;
            zombieToOwner[id] = msg.sender;
            ownerZombieCount[msg.sender]++;
            NewZombie(id, _name, _dna);
          }

          function _generatePseudoRandomDna(string _str) private view returns (uint) {
            uint rand = uint(keccak256(_str));
            return rand % dnaModulus;
          }

          function createPseudoRandomZombie(string _name) public {
            require(ownerZombieCount[msg.sender] == 0);
            uint randDna = _generatePseudoRandomDna(_name);
            randDna = randDna - randDna % 100;
            _createZombie(_name, randDna);
          }

        }
      "ownable.sol": |
        /**
         * @title Ownable
         * @dev The Ownable contract has an owner address, and provides basic authorization control
         * functions, this simplifies the implementation of "user permissions".
         */
        contract Ownable {
          address public owner;

          event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

          /**
           * @dev The Ownable constructor sets the original `owner` of the contract to the sender
           * account.
           */
          function Ownable() public {
            owner = msg.sender;
          }


          /**
           * @dev Throws if called by any account other than the owner.
           */
          modifier onlyOwner() {
            require(msg.sender == owner);
            _;
          }


          /**
           * @dev Allows the current owner to transfer control of the contract to a newOwner.
           * @param newOwner The address to transfer ownership to.
           */
          function transferOwnership(address newOwner) public onlyOwner {
            require(newOwner != address(0));
            OwnershipTransferred(owner, newOwner);
            owner = newOwner;
          }

        }
      "safemath.sol": |
        pragma solidity ^0.4.18;

        /**
         * @title SafeMath
         * @dev Math operations with safety checks that throw on error
         */
        library SafeMath {

          /**
          * @dev Multiplies two numbers, throws on overflow.
          */
          function mul(uint256 a, uint256 b) internal pure returns (uint256) {
            if (a == 0) {
              return 0;
            }
            uint256 c = a * b;
            assert(c / a == b);
            return c;
          }

          /**
          * @dev Integer division of two numbers, truncating the quotient.
          */
          function div(uint256 a, uint256 b) internal pure returns (uint256) {
            // assert(b > 0); // Solidity automatically throws when dividing by 0
            uint256 c = a / b;
            // assert(a == b * c + a % b); // There is no case in which this doesn't hold
            return c;
          }

          /**
          * @dev Subtracts two numbers, throws on overflow (i.e. if subtrahend is greater than minuend).
          */
          function sub(uint256 a, uint256 b) internal pure returns (uint256) {
            assert(b <= a);
            return a - b;
          }

          /**
          * @dev Adds two numbers, throws on overflow.
          */
          function add(uint256 a, uint256 b) internal pure returns (uint256) {
            uint256 c = a + b;
            assert(c >= a);
            return c;
          }
        }
      "erc721.sol": |
        contract ERC721 {
          event Transfer(address indexed _from, address indexed _to, uint256 _tokenId);
          event Approval(address indexed _owner, address indexed _approved, uint256 _tokenId);

          function balanceOf(address _owner) public view returns (uint256 _balance);
          function ownerOf(uint256 _tokenId) public view returns (address _owner);
          function transfer(address _to, uint256 _tokenId) public;
          function approve(address _to, uint256 _tokenId) public;
          function takeOwnership(uint256 _tokenId) public;
        }
    answer: |
      <!DOCTYPE html>
      <html lang="en">
        <head>
          <meta charset="UTF-8">
          <title>CryptoZombies front-end</title>
          <script language="javascript" type="text/javascript" src="https://cdnjs.cloudflare.com/ajax/libs/jquery/3.3.1/jquery.min.js"></script>
          <script language="javascript" type="text/javascript" src="web3.min.js"></script>
          <script language="javascript" type="text/javascript" src="cryptozombies_abi.js"></script>
        </head>
        <body>
          <div id="txStatus"></div>
          <div id="zombies"></div>

          <script>
            var cryptoZombies;
            var userAccount;

            function startApp() {
              var cryptoZombiesAddress = "YOUR_CONTRACT_ADDRESS";
              cryptoZombies = new web3js.eth.Contract(cryptoZombiesABI, cryptoZombiesAddress);

              var accountInterval = setInterval(function() {
                // Check if account has changed
                if (web3.eth.accounts[0] !== userAccount) {
                  userAccount = web3.eth.accounts[0];
                  // Call a function to update the UI with the new account
                  getZombiesByOwner(userAccount)
                  .then(displayZombies);
                }
              }, 100);
            }

            function displayZombies(ids) {
              $("#zombies").empty();
              for (id of ids) {
                // Look up zombie details from our contract. Returns a `zombie` object
                getZombieDetails(id)
                .then(function(zombie) {
                  // Using ES6's "template literals" to inject variables into the HTML.
                  // Append each one to our #zombies div
                  $("#zombies").append(`<div class="zombie">
                    <ul>
                      <li>Name: ${zombie.name}</li>
                      <li>DNA: ${zombie.dna}</li>
                      <li>Level: ${zombie.level}</li>
                      <li>Wins: ${zombie.winCount}</li>
                      <li>Losses: ${zombie.lossCount}</li>
                      <li>Ready Time: ${zombie.readyTime}</li>
                    </ul>
                  </div>`);
                });
              }
            }

            function createPseudoRandomZombie(name) {
              // This is going to take a while, so update the UI to let the user know
              // the transaction has been sent
              $("#txStatus").text("Creating new zombie on the blockchain. This may take a while...");
              // Send the tx to our contract:
              return cryptoZombies.methods.createPseudoRandomZombie(name)
              .send({ from: userAccount })
              .on("receipt", function(receipt) {
                $("#txStatus").text("Successfully created " + name + "!");
                // Transaction was accepted into the blockchain, let's redraw the UI
                getZombiesByOwner(userAccount).then(displayZombies);
              })
              .on("error", function(error) {
                // Do something to alert the user their transaction has failed
                $("#txStatus").text(error);
              });
            }

            function feedOnKitty(zombieId, kittyId) {
              // This is going to take a while, so update the UI to let the user know
              // the transaction has been sent
              $("#txStatus").text("Eating a kitty. This may take a while...");
              // Send the tx to our contract:
              return cryptoZombies.methods.feedOnKitty(zombieId, kittyId)
              .send({ from: userAccount })
              .on("receipt", function(receipt) {
                $("#txStatus").text("Ate a kitty and spawned a new Zombie!");
                // Transaction was accepted into the blockchain, let's redraw the UI
                getZombiesByOwner(userAccount).then(displayZombies);
              })
              .on("error", function(error) {
                // Do something to alert the user their transaction has failed
                $("#txStatus").text(error);
              });
            }

            function getZombieDetails(id) {
              return cryptoZombies.methods.zombies(id).call();
            }

            function zombieToOwner(id) {
              return cryptoZombies.methods.zombieToOwner(id).call();
            }

            function getZombiesByOwner(owner) {
              return cryptoZombies.methods.getZombiesByOwner(owner).call();
            }

            window.addEventListener('load', function() {

              // Checking if Web3 has been injected by the browser (Mist/MetaMask)
              if (typeof web3 !== 'undefined') {
                // Use Mist/MetaMask's provider
                web3js = new Web3(web3.currentProvider);
              } else {
                // Handle the case where the user doesn't have Metamask installed
                // Probably show them a message prompting them to install Metamask
              }

              // Now you can start your app & access web3 freely:
              startApp();

            })
          </script>
        </body>
      </html>
---

素晴らしい！これで我々のUIはユーザーのMetamaskアカウントを検出して、自動でホームページ上にゾンビ軍団を表示するようになった！

では今度は、スマートコントラクト上のデータを変更するための`send`の使用を見ていこう。

`call`関数とはいくつか大きな違いがある:

1. トランザクションを`send`するのには関数を呼び出す者の`from`アドレスが必要だ(Solidityのコードでは`msg.sender`となる)。これがDAppのユーザーであるようにしたいから、彼らにトランザクションへの署名を要求するようMetamaskがポップアップする。

2. トランザクションを`send`するにはガスがかかる

3. ユーザーがトランザクションを`send`してから、それが実際にブロックチェーン上で有効になるまでにはかなりの遅れがある。 この原因は、トランザクションがブロックに含まれるのを待つ必要があり、またイーサリアムのブロック生成時間が平均15秒であるからだ。 イーサリアム上にたくさん保留中トランザクションがある場合や、ユーザーがあまりに低いガスプライスを送信した場合は、トランザクションが取り込まれるまで数ブロック待たなければならず、数分かかることもある。

  このため、コードの非同期性を処理するためのロジックがアプリケーションで必要となる。

## ゾンビの作成

我々のコントラクトの最初の関数を使った例を見てみよう。新しいユーザーがこの関数を呼び出す: `createPseudoRandomZombie`.

復習として、これがコントラクトのSolidityコードだ:

```
function createPseudoRandomZombie(string _name) public {
  require(ownerZombieCount[msg.sender] == 0);
  uint randDna = _generatePseudoRandomDna(_name);
  randDna = randDna - randDna % 100;
  _createZombie(_name, randDna);
}
```

以下はMetamaskを使ってWeb3.jsでこの関数を呼び出す例だ:

```
function createPseudoRandomZombie(name) {
  // しばらく時間がかかるので、UIを更新してユーザーに
  // トランザクションが送信されたことを知らせる
  $("#txStatus").text("Creating new zombie on the blockchain. This may take a while...");
  // トランザクションをコントラクトに送信する:
  return cryptoZombies.methods.createPseudoRandomZombie(name)
  .send({ from: userAccount })
  .on("receipt", function(receipt) {
    $("#txStatus").text("Successfully created " + name + "!");
    // トランザクションがブロックチェーンに取り込まれた。UIをアップデートしよう
    getZombiesByOwner(userAccount).then(displayZombies);
  })
  .on("error", function(error) {
    // トランザクションが失敗したことをユーザーに通知するために何かを行う
    $("#txStatus").text(error);
  });
}
```

我々の関数はトランザクションをWeb3プロバイダに`send`して、いくつかのイベントリスナーを連ねていく:

- `receipt`は、トランザクションがEthereumのブロックに含まれると発行される。これは我々のゾンビが作成され、コントラクトに保存されたことを意味する。
- `error`は十分な量のガスを送っていないといったように、ブロックへのトランザクションの取り込みを妨げる問題があるときに発生する。 我々は、UIでトランザクションがうまくいかなかったことをユーザーに通知し、トランザクションをやり直せるようにしたい。

> 注: `send`を呼び出す場合、オプションで`gas`と`gasPrice`を指定することができます。(例 `.send({ from: userAccount, gas: 3000000 })`) これを指定しなければ、Metamaskがこれらの数値をユーザーに選ばせます。

## さあテストだ

`txStatus`のIDを持つ`div`を追加しておいた。こうやって、このdivを使ってトランザクションステータスのメッセージを表示してUIを更新することができる。

1. `displayZombies`の下に、上の`createPseudoRandomZombie`からコードをコピー＆ペーストせよ。

2. 別の関数を実装しよう: `feedOnKitty`

  `feedOnKitty`を呼び出すロジックはほとんど同じだ。関数を呼び出すトランザクションを送信し、トランザクションがうまくいくと新たなゾンビが生成されるから、そのあとそれをUIに表示し直したい。

  そのすぐ下に`createPseudoRandomZombie`のコピーを作成するが、以下の変更をするように:

  a) ２番目の関数`feedOnKitty`を呼び出せ。これは`zombieId`と`kittyId`の２つの引数を受け取る。

  b) `#txStatus`のテキストを次のように更新しなくてはならない: `"Eating a kitty. This may take a while..."`(キティを捕食中。しばしお待ちください...)

  c) コントラクトの`feedOnKitty`を呼び出し、同じ２つの引数を渡せ。

  d) `#txStatus`の成功メッセージを次のようにせよ: `"Ate a kitty and spawned a new Zombie!"`(キティを捕食して新たなゾンビを生み出した！)
