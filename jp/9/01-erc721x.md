---
title: 背景となる知識＆実装スタート
actions: ['答え合わせ', 'ヒント']
requireLogin: true
material:
  editor:
    language: sol
    startingCode:
        "ZombieToken.sol": |
          //ここから始めるのだ

        "ERC721XToken.sol": |

          // 全てのファイルを含んだ完全な実装は https://github.com/loomnetwork/erc721x

          pragma solidity ^0.4.25;

          import "./../../Interfaces/ERC721X.sol";

          import "./../../Interfaces/ERC721XReceiver.sol";
          import "./ERC721XTokenNFT.sol";

          import "openzeppelin-solidity/contracts/AddressUtils.sol";
          import "./../../Libraries/ObjectsLib.sol";


          // Additional features over NFT token that is compatible with batch transfers
          contract ERC721XToken is ERC721X, ERC721XTokenNFT {

              using ObjectLib for ObjectLib.Operations;
              using AddressUtils for address;

              bytes4 internal constant ERC721X_RECEIVED = 0x660b3370;
              bytes4 internal constant ERC721X_BATCH_RECEIVE_SIG = 0xe9e5be6a;

              event BatchTransfer(address from, address to, uint256[] tokenTypes, uint256[] amounts);


              modifier isOperatorOrOwner(address _from) {
                  require((msg.sender == _from) || operators[_from][msg.sender], "msg.sender is neither _from nor operator");
                  _;
              }

              function implementsERC721X() public pure returns (bool) {
                  return true;
              }

              /**
               * @dev transfer objects from different tokenIds to specified address
               * @param _from The address to BatchTransfer objects from.
               * @param _to The address to batchTransfer objects to.
               * @param _tokenIds Array of tokenIds to update balance of
               * @param _amounts Array of amount of object per type to be transferred.
               * Note:  Arrays should be sorted so that all tokenIds in a same bin are adjacent (more efficient).
               */
              function _batchTransferFrom(address _from, address _to, uint256[] _tokenIds, uint256[] _amounts)
                  internal
                  isOperatorOrOwner(_from)
              {

                  // Requirements
                  require(_tokenIds.length == _amounts.length, "Inconsistent array length between args");
                  require(_to != address(0), "Invalid recipient");

                  if (tokenType[_tokenIds[0]] == NFT) {
                      tokenOwner[_tokenIds[0]] = _to;
                      emit Transfer(_from, _to, _tokenIds[0]);
                  }

                  // Load first bin and index where the object balance exists
                  (uint256 bin, uint256 index) = ObjectLib.getTokenBinIndex(_tokenIds[0]);

                  // Balance for current bin in memory (initialized with first transfer)
                  // Written with bad library syntax instead of as below to bypass stack limit error
                  uint256 balFrom = ObjectLib.updateTokenBalance(
                      packedTokenBalance[_from][bin], index, _amounts[0], ObjectLib.Operations.SUB
                  );
                  uint256 balTo = ObjectLib.updateTokenBalance(
                      packedTokenBalance[_to][bin], index, _amounts[0], ObjectLib.Operations.ADD
                  );

                  // Number of transfers to execute
                  uint256 nTransfer = _tokenIds.length;

                  // Last bin updated
                  uint256 lastBin = bin;

                  for (uint256 i = 1; i < nTransfer; i++) {
                      // If we're transferring an NFT we additionally should update the tokenOwner and emit the corresponding event
                      if (tokenType[_tokenIds[i]] == NFT) {
                          tokenOwner[_tokenIds[i]] = _to;
                          emit Transfer(_from, _to, _tokenIds[i]);
                      }
                      (bin, index) = _tokenIds[i].getTokenBinIndex();

                      // If new bin
                      if (bin != lastBin) {
                          // Update storage balance of previous bin
                          packedTokenBalance[_from][lastBin] = balFrom;
                          packedTokenBalance[_to][lastBin] = balTo;

                          // Load current bin balance in memory
                          balFrom = packedTokenBalance[_from][bin];
                          balTo = packedTokenBalance[_to][bin];

                          // Bin will be the most recent bin
                          lastBin = bin;
                      }

                      // Update memory balance
                      balFrom = balFrom.updateTokenBalance(index, _amounts[i], ObjectLib.Operations.SUB);
                      balTo = balTo.updateTokenBalance(index, _amounts[i], ObjectLib.Operations.ADD);
                  }

                  // Update storage of the last bin visited
                  packedTokenBalance[_from][bin] = balFrom;
                  packedTokenBalance[_to][bin] = balTo;

                  // Emit batchTransfer event
                  emit BatchTransfer(_from, _to, _tokenIds, _amounts);
              }

              function batchTransferFrom(address _from, address _to, uint256[] _tokenIds, uint256[] _amounts) public {
                  // Batch Transfering
                  _batchTransferFrom(_from, _to, _tokenIds, _amounts);
              }

              /**
               * @dev transfer objects from different tokenIds to specified address
               * @param _from The address to BatchTransfer objects from.
               * @param _to The address to batchTransfer objects to.
               * @param _tokenIds Array of tokenIds to update balance of
               * @param _amounts Array of amount of object per type to be transferred.
               * @param _data Data to pass to onERC721XReceived() function if recipient is contract
               * Note:  Arrays should be sorted so that all tokenIds in a same bin are adjacent (more efficient).
               */
              function safeBatchTransferFrom(
                  address _from,
                  address _to,
                  uint256[] _tokenIds,
                  uint256[] _amounts,
                  bytes _data
              )
                  public
              {

                  // Batch Transfering
                  _batchTransferFrom(_from, _to, _tokenIds, _amounts);

                  // Pass data if recipient is contract
                  if (_to.isContract()) {
                      bytes4 retval = ERC721XReceiver(_to).onERC721XBatchReceived(
                          msg.sender, _from, _tokenIds, _amounts, _data
                      );
                      require(retval == ERC721X_BATCH_RECEIVE_SIG);
                  }
              }

              function transfer(address _to, uint256 _tokenId, uint256 _amount) public {
                  _transferFrom(msg.sender, _to, _tokenId, _amount);
              }

              function transferFrom(address _from, address _to, uint256 _tokenId, uint256 _amount) public {
                  _transferFrom(_from, _to, _tokenId, _amount);
              }

              function _transferFrom(address _from, address _to, uint256 _tokenId, uint256 _amount)
                  internal
                  isOperatorOrOwner(_from)
              {
                  require(tokenType[_tokenId] == FT);
                  require(_amount <= balanceOf(_from, _tokenId), "Quantity greater than from balance");
                  require(_to != address(0), "Invalid to address");

                  _updateTokenBalance(_from, _tokenId, _amount, ObjectLib.Operations.SUB);
                  _updateTokenBalance(_to, _tokenId, _amount, ObjectLib.Operations.ADD);
                  emit TransferWithQuantity(_from, _to, _tokenId, _amount);
              }

              function safeTransferFrom(address _from, address _to, uint256 _tokenId, uint256 _amount) public {
                  safeTransferFrom(_from, _to, _tokenId, _amount, "");
              }

              function safeTransferFrom(address _from, address _to, uint256 _tokenId, uint256 _amount, bytes _data) public {
                  _transferFrom(_from, _to, _tokenId, _amount);
                  require(
                      checkAndCallSafeTransfer(_from, _to, _tokenId, _amount, _data),
                      "Sent to a contract which is not an ERC721X receiver"
                  );
              }

              function _mint(uint256 _tokenId, address _to, uint256 _supply) internal {
                  // If the token doesn't exist, add it to the tokens array
                  if (!exists(_tokenId)) {
                      tokenType[_tokenId] = FT;
                      allTokens.push(_tokenId);
                  } else {
                      // if the token exists, it must be a FT
                      require(tokenType[_tokenId] == FT, "Not a FT");
                  }

                  _updateTokenBalance(_to, _tokenId, _supply, ObjectLib.Operations.ADD);
                  emit TransferWithQuantity(address(this), _to, _tokenId, _supply);
              }


              function checkAndCallSafeTransfer(
                  address _from,
                  address _to,
                  uint256 _tokenId,
                  uint256 _amount,
                  bytes _data
              )
                  internal
                  returns (bool)
              {
                  if (!_to.isContract()) {
                      return true;
                  }

                  bytes4 retval = ERC721XReceiver(_to).onERC721XReceived(
                      msg.sender, _from, _tokenId, _amount, _data);
                  return(retval == ERC721X_RECEIVED);
              }

          }
    answer: >
      pragma solidity ^0.4.25;

      import "./ERC721XToken.sol";

      contract ZombieCard is ERC721XToken {

      }
---

このレッスンでは、サンプルのブロックチェーンゲーム用にトークンコントラクトを作っていくぞ。

ここでは、マジック：ザ・ギャザリングやハースストーンに似た、我々Loom Networkによるブロックチェーンベースのカード収集ゲーム・<a href="https://loom.games/" target=_blank>Zombie Battleground</a>におけるERC721xの実装方法を大まかに基にしている。

これで現実世界のユースケースがどんなものかわかるだろうし、レッスン修了後は独自のゲームを実装する力がより備わっていることだろう。

最初のチャプターでは、お主達みんなが同じように理解しているか確認するために背景となる知識から初めていくからな...

## 背景となる知識: トークンとは一体何なのか？

ゲーム内アイテムを「トークン」と呼ぶと、やや誤解を招く可能性がある。「トークン」という言葉は、通貨みたいに使われるものを意味しているからな。

だがEthereumにおける **_トークン_** という言葉は、`transfer(address _to, uint256 _value)`や`balanceOf(address _owner)`といった共通の関数を全て実装したスマートコントラクト基準のことを指している。

もちろん通貨を移転したり残高を保有したりすることは可能だが、剣やカードといったゲーム内アイテムの全てを移転したり保有したりすることもできるのだ。

ゲームの剣は通貨みたいに使うことはできないけれども、プレイヤーが保有可能ないかなるものにも、これと同じ基本的機能があってほしいものだ。

結果として「トークン」のコントラクトはゲーム内アイテム全てに適用可能なので、これら共通メソッドを抽象化するのに役立つ — こうやってEthereum上の全ゲームアイテムは共通のインターフェースを持つことになり、同じトークン基準に従ったウォレットやマーケットプレイスによりサポートが可能となるのだ。

## 様々なトークンスタンダード: ERC20とERC721

最も一般的に知られたトークン基準は **_ERC20トークン_** だ。このトークンは通貨のような働きをするが、トレーディングカードゲームのカードみたいなデジタルアイテムを表すには、あまり便利なものではない。

第一に、カードは通貨みたいに分割不可能だ — カードの５分の２を所有、なんてことはできないからな。

第二に、全てのカードが同等に作られているわけではないから、カードは互いに代替不可である。お主の **Z-Virus** カードを他人の **Ozmoziz** に交換したくはないだろう。

そこでクリプト収集物により適した別のトークン基準があり、 **_ERC721トークン_** と呼ばれている。

**_ERC721トークン_** は互いに代替可能 **ではない**。各アイテムはユニークであるという前提で、それぞれに固有のIDを持っている。また分割は不可能だから、丸々１つでのみトレードすることができる。

つまりこのトークンは我らがトレーディングカードゲームを作るのにより適しているというわけだ。

**より** 適していると今説明したな。では完璧じゃないのかって？ 😉

## ERC721x — ２つの世界のベストなもの

ERC721のゲーム内アイテムの問題点は、ほとんどのゲームには全く同一のアイテムが **ある** ということだ。

例えば、我々のカードゲームには様々な300枚のカードがあるとしよう。しかしこれらのカードは１枚よりも多くのコピーが必要だ。そうしないと、たくさんの人々がゲームをプレイできなくなるからな！

同じカードの複数コピーは(ほとんどの部分で)互いに代替可能である。なのでこのことを考慮したトークン基準があるというのは理にかなった話だろう。

こういったユースケースを扱うために、ERC721xは複数トークン(カードテンプレートとして考えること)の定義や、さらに今後存在するカードの総数といったものをカード毎に定義することを可能にしているのだ。

今後のチャプターで１つ１つやって行く中で、お主もその方法を理解できるだろう 👨‍🏫

## さあ始めよう: プロジェクトにおけるERC721xの使用

新規のERC721xトークンを実装する場合に、スクラッチから始める必要はない。ERC20トークンでテンプレートを<a href="https://github.com/OpenZeppelin/openzeppelin-solidity" target=_blank>Open Zeppelin</a>などからコピーして必要な変更を加えたみたいに、ERC721xについても我々の実装を自分のプロジェクトに導入してスタートすることができるのだ。

今後プロジェクトにERC721xの実装を含めるには、Githubにある<a href="https://github.com/loomnetwork/erc721x" target=_blank>こちら</a>の手順に従うのだ。だがこのレッスンではお主のために、`ERC721XToken.sol`を事前にプロジェクトに含めておいたからな。

全コードを理解できなくても心配することはない — １つ１つ理解すべき部分を一緒にやって行くぞ。

# さあテストだ

基礎から初めて行くぞ: `ERC721XToken`のロジックを継承している"ZombieCard"という新たなコントラクトを作っていこう。

1. まずは冒頭で、`pragma solidity ^0.4.25`を使用することを宣言せよ。

2. pragmaの宣言後、ファイル`./ERC721XToken.sol`を`import`せよ。

3. 次に"ZombieCard"という名の新たな`contract`を宣言せよ。キーワード`is`を使って、このコントラクトが`ERC721XToken`を継承するようにすること。コントラクト本文は今は空のままで良いからな。

> 注: 基本を思い出せずに困ってしまったら、<a href="https://cryptozombies.io/jp/lesson/1/chapter/2">レッスン１ チャプター２</a>と<a href="https://cryptozombies.io/jp/lesson/2/chapter/5">レッスン２ チャプター５</a>を復習してみましょう。
