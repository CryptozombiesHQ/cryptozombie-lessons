---
title: Tokeny na Ethereu
actions: ['checkAnswer', 'hints']
requireLogin: true
material:
  editor:
    language: sol
    startingCode:
      "zombieownership.sol": |
        // Začni tu
      "zombieattack.sol": |
        pragma solidity ^0.4.19;

        import "./zombiehelper.sol";

        contract ZombieAttack is ZombieHelper {
          uint randNonce = 0;
          uint attackVictoryProbability = 70;

          function randMod(uint _modulus) internal returns(uint) {
            randNonce++;
            return uint(keccak256(now, msg.sender, randNonce)) % _modulus;
          }

          function attack(uint _zombieId, uint _targetId) external ownerOf(_zombieId) {
            Zombie storage myZombie = zombies[_zombieId];
            Zombie storage enemyZombie = zombies[_targetId];
            uint rand = randMod(100);
            if (rand <= attackVictoryProbability) {
              myZombie.winCount++;
              myZombie.level++;
              enemyZombie.lossCount++;
              feedAndMultiply(_zombieId, enemyZombie.dna, "zombie");
            } else {
              myZombie.lossCount++;
              enemyZombie.winCount++;
              _triggerCooldown(myZombie);
            }
          }
        }
      "zombiehelper.sol": |
        pragma solidity ^0.4.19;

        import "./zombiefeeding.sol";

        contract ZombieHelper is ZombieFeeding {

          uint levelUpFee = 0.001 ether;

          modifier aboveLevel(uint _level, uint _zombieId) {
            require(zombies[_zombieId].level >= _level);
            _;
          }

          function withdraw() external onlyOwner {
            owner.transfer(this.balance);
          }

          function setLevelUpFee(uint _fee) external onlyOwner {
            levelUpFee = _fee;
          }

          function levelUp(uint _zombieId) external payable {
            require(msg.value == levelUpFee);
            zombies[_zombieId].level++;
          }

          function changeName(uint _zombieId, string _newName) external aboveLevel(2, _zombieId) ownerOf(_zombieId) {
            zombies[_zombieId].name = _newName;
          }

          function changeDna(uint _zombieId, uint _newDna) external aboveLevel(20, _zombieId) ownerOf(_zombieId) {
            zombies[_zombieId].dna = _newDna;
          }

          function getZombiesByOwner(address _owner) external view returns(uint[]) {
            uint[] memory result = new uint[](ownerZombieCount[_owner]);
            uint counter = 0;
            for (uint i = 0; i < zombies.length; i++) {
              if (zombieToOwner[i] == _owner) {
                result[counter] = i;
                counter++;
              }
            }
            return result;
          }

        }
      "zombiefeeding.sol": |
        pragma solidity ^0.4.19;

        import "./zombiefactory.sol";

        contract KittyInterface {
          function getKitty(uint256 _id) external view returns (
            bool isGestating,
            bool isReady,
            uint256 cooldownIndex,
            uint256 nextActionAt,
            uint256 siringWithId,
            uint256 birthTime,
            uint256 matronId,
            uint256 sireId,
            uint256 generation,
            uint256 genes
          );
        }

        contract ZombieFeeding is ZombieFactory {

          KittyInterface kittyContract;

          modifier ownerOf(uint _zombieId) {
            require(msg.sender == zombieToOwner[_zombieId]);
            _;
          }

          function setKittyContractAddress(address _address) external onlyOwner {
            kittyContract = KittyInterface(_address);
          }

          function _triggerCooldown(Zombie storage _zombie) internal {
            _zombie.readyTime = uint32(now + cooldownTime);
          }

          function _isReady(Zombie storage _zombie) internal view returns (bool) {
              return (_zombie.readyTime <= now);
          }

          function feedAndMultiply(uint _zombieId, uint _targetDna, string _species) internal ownerOf(_zombieId) {
            Zombie storage myZombie = zombies[_zombieId];
            require(_isReady(myZombie));
            _targetDna = _targetDna % dnaModulus;
            uint newDna = (myZombie.dna + _targetDna) / 2;
            if (keccak256(_species) == keccak256("kitty")) {
              newDna = newDna - newDna % 100 + 99;
            }
            _createZombie("NoName", newDna);
            _triggerCooldown(myZombie);
          }

          function feedOnKitty(uint _zombieId, uint _kittyId) public {
            uint kittyDna;
            (,,,,,,,,,kittyDna) = kittyContract.getKitty(_kittyId);
            feedAndMultiply(_zombieId, kittyDna, "kitty");
          }
        }
      "zombiefactory.sol": |
        pragma solidity ^0.4.19;

        import "./ownable.sol";

        contract ZombieFactory is Ownable {

            event NewZombie(uint zombieId, string name, uint dna);

            uint dnaDigits = 16;
            uint dnaModulus = 10 ** dnaDigits;
            uint cooldownTime = 1 days;

            struct Zombie {
              string name;
              uint dna;
              uint32 level;
              uint32 readyTime;
              uint16 winCount;
              uint16 lossCount;
            }

            Zombie[] public zombies;

            mapping (uint => address) public zombieToOwner;
            mapping (address => uint) ownerZombieCount;

            function _createZombie(string _name, uint _dna) internal {
                uint id = zombies.push(Zombie(_name, _dna, 1, uint32(now + cooldownTime), 0, 0)) - 1;
                zombieToOwner[id] = msg.sender;
                ownerZombieCount[msg.sender]++;
                NewZombie(id, _name, _dna);
            }

            function _generateRandomDna(string _str) private view returns (uint) {
                uint rand = uint(keccak256(_str));
                return rand % dnaModulus;
            }

            function createRandomZombie(string _name) public {
                require(ownerZombieCount[msg.sender] == 0);
                uint randDna = _generateRandomDna(_name);
                randDna = randDna - randDna % 100;
                _createZombie(_name, randDna);
            }

        }
      "ownable.sol": |
        /**
         * @title Ownable
         * @dev The Ownable contract has an owner address, and provides basic authorization control
         * functions, this simplifies the implementation of "user permissions".
         */
        contract Ownable {
          address public owner;

          event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

          /**
           * @dev The Ownable constructor sets the original `owner` of the contract to the sender
           * account.
           */
          function Ownable() public {
            owner = msg.sender;
          }


          /**
           * @dev Throws if called by any account other than the owner.
           */
          modifier onlyOwner() {
            require(msg.sender == owner);
            _;
          }


          /**
           * @dev Allows the current owner to transfer control of the contract to a newOwner.
           * @param newOwner The address to transfer ownership to.
           */
          function transferOwnership(address newOwner) public onlyOwner {
            require(newOwner != address(0));
            OwnershipTransferred(owner, newOwner);
            owner = newOwner;
          }

        }
    answer: |
      pragma solidity ^0.4.19;

      import "./zombieattack.sol";
      
      contract ZombieOwnership is ZombieAttack {

      }
---

Poďme sa porozprávať o **_tokenoch_**.

Ak si sa v oblasti Ethereum pohybuješ už nejakú dobu, pravdepodobne si počul ľudí často spomínať rôzne tokeny - hlavne **_ERC20 tokeny_**.

**_Token_** na Ethereu je v podstate smart kontrakt ktorý dodržuje určité spoločné pravidlá - presnejšie, implementuje množinu funkcií ktoré su definované aj na kontraktoch všetkých ostatných ERC20 tokenov, ako napríklad `transfer(address _to, uint256 _value)` a `balanceOf(address _owner)`.

Interne tieto smart kontrakty obvykle používajú mapovanie `mapping(address => uint256) balances` na to, aby si udržiavali prehľad o tom ktorý Ethereum účet má koľko tokenov. 

Takže token je v podstate len smart kontrakt ktorý si drží prehľad o tom ktorá adresa má koľko tokenov, a implementuje funkcie ktoré umožňujú vlastníkom tokenov prevádzať tieto tokeny na iné adresy. 

### Prečo na tom záleží?

ERC20 tokeny zdieľajú množinu funkcií s totožnými menami. Preto je možné so všetkým ERC20 tokenmi pracovať rovnakým spôsobom.

To znamená, že ak vytvoríš aplikáciu ktorá je schopná pracovať s ERC20 tokenom, bude schopná pracovať s so všetkými ostatnými tokenmi implementujúcimi ERC20 štandard. Ak sa preto v budúcnosti rozhodneš vo svojej aplikácií začať používať nejaké ďalšie ERC20 tokeny, nebude ťa to stáť skoro žiadnu námahu. Proste vo svojej aplikácií nastavíš adresu ERC20 tokenu, a bum, tvoja aplikácia pracuje s novým tokenom.

Praktickým príkladom toho sú crypto exchange. Keď sa exchange rozhodne pridať nový ERC20 token, je to pre nich jednoduché. Stačí im pridať do ich databázy adresu smart kontraktu reprezentujúci daný token. Užívatelia môžu potom prostredníctom rovnakého kontraktu poslať tieto tokeny na adresu exchange. Naopak, keď sa užívateľ rozhodne tokeny si z exchange vytiahnuť, exchange prostredníctvom tohoto kontraktu pošle tokeny tam, kam si o to užívateľ požiada.

Exchange implementujú logiku prenosu tokenov len raz. Keď sa potom rozhodnú podporovať nový ERC20 token, je to len záležitosť pridania novej adresy smart kontraktu do ich databáz.  

### Ďalšie token štandardy

ERC20 tokeny su fakt super, pretože tokeny sa správajú ako meny. Tie by ale neboli veľmi užitočné pre reprezentáciu zombiekov v našej hre.

Za prvé, zombie nie sú delitelný ako meny. Síce niekomu môžeš poslať 0.237 ETH, no odoslanie 237 tisícin tvojho zombie nedáva zmysel. 

Za druhé, nie všetci zombie sú si rovní. Tvoj Level 2 zombie "**Steve**" absolútne nie je rovný môjmu Level 732 zombie "**H4XF13LD MORRIS 💯💯😎💯💯**". (Nie, ani trošku, *Steve*)

Existuje iný token štandard, ktorý je oveľa vhodnejší pre krypto zberatelské predmety (crypto-collectibles) ako sú CryptoZombies. Sú to **_ERC721 tokeny_**.

**_ERC721 tokeny_** **NIE SÚ** zameniteľné, pretože každý jeden z nich je považovaný za unikátny a ďalej nedeliteľný. Je možné ich obchodovať v celkoch, každý ma unikátne ID. Sú preto perfektným kandidátom riešenia, ako spraviť našich zombie obchodovateľných.

> Uvedom si že používanie štandardu ako ERC721 má výhodu v tom že v rámci našeho kontraktu nemusíme implementovať logiku aukcií a escrow, ktorá by určovala ako môžu hráči so svojimi zombie obchodovať. Miesto toho niekto iný môže vytvoriť platformu na obchodovanie s ERC721 aktívami. Pokiaľ sú potom naši zombíci v súlade s ERC721, budú plne kompatibilný s takouto obchodovacou platformou. Toto je jasná výhoda dodržiavania token štandardov miesto vymýšlania vlastnej obchodovacej logiky.

## Vyskúšaj si to sám

V ďalšej kapitole sa ideme ponoriť do implementácie ERC721. Ale pred tým si poďme pripraviť súbor s novým kontraktom.

Všetku logiku spojenú s ERC721 budeme mať v kontrakte názvom `ZombieOwnership`.

1. Na začiatku súboru deklaruj verziu `pragma` (pozri si súbory z predošlých lekcií pre zopakovanie syntaxe).

2. Ďalej sprav `import` súboru `zombieattack.sol`.

3. Deklaruj nový kontrakt, `ZombieOwnership`, ktorý dedí od  `ZombieAttack`. Telo kontraktu ponechaj zatiaľ prázdne.
