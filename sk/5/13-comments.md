---
title: Koment√°re
actions: ['checkAnswer', 'hints']
requireLogin: true
material:
  editor:
    language: sol
    startingCode:
      "zombieownership.sol": |
        pragma solidity ^0.4.19;

        import "./zombieattack.sol";
        import "./erc721.sol";
        import "./safemath.sol";

        /// TODO: Tento koment√°r nahraƒè natspec dokument√°ciou
        contract ZombieOwnership is ZombieAttack, ERC721 {

          using SafeMath for uint256;

          mapping (uint => address) zombieApprovals;

          function balanceOf(address _owner) public view returns (uint256 _balance) {
            return ownerZombieCount[_owner];
          }

          function ownerOf(uint256 _tokenId) public view returns (address _owner) {
            return zombieToOwner[_tokenId];
          }

          function _transfer(address _from, address _to, uint256 _tokenId) private {
            ownerZombieCount[_to] = ownerZombieCount[_to].add(1);
            ownerZombieCount[msg.sender] = ownerZombieCount[msg.sender].sub(1);
            zombieToOwner[_tokenId] = _to;
            Transfer(_from, _to, _tokenId);
          }

          function transfer(address _to, uint256 _tokenId) public onlyOwnerOf(_tokenId) {
            _transfer(msg.sender, _to, _tokenId);
          }

          function approve(address _to, uint256 _tokenId) public onlyOwnerOf(_tokenId) {
            zombieApprovals[_tokenId] = _to;
            Approval(msg.sender, _to, _tokenId);
          }

          function takeOwnership(uint256 _tokenId) public {
            require(zombieApprovals[_tokenId] == msg.sender);
            address owner = ownerOf(_tokenId);
            _transfer(owner, msg.sender, _tokenId);
          }
        }
      "zombieattack.sol": |
        pragma solidity ^0.4.19;

        import "./zombiehelper.sol";

        contract ZombieAttack is ZombieHelper {
          uint randNonce = 0;
          uint attackVictoryProbability = 70;

          function randMod(uint _modulus) internal returns(uint) {
            randNonce = randNonce.add(1);
            return uint(keccak256(now, msg.sender, randNonce)) % _modulus;
          }

          function attack(uint _zombieId, uint _targetId) external onlyOwnerOf(_zombieId) {
            Zombie storage myZombie = zombies[_zombieId];
            Zombie storage enemyZombie = zombies[_targetId];
            uint rand = randMod(100);
            if (rand <= attackVictoryProbability) {
              myZombie.winCount = myZombie.winCount.add(1);
              myZombie.level = myZombie.level.add(1);
              enemyZombie.lossCount = enemyZombie.lossCount.add(1);
              feedAndMultiply(_zombieId, enemyZombie.dna, "zombie");
            } else {
              myZombie.lossCount = myZombie.lossCount.add(1);
              enemyZombie.winCount = enemyZombie.winCount.add(1);
              _triggerCooldown(myZombie);
            }
          }
        }
      "zombiehelper.sol": |
        pragma solidity ^0.4.19;

        import "./zombiefeeding.sol";

        contract ZombieHelper is ZombieFeeding {

          uint levelUpFee = 0.001 ether;

          modifier aboveLevel(uint _level, uint _zombieId) {
            require(zombies[_zombieId].level >= _level);
            _;
          }

          function withdraw() external onlyOwner {
            owner.transfer(this.balance);
          }

          function setLevelUpFee(uint _fee) external onlyOwner {
            levelUpFee = _fee;
          }

          function levelUp(uint _zombieId) external payable {
            require(msg.value == levelUpFee);
            zombies[_zombieId].level = zombies[_zombieId].level.add(1);
          }

          function changeName(uint _zombieId, string _newName) external aboveLevel(2, _zombieId) onlyOwnerOf(_zombieId) {
            zombies[_zombieId].name = _newName;
          }

          function changeDna(uint _zombieId, uint _newDna) external aboveLevel(20, _zombieId) onlyOwnerOf(_zombieId) {
            zombies[_zombieId].dna = _newDna;
          }

          function getZombiesByOwner(address _owner) external view returns(uint[]) {
            uint[] memory result = new uint[](ownerZombieCount[_owner]);
            uint counter = 0;
            for (uint i = 0; i < zombies.length; i++) {
              if (zombieToOwner[i] == _owner) {
                result[counter] = i;
                counter++;
              }
            }
            return result;
          }

        }
      "zombiefeeding.sol": |
        pragma solidity ^0.4.19;

        import "./zombiefactory.sol";

        contract KittyInterface {
          function getKitty(uint256 _id) external view returns (
            bool isGestating,
            bool isReady,
            uint256 cooldownIndex,
            uint256 nextActionAt,
            uint256 siringWithId,
            uint256 birthTime,
            uint256 matronId,
            uint256 sireId,
            uint256 generation,
            uint256 genes
          );
        }

        contract ZombieFeeding is ZombieFactory {

          KittyInterface kittyContract;

          modifier onlyOwnerOf(uint _zombieId) {
            require(msg.sender == zombieToOwner[_zombieId]);
            _;
          }

          function setKittyContractAddress(address _address) external onlyOwner {
            kittyContract = KittyInterface(_address);
          }

          function _triggerCooldown(Zombie storage _zombie) internal {
            _zombie.readyTime = uint32(now + cooldownTime);
          }

          function _isReady(Zombie storage _zombie) internal view returns (bool) {
              return (_zombie.readyTime <= now);
          }

          function feedAndMultiply(uint _zombieId, uint _targetDna, string _species) internal onlyOwnerOf(_zombieId) {
            Zombie storage myZombie = zombies[_zombieId];
            require(_isReady(myZombie));
            _targetDna = _targetDna % dnaModulus;
            uint newDna = (myZombie.dna + _targetDna) / 2;
            if (keccak256(_species) == keccak256("kitty")) {
              newDna = newDna - newDna % 100 + 99;
            }
            _createZombie("NoName", newDna);
            _triggerCooldown(myZombie);
          }

          function feedOnKitty(uint _zombieId, uint _kittyId) public {
            uint kittyDna;
            (,,,,,,,,,kittyDna) = kittyContract.getKitty(_kittyId);
            feedAndMultiply(_zombieId, kittyDna, "kitty");
          }
        }
      "zombiefactory.sol": |
        pragma solidity ^0.4.19;

        import "./ownable.sol";
        import "./safemath.sol";

        contract ZombieFactory is Ownable {

          using SafeMath for uint256;
          using SafeMath32 for uint32;
          using SafeMath16 for uint16;

          event NewZombie(uint zombieId, string name, uint dna);

          uint dnaDigits = 16;
          uint dnaModulus = 10 ** dnaDigits;
          uint cooldownTime = 1 days;

          struct Zombie {
            string name;
            uint dna;
            uint32 level;
            uint32 readyTime;
            uint16 winCount;
            uint16 lossCount;
          }

          Zombie[] public zombies;

          mapping (uint => address) public zombieToOwner;
          mapping (address => uint) ownerZombieCount;

          function _createZombie(string _name, uint _dna) internal {
            uint id = zombies.push(Zombie(_name, _dna, 1, uint32(now + cooldownTime), 0, 0)) - 1;
            zombieToOwner[id] = msg.sender;
            ownerZombieCount[msg.sender] = ownerZombieCount[msg.sender].add(1);
            NewZombie(id, _name, _dna);
          }

          function _generateRandomDna(string _str) private view returns (uint) {
            uint rand = uint(keccak256(_str));
            return rand % dnaModulus;
          }

          function createRandomZombie(string _name) public {
            require(ownerZombieCount[msg.sender] == 0);
            uint randDna = _generateRandomDna(_name);
            randDna = randDna - randDna % 100;
            _createZombie(_name, randDna);
          }

        }
      "ownable.sol": |
        /**
         * @title Ownable
         * @dev The Ownable contract has an owner address, and provides basic authorization control
         * functions, this simplifies the implementation of "user permissions".
         */
        contract Ownable {
          address public owner;

          event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

          /**
           * @dev The Ownable constructor sets the original `owner` of the contract to the sender
           * account.
           */
          function Ownable() public {
            owner = msg.sender;
          }


          /**
           * @dev Throws if called by any account other than the owner.
           */
          modifier onlyOwner() {
            require(msg.sender == owner);
            _;
          }


          /**
           * @dev Allows the current owner to transfer control of the contract to a newOwner.
           * @param newOwner The address to transfer ownership to.
           */
          function transferOwnership(address newOwner) public onlyOwner {
            require(newOwner != address(0));
            OwnershipTransferred(owner, newOwner);
            owner = newOwner;
          }

        }
      "safemath.sol": |
        pragma solidity ^0.4.18;

        /**
         * @title SafeMath
         * @dev Math operations with safety checks that throw on error
         */
        library SafeMath {

          /**
          * @dev Multiplies two numbers, throws on overflow.
          */
          function mul(uint256 a, uint256 b) internal pure returns (uint256) {
            if (a == 0) {
              return 0;
            }
            uint256 c = a * b;
            assert(c / a == b);
            return c;
          }

          /**
          * @dev Integer division of two numbers, truncating the quotient.
          */
          function div(uint256 a, uint256 b) internal pure returns (uint256) {
            // assert(b > 0); // Solidity automatically throws when dividing by 0
            uint256 c = a / b;
            // assert(a == b * c + a % b); // There is no case in which this doesn't hold
            return c;
          }

          /**
          * @dev Subtracts two numbers, throws on overflow (i.e. if subtrahend is greater than minuend).
          */
          function sub(uint256 a, uint256 b) internal pure returns (uint256) {
            assert(b <= a);
            return a - b;
          }

          /**
          * @dev Adds two numbers, throws on overflow.
          */
          function add(uint256 a, uint256 b) internal pure returns (uint256) {
            uint256 c = a + b;
            assert(c >= a);
            return c;
          }
        }

        /**
         * @title SafeMath32
         * @dev SafeMath library implemented for uint32
         */
        library SafeMath32 {

          function mul(uint32 a, uint32 b) internal pure returns (uint32) {
            if (a == 0) {
              return 0;
            }
            uint32 c = a * b;
            assert(c / a == b);
            return c;
          }

          function div(uint32 a, uint32 b) internal pure returns (uint32) {
            // assert(b > 0); // Solidity automatically throws when dividing by 0
            uint32 c = a / b;
            // assert(a == b * c + a % b); // There is no case in which this doesn't hold
            return c;
          }

          function sub(uint32 a, uint32 b) internal pure returns (uint32) {
            assert(b <= a);
            return a - b;
          }

          function add(uint32 a, uint32 b) internal pure returns (uint32) {
            uint32 c = a + b;
            assert(c >= a);
            return c;
          }
        }

        /**
         * @title SafeMath16
         * @dev SafeMath library implemented for uint16
         */
        library SafeMath16 {

          function mul(uint16 a, uint16 b) internal pure returns (uint16) {
            if (a == 0) {
              return 0;
            }
            uint16 c = a * b;
            assert(c / a == b);
            return c;
          }

          function div(uint16 a, uint16 b) internal pure returns (uint16) {
            // assert(b > 0); // Solidity automatically throws when dividing by 0
            uint16 c = a / b;
            // assert(a == b * c + a % b); // There is no case in which this doesn't hold
            return c;
          }

          function sub(uint16 a, uint16 b) internal pure returns (uint16) {
            assert(b <= a);
            return a - b;
          }

          function add(uint16 a, uint16 b) internal pure returns (uint16) {
            uint16 c = a + b;
            assert(c >= a);
            return c;
          }
        }
      "erc721.sol": |
        contract ERC721 {
          event Transfer(address indexed _from, address indexed _to, uint256 _tokenId);
          event Approval(address indexed _owner, address indexed _approved, uint256 _tokenId);

          function balanceOf(address _owner) public view returns (uint256 _balance);
          function ownerOf(uint256 _tokenId) public view returns (address _owner);
          function transfer(address _to, uint256 _tokenId) public;
          function approve(address _to, uint256 _tokenId) public;
          function takeOwnership(uint256 _tokenId) public;
        }
    answer: |
      pragma solidity ^0.4.19;

      import "./zombieattack.sol";
      import "./erc721.sol";
      import "./safemath.sol";

      contract ZombieOwnership is ZombieAttack, ERC721 {

        using SafeMath for uint256;

        mapping (uint => address) zombieApprovals;

        function balanceOf(address _owner) public view returns (uint256 _balance) {
          return ownerZombieCount[_owner];
        }

        function ownerOf(uint256 _tokenId) public view returns (address _owner) {
          return zombieToOwner[_tokenId];
        }

        function _transfer(address _from, address _to, uint256 _tokenId) private {
          ownerZombieCount[_to] = ownerZombieCount[_to].add(1);
          ownerZombieCount[msg.sender] = ownerZombieCount[msg.sender].sub(1);
          zombieToOwner[_tokenId] = _to;
          Transfer(_from, _to, _tokenId);
        }

        function transfer(address _to, uint256 _tokenId) public onlyOwnerOf(_tokenId) {
          _transfer(msg.sender, _to, _tokenId);
        }

        function approve(address _to, uint256 _tokenId) public onlyOwnerOf(_tokenId) {
          zombieApprovals[_tokenId] = _to;
          Approval(msg.sender, _to, _tokenId);
        }

        function takeOwnership(uint256 _tokenId) public {
          require(zombieApprovals[_tokenId] == msg.sender);
          address owner = ownerOf(_tokenId);
          _transfer(owner, msg.sender, _tokenId);
        }
      }
---

Solidity k√≥d na≈°e zombie hry je koneƒçne hotov√Ω!
The Solidity code for our zombie game is finally finished!

V ƒèal≈°√≠ch lekci√°ch sa pozrieme na to, ako nasadi≈• n√°≈° kontrakt na Ethereum, a ako sn√≠m pracova≈• z Javascriptu prostredn√≠ctvom Web3.js.
In the next lessons, we'll look at how to deploy the code to Ethereum, and how to interact with it with Web3.js.

Posledn√° vec, pred t√Ωm ne≈æ ≈•a v Lekcii 5 nech√°me √≠s≈•: Poƒème sa pobavi≈• o **komentovan√≠ k√≥du**.
But one final thing before we let you go in Lesson 5: Let's talk about **commenting your code**.

## Syntax pre koment√°re
## Syntax for comments

Koment√°re v Solidity su presne ako tie v JavaScripte. Doposiaƒæ si v CryptoZombies lekci√°ch videl u≈æ p√°r pr√≠kladov koment√°rov na jednom riadku, napr√≠klad:
Commenting in Solidity is just like JavaScript. You've already seen some examples of single line comments throughout the CryptoZombies lessons:

```
// Toto je koment√°r na jeden riadok. Je to tak√Ω odkaz pre seba sam√©ho (alebo pre druh√Ωch)
// This is a single-line comment. It's kind of like a note to self (or to others)
```

Staƒç√≠ prida≈• dvojit√© `//` kdekoƒævek, a mo≈æe≈° komentova≈•. Je to tak jednoduch√©, ≈æe by si to mal robi≈• neust√°le.
Just add double `//` anywhere and you're commenting. It's so easy that you should do it all the time.

Ale viem na ƒço mysl√≠≈° - jedno riadkov√© koment√°re nestaƒçia. Nakoniec, veƒè si aj roden√Ω spisovateƒæ!
But I hear you ‚Äî sometimes a single line is not enough. You are born a writer, after all!

Tak≈æe takto m√¥≈æme robi≈• viac riadkov√© koment√°re.
Thus we also have multi-line comments:

```
contract CryptoZombies {
  /* Toto je koment√°r na niekoƒæko riadkov. Chcel by som ti poƒèakova≈•
    ≈æe si si na≈°iel ƒças na tento program√°torsky kurz. 
    Viem ≈æe je zadarmo, a zadarmo aj nav≈ædy zostane, no aj tak
    sa do neho sna≈æ√≠me da≈• na≈°e srdcia a spravi≈• ho tak dobr√Ω
    ako len m√¥≈æe by≈•.

    Vec, ≈æe toto je staƒ∫e len zaƒçiatok Blockchain v√Ωvoja.
    Dotiahli sme to zatiaƒæ ƒèaleko, no existuje veƒæa sp√¥sobov,
    ako spravi≈• t√∫to komunitu e≈°te lep≈°iu. Ak sme niekde spravili
    chybu, m√¥≈æe≈° n√°m pom√¥c≈• t√Ωm ≈æe otvr√≠≈° nov√Ω pull request tu:
    https://github.com/loomnetwork/cryptozombie-lessons

    Ak m√°≈° nejak√© nov√© n√°pady, koment√°re, alebo chce≈° len pozravi≈•,
    zastav sa v na≈°ej Telegram komunite t https://t.me/loomnetwork
  */
}
```

≈†peci√°lne dobrou praktikou je komentova≈• svoj k√≥d, a vysvetƒæova≈• oƒçak√°van√© spr√°vanie ka≈ædej funkcie tvojho kontraktu. T√Ωm p√°dom in√Ω program√°tor (pr√≠padne ty po 6 mesiacoch) bude schopn√Ω r√Ωchlo vstreba≈• a pochopi≈•, ako sa m√° kontrakt zhruba spr√°va≈• bez toho, aby musel ƒç√≠ta≈• k√≥d.
In particular, it's good practice to comment your code to explain the expected behavior of every function in your contract. This way another developer (or you, after a 6 month hiatus from a project!) can quickly skim and understand at a high level what your code does without having to read the code itself.

≈†tandardn√Ωm sp√¥sob v Solidity komunite je pou≈æ√≠vanie form√°tu zvan√©ho **_natspec_**,, ktor√Ω vyzer√° nejako takto:
The standard in the Solidity community is to use a format called **_natspec_**, which looks like this:

```
/// @title Kontrakt pre z√°kladn√© matematick√© oper√°cie
/// @author H4XF13LD MORRIS üíØüíØüòéüíØüíØ
/// @notice Zatiaƒæ m√° tento kontrakt len sƒç√≠tavanie a n√°sobenie
contract Math {
  /// @notice V√Ωnasob√≠ spolu 2 ƒç√≠sla
  /// @param x je prv√Ω uint.
  /// @param y je druh√Ω uint.
  /// @return z je ich n√°sobok (x * y)
  /// @dev T√°to funkcia moment√°lne nekontroluje preteƒçenie
  function multiply(uint x, uint y) returns (uint z) {
    // Toto je len obyƒçajn√Ω koment√°r, nebude zachyten√Ω natspecom
    z = x * y;
  }
}
```

`@title` (titul) and `@author` s√∫ asi jasn√©.

`@notice` vysvetƒæuje **u≈æ√≠vateƒæovi kontraktu**, ƒço kontrakt / funkcia rob√≠. `@dev` je pre vysvetlenie extra detailov pre program√°torov

`@param` a `@return` s√∫ pre popis toho, ƒço ktor√© parametre a n√°vratov√© hodnoty reprezentuj√∫

V≈°imni si ≈æe nie v≈ædy mus√≠me pou≈æi≈• v≈°etky tieto natspec tagy pre ka≈æd√∫ jednu funkciu - v≈°etky z t√Ωchto tagov su nepovinn√©. Ale je dobr√© zanecha≈• aspo≈à `@dev` pozn√°mku o tom, ƒço dan√° funkcia rob√≠
Note that you don't always have to use all of these tags for every function ‚Äî¬†all tags are optional. But at the very least, leave a `@dev` note explaining what each function does.

## Vysk√∫≈°aj si to s√°m
# Put it to the test

Ak si si e≈°te nev≈°imol, softv√©r kontroluj√∫ci tvoje CryptoZombies rie≈°enia ignoruje koment√°re. Tak≈æe vlastne nem√¥≈æeme skontrolova≈• tvoj natsepc k√≥d na konci tejto kapitoly ;)  
If you haven't noticed by now, the CryptoZombies answer-checker ignores comments when it checks your answers. So we can't actually check your natspec code for this chapter ;)

Ka≈ædop√°dne, v tejto chv√≠li si u≈æ Solidity guru. V tejto lekcii ti budeme preto d√¥verova≈•, ≈æe to zvl√°dne≈°!
However, by now you're a Solidity whiz ‚Äî¬†we're just going to assume you've got this!

Tak ƒçi onak si to v√Ωsk√∫≈°aj, pridaj p√°r natspect tagov pre `ZombieOwnership`:
Give it a try anyway, and try adding some natspec tags to `ZombieOwnership`:

1. `@title` - teda nieƒço ako "Kontrakt ktor√Ω mana≈æuje prevod zombie vlastn√≠ctva"
1. `@title` ‚Äî¬†E.g. A contract that manages transfering zombie ownership

2. `@author` - Tvoje meno!
2. `@author` ‚Äî¬†Your name!

3. `@dev` - napr√≠klad nieƒço ako: "V s√∫lade s n√°vrhom implement√°cioe ERC721 podƒæa OpenZeppelin"
3. `@dev` ‚Äî E.g. Compliant with OpenZeppelin's implementation of the ERC721 spec draft
