---
title: The getLatestEthPrice Function - Cont'd
actions: ['checkAnswer', 'hints']
requireLogin: true
material:
  editor:
    language: sol
    startingCode:
      "oracle/EthPriceOracle.sol": |
        pragma solidity 0.5.0;
        import "openzeppelin-solidity/contracts/ownership/Ownable.sol";
        import "./CallerContractInterface.sol";
        contract EthPriceOracle is Ownable {
          uint private randNonce = 0;
          uint private modulus = 1000;
          mapping(uint256=>bool) pendingRequests;
          event GetLatestEthPriceEvent(address callerAddress, uint id);
          event SetLatestEthPriceEvent(uint256 ethPrice, address callerAddress);
          function getLatestEthPrice() public returns (uint256) {
            randNonce++;
            uint id = uint(keccak256(abi.encodePacked(now, msg.sender, randNonce))) % modulus;

            // Start here
          }
        }
      "caller/CallerContract.sol": |
        pragma solidity 0.5.0;
        import "./EthPriceOracleInterface.sol";
        import "openzeppelin-solidity/contracts/ownership/Ownable.sol";
        contract CallerContract is Ownable {
            uint256 private ethPrice;
            EthPriceOracleInterface private oracleInstance;
            address private oracleAddress;
            mapping(uint256=>bool) myRequests;
            event newOracleAddressEvent(address oracleAddress);
            event ReceivedNewRequestIdEvent(uint256 id);
            event PriceUpdatedEvent(uint256 ethPrice, uint256 id);
            function setOracleInstanceAddress (address _oracleInstanceAddress) public onlyOwner {
              oracleAddress = _oracleInstanceAddress;
              oracleInstance = EthPriceOracleInterface(oracleAddress);
              emit newOracleAddressEvent(oracleAddress);
            }
            function updateEthPrice() public {
              uint256 id = oracleInstance.getLatestEthPrice();
              myRequests[id] = true;
              emit ReceivedNewRequestIdEvent(id);
            }
            function callback(uint256 _ethPrice, uint256 _id) public onlyOracle {
              require(myRequests[_id], "This request is not in my pending list.");
              ethPrice = _ethPrice;
              delete myRequests[_id];
              emit PriceUpdatedEvent(_ethPrice, _id);
            }
            modifier onlyOracle() {
              require(msg.sender == oracleAddress, "You are not authorized to call this function.");
              _;
            }
        }
      "caller/EthPriceOracleInterface.sol": |
        pragma solidity 0.5.0;
        contract EthPriceOracleInterface {
          function getLatestEthPrice() public returns (uint256);
        }
      "oracle/CallerContractInterface.sol": |
        pragma solidity 0.5.0;
        contract CallerContracInterface {
          function callback(uint256 _ethPrice, uint256 id) public;
        }
    answer: |
      pragma solidity 0.5.0;
      import "openzeppelin-solidity/contracts/ownership/Ownable.sol";
      import "./CallerContractInterface.sol";
      contract EthPriceOracle is Ownable {
        uint private randNonce = 0;
        uint private modulus = 1000;
        mapping(uint256=>bool) pendingRequests;
        event GetLatestEthPriceEvent(address callerAddress, uint id);
        event SetLatestEthPriceEvent(uint256 ethPrice, address callerAddress);
        function getLatestEthPrice() public returns (uint256) {
          randNonce++;
          uint id = uint(keccak256(abi.encodePacked(now, msg.sender, randNonce))) % modulus;
          pendingRequests[id] = true;
          emit GetLatestEthPriceEvent(msg.sender, id);
          return id;
        }
      }
---

Great job, you've computed the request `id`.

Next, you would want to implement a simple system that keeps tracks of pending requests. Just like you did for the caller contract, for this you'll be using a mapping. This time let's call it `pendingRequests`.

The `getLatestEthPrice` function should also fire an event and, lastly, it should return the request `id`.

Let's see how you can implement this in code.

## Put It to the Test

We've gone ahead and defined a mapping called `pendingRequests`. The key is a `uint` (the random number) and the value is a `bool`.

1. First, you'll want to change the `pendingRequests` mapping for this `id` to `true`.
2. Let's `emit` `GetLatestEthPriceEvent`. It should pass the following parameters: `msg.sender` (an `address`) and `id` (a `uint256`).
3. Finally, the function should `return id`.
