---
title: Using SafeMath
actions: ['checkAnswer', 'hints']
requireLogin: true
material:
  editor:
    language: JavaScript
    startingCode:
      "oracle/EthPriceOracle.sol": |
        pragma solidity 0.5.0;
        import "openzeppelin-solidity/contracts/access/Roles.sol";
        import "openzeppelin-solidity/contracts/math/SafeMath.sol";
        import "./CallerContractInterface.sol";
        contract EthPriceOracle {
          using Roles for Roles.Role;
          Roles.Role private owners;
          Roles.Role private oracles;
          // 1. Tell your contract to use `SafeMath` for `uint256`
          uint private randNonce = 0;
          uint private modulus = 1000;
          uint private numOracles = 0;
          uint private THRESHOLD = 0;
          mapping(uint256=>bool) pendingRequests;
          struct Response {
            address oracleAddress;
            address callerAddress;
            uint256 ethPrice;
          }
          event GetLatestEthPriceEvent(address callerAddress, uint id);
          event SetLatestEthPriceEvent(uint256 ethPrice, address callerAddress);
          event AddOracleEvent(address oracleAddress);
          event RemoveOracleEvent(address oracleAddress);
          event SetThresholdEvent (uint threshold);
          constructor (address _owner) public {
            owners.add(_owner);
          }
          function addOracle (address _oracle) public {
            require(owners.has(msg.sender), "Not an owner!");
            require(!oracles.has(_oracle), "Already an oracle!");
            oracles.add(_oracle);
            numOracles++;
            emit AddOracleEvent(_oracle);
          }
          function removeOracle (address _oracle) public {
            require(owners.has(msg.sender), "Not an owner!");
            require(oracles.has(_oracle), "Not an oracle!");
            require (numOracles > 1, "Do not remove the last oracle!");
            oracles.remove(_oracle);
            numOracles--;
            emit RemoveOracleEvent(_oracle);
          }
          function setThreshold (uint _threshold) public {
            require(owners.has(msg.sender), "Not an owner!");
            THRESHOLD = _threshold;
            emit SetThresholdEvent(THRESHOLD);
          }
          function getLatestEthPrice() public returns (uint256) {
            randNonce++;
            uint id = uint(keccak256(abi.encodePacked(now, msg.sender, randNonce))) % modulus;
            pendingRequests[id] = true;
            emit GetLatestEthPriceEvent(msg.sender, id);
            return id;
          }
          function setLatestEthPrice(uint256 _ethPrice, address _callerAddress, uint256 _id) public {
            require(oracles.has(msg.sender), "Not an oracle!");
            require(pendingRequests[_id], "This request is not in my pending list.");
            Response memory resp;
            resp = Response(msg.sender, _callerAddress, _ethPrice);
            requestIdToResponse[_id].push(resp);
            uint numResponses = requestIdToResponse[_id].length;
            if (numResponses == THRESHOLD) {
              uint computedEthPrice = 0;
                for (uint f=0; f < requestIdToResponse[_id].length; f++) {
                computedEthPrice +=  requestIdToResponse[_id][f].ethPrice; // Replace this with a `SafeMatch` method
              }
              computedEthPrice = computedEthPrice / numResponses; // Replace this with a `SafeMath` method
              delete pendingRequests[_id];
              CallerContracInterface callerContractInstance;
              callerContractInstance = CallerContracInterface(_callerAddress);
              callerContractInstance.callback(_ethPrice, _id);
              emit SetLatestEthPriceEvent(_ethPrice, _callerAddress);
            }
          }
        }
    answer: |
      pragma solidity 0.5.0;
      import "openzeppelin-solidity/contracts/access/Roles.sol";
      import "openzeppelin-solidity/contracts/math/SafeMath.sol";
      import "./CallerContractInterface.sol";
      contract EthPriceOracle {
        using Roles for Roles.Role;
        Roles.Role private owners;
        Roles.Role private oracles;
        using SafeMath for uint256;
        uint private randNonce = 0;
        uint private modulus = 1000;
        uint private numOracles = 0;
        uint private THRESHOLD = 0;
        mapping(uint256=>bool) pendingRequests;
        struct Response {
          address oracleAddress;
          address callerAddress;
          uint256 ethPrice;
        }
        event GetLatestEthPriceEvent(address callerAddress, uint id);
        event SetLatestEthPriceEvent(uint256 ethPrice, address callerAddress);
        event AddOracleEvent(address oracleAddress);
        event RemoveOracleEvent(address oracleAddress);
        event SetThresholdEvent (uint threshold);
        constructor (address _owner) public {
          owners.add(_owner);
        }
        function addOracle (address _oracle) public {
          require(owners.has(msg.sender), "Not an owner!");
          require(!oracles.has(_oracle), "Already an oracle!");
          oracles.add(_oracle);
          numOracles++;
          emit AddOracleEvent(_oracle);
        }
        function removeOracle (address _oracle) public {
          require(owners.has(msg.sender), "Not an owner!");
          require(oracles.has(_oracle), "Not an oracle!");
          require (numOracles > 1, "Do not remove the last oracle!");
          oracles.remove(_oracle);
          numOracles--;
          emit RemoveOracleEvent(_oracle);
        }
        function setThreshold (uint _threshold) public {
          require(owners.has(msg.sender), "Not an owner!");
          THRESHOLD = _threshold;
          emit SetThresholdEvent(THRESHOLD);
        }
        function getLatestEthPrice() public returns (uint256) {
          randNonce++;
          uint id = uint(keccak256(abi.encodePacked(now, msg.sender, randNonce))) % modulus;
          pendingRequests[id] = true;
          emit GetLatestEthPriceEvent(msg.sender, id);
          return id;
        }
        function setLatestEthPrice(uint256 _ethPrice, address _callerAddress, uint256 _id) public {
          require(oracles.has(msg.sender), "Not an oracle!");
          require(pendingRequests[_id], "This request is not in my pending list.");
          Response memory resp;
          resp = Response(msg.sender, _callerAddress, _ethPrice);
          requestIdToResponse[_id].push(resp);
          uint numResponses = requestIdToResponse[_id].length;
          if (numResponses == THRESHOLD) {
            uint computedEthPrice = 0;
              for (uint f=0; f < requestIdToResponse[_id].length; f++) {
              computedEthPrice = computedEthPrice.add(requestIdToResponse[_id][f].ethPrice);
            }
            computedEthPrice = computedEthPrice.div(numResponses);
            delete pendingRequests[_id];
            CallerContracInterface callerContractInstance;
            callerContractInstance = CallerContracInterface(_callerAddress);
            callerContractInstance.callback(_ethPrice, _id);
            emit SetLatestEthPriceEvent(_ethPrice, _callerAddress);
          }
        }
      }
---

Now let's take some time to discuss security in your contract.

Doing something like `computedEthPrice +=  requestIdToResponse[_id][f].ethPrice;` works well for a large number of oracles but the thing is that `computedEthPrice` can overflow for a very large number of oracles.

First, let's look into what's an overflow.

Suppose you define an `uint8` variable like this:

```Solidity
uint8 exampleOverflow = 255;
```

Because the `exampleOverflow` variable can only have 8 bits, the largest number you can store is `255` (`2^8 -1` or `11111111`).

Now, if you execute the following line of code:

```Solidity
exampleOverflow++;
```

Then, the `exampleOverflow` variable will equal `0`, even if you just increased it. Give it a try if you want toðŸ˜‰.

Similarly, if you subtract `1` from a `uint8` variable whose value is 0, the result will equal `255`. This is called an **underflow**.

Now, the contract is indeed using `uint`, making it highly improbable that'll overflow, but it's a good practice to protect it contract against unexpected behavior.


## The SafeMath Library

For such situations, OpenZepppelin created the `SafeMath` library. Let's look at the code:

```Solidity
pragma solidity ^0.5.0;

/**
 * @dev Wrappers over Solidity's arithmetic operations with added overflow
 * checks.
 *
 * Arithmetic operations in Solidity wrap on overflow. This can easily result
 * in bugs, because programmers usually assume that an overflow raises an
 * error, which is the standard behavior in high level programming languages.
 * `SafeMath` restores this intuition by reverting the transaction when an
 * operation overflows.
 *
 * Using this library instead of the unchecked operations eliminates an entire
 * class of bugs, so it's recommended to use it always.
 */
library SafeMath {
    /**
     * @dev Returns the addition of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `+` operator.
     *
     * Requirements:
     * - Addition cannot overflow.
     */
    function add(uint256 a, uint256 b) internal pure returns (uint256) {
        uint256 c = a + b;
        require(c >= a, "SafeMath: addition overflow");

        return c;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting on
     * overflow (when the result is negative).
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     * - Subtraction cannot overflow.
     */
    function sub(uint256 a, uint256 b) internal pure returns (uint256) {
        return sub(a, b, "SafeMath: subtraction overflow");
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting with custom message on
     * overflow (when the result is negative).
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     * - Subtraction cannot overflow.
     *
     * _Available since v2.4.0._
     */
    function sub(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        require(b <= a, errorMessage);
        uint256 c = a - b;

        return c;
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `*` operator.
     *
     * Requirements:
     * - Multiplication cannot overflow.
     */
    function mul(uint256 a, uint256 b) internal pure returns (uint256) {
        // Gas optimization: this is cheaper than requiring 'a' not being zero, but the
        // benefit is lost if 'b' is also tested.
        // See: https://github.com/OpenZeppelin/openzeppelin-contracts/pull/522
        if (a == 0) {
            return 0;
        }

        uint256 c = a * b;
        require(c / a == b, "SafeMath: multiplication overflow");

        return c;
    }

    /**
     * @dev Returns the integer division of two unsigned integers. Reverts on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator. Note: this function uses a
     * `revert` opcode (which leaves remaining gas untouched) while Solidity
     * uses an invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     * - The divisor cannot be zero.
     */
    function div(uint256 a, uint256 b) internal pure returns (uint256) {
        return div(a, b, "SafeMath: division by zero");
    }

    /**
     * @dev Returns the integer division of two unsigned integers. Reverts with custom message on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator. Note: this function uses a
     * `revert` opcode (which leaves remaining gas untouched) while Solidity
     * uses an invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     * - The divisor cannot be zero.
     *
     * _Available since v2.4.0._
     */
    function div(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        // Solidity only automatically asserts when dividing by 0
        require(b > 0, errorMessage);
        uint256 c = a / b;
        // assert(a == b * c + a % b); // There is no case in which this doesn't hold

        return c;
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * Reverts when dividing by zero.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     * - The divisor cannot be zero.
     */
    function mod(uint256 a, uint256 b) internal pure returns (uint256) {
        return mod(a, b, "SafeMath: modulo by zero");
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * Reverts with custom message when dividing by zero.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     * - The divisor cannot be zero.
     *
     * _Available since v2.4.0._
     */
    function mod(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        require(b != 0, errorMessage);
        return a % b;
    }
}
```

Similarly to what you did in the previous chapters with `Roles`, first you'll have to add the following line of code:

```Solidity
using SafeMath for uint256;
```

Then, you can use SafeMath's functions- `add`, `sub`, `mul`, and `div`:

```Solidity
using SafeMath for uint256;
// now we can use these methods on any uint256
uint256 test = 4;
test = test.div(2); // test now equals 2
test = test.add(5); // test now equals 7
```

## Put It to the Test

We've already imported from the OpenZeppelin's `SafeMath` library for you.

1. First, let's tell your contract to use `SafeMath` for `uint256`. Add the `using SafeMath for uint256;` declaration.
2. Replace `computedEthPrice +=  requestIdToResponse[_id][f].ethPrice;` with a `SafeMath` method.
3. Replace `computedEthPrice = computedEthPrice / numResponses;` with a SafeMath method.
