---
title: Interfész használata
actions: ['checkAnswer', 'hints']
requireLogin: true
material:
  editor:
    language: sol
    startingCode:
      "zombiefeeding.sol": |
        pragma solidity >=0.5.0 <0.6.0;

        import "./zombiefactory.sol";

        contract KittyInterface {
          function getKitty(uint256 _id) external view returns (
            bool isGestating,
            bool isReady,
            uint256 cooldownIndex,
            uint256 nextActionAt,
            uint256 siringWithId,
            uint256 birthTime,
            uint256 matronId,
            uint256 sireId,
            uint256 generation,
            uint256 genes
          );
        }

        contract ZombieFeeding is ZombieFactory {

          address ckAddress = 0x06012c8cf97BEaD5deAe237070F9587f8E7A266d;
          // Inicializáld a kittyContract-ot itt a fenti `ckAddress` használatával

          function feedAndMultiply(uint _zombieId, uint _targetDna) public {
            require(msg.sender == zombieToOwner[_zombieId]);
            Zombie storage myZombie = zombies[_zombieId];
            _targetDna = _targetDna % dnaModulus;
            uint newDna = (myZombie.dna + _targetDna) / 2;
            _createZombie("NoName", newDna);
          }

        }
      "zombiefactory.sol": |
        pragma solidity >=0.5.0 <0.6.0;

        contract ZombieFactory {

            event NewZombie(uint zombieId, string name, uint dna);

            uint dnaDigits = 16;
            uint dnaModulus = 10 ** dnaDigits;

            struct Zombie {
                string name;
                uint dna;
            }

            Zombie[] public zombies;

            mapping (uint => address) public zombieToOwner;
            mapping (address => uint) ownerZombieCount;

            function _createZombie(string memory _name, uint _dna) internal {
                uint id = zombies.push(Zombie(_name, _dna)) - 1;
                zombieToOwner[id] = msg.sender;
                ownerZombieCount[msg.sender]++;
                emit NewZombie(id, _name, _dna);
            }

            function _generateRandomDna(string memory _str) private view returns (uint) {
                uint rand = uint(keccak256(abi.encodePacked(_str)));
                return rand % dnaModulus;
            }

            function createRandomZombie(string memory _name) public {
                require(ownerZombieCount[msg.sender] == 0);
                uint randDna = _generateRandomDna(_name);
                _createZombie(_name, randDna);
            }

        }
    answer: >
      pragma solidity >=0.5.0 <0.6.0;

      import "./zombiefactory.sol";

      contract KittyInterface {
        function getKitty(uint256 _id) external view returns (
          bool isGestating,
          bool isReady,
          uint256 cooldownIndex,
          uint256 nextActionAt,
          uint256 siringWithId,
          uint256 birthTime,
          uint256 matronId,
          uint256 sireId,
          uint256 generation,
          uint256 genes
        );
      }

      contract ZombieFeeding is ZombieFactory {

        address ckAddress = 0x06012c8cf97BEaD5deAe237070F9587f8E7A266d;
        KittyInterface kittyContract = KittyInterface(ckAddress);

        function feedAndMultiply(uint _zombieId, uint _targetDna) public {
          require(msg.sender == zombieToOwner[_zombieId]);
          Zombie storage myZombie = zombies[_zombieId];
          _targetDna = _targetDna % dnaModulus;
          uint newDna = (myZombie.dna + _targetDna) / 2;
          _createZombie("NoName", newDna);
        }

      }
---

Folytatva az előző példánkat a `NumberInterface`-szel, miután definiáltuk az interfészt így:

```
contract NumberInterface {
  function getNum(address _myAddress) public view returns (uint);
}
```

Használhatjuk egy szerződésben így:

```
contract MyContract {
  address NumberInterfaceAddress = 0xab38...
  // ^ A FavoriteNumber szerződés címe az Ethereumon
  NumberInterface numberContract = NumberInterface(NumberInterfaceAddress);
  // Most a `numberContract` a másik szerződésre mutat

  function someFunction() public {
    // Most meghívhatjuk a `getNum`-ot abból a szerződésből:
    uint num = numberContract.getNum(msg.sender);
    // ...és csinálhatunk valamit a `num`-mal itt
  }
}
```

Így a szerződésed interaktálhat bármely más szerződéssel az Ethereum blockchainen, amíg azok `public` vagy `external` függvényekként teszik elérhetővé ezeket a függvényeket.

# Tegyük próbára

Állítsuk be a szerződésünket, hogy olvasson a CryptoKitties okos szerződésből!

1. Elmentettem a CryptoKitties szerződés címét a kódban neked, egy `ckAddress` nevű változóban. A következő sorban hozz létre egy `KittyInterface`-t `kittyContract` néven, és inicializáld a `ckAddress`-szel — pont úgy, ahogy a fenti `numberContract`-tal csináltuk.
