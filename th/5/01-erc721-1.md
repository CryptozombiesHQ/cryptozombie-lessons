---
title: Tokens on Ethereum
actions: ['checkAnswer', 'hints']
requireLogin: true
material:
  editor:
    language: sol
    startingCode:
      "zombieownership.sol": |
        // เริ่มโค้ดตรงนี้
      "zombieattack.sol": |
        pragma solidity ^0.4.19;

        import "./zombiehelper.sol";

        contract ZombieAttack is ZombieHelper {
          uint randNonce = 0;
          uint attackVictoryProbability = 70;

          function randMod(uint _modulus) internal returns(uint) {
            randNonce++;
            return uint(keccak256(now, msg.sender, randNonce)) % _modulus;
          }

          function attack(uint _zombieId, uint _targetId) external ownerOf(_zombieId) {
            Zombie storage myZombie = zombies[_zombieId];
            Zombie storage enemyZombie = zombies[_targetId];
            uint rand = randMod(100);
            if (rand <= attackVictoryProbability) {
              myZombie.winCount++;
              myZombie.level++;
              enemyZombie.lossCount++;
              feedAndMultiply(_zombieId, enemyZombie.dna, "zombie");
            } else {
              myZombie.lossCount++;
              enemyZombie.winCount++;
              _triggerCooldown(myZombie);
            }
          }
        }
      "zombiehelper.sol": |
        pragma solidity ^0.4.19;

        import "./zombiefeeding.sol";

        contract ZombieHelper is ZombieFeeding {

          uint levelUpFee = 0.001 ether;

          modifier aboveLevel(uint _level, uint _zombieId) {
            require(zombies[_zombieId].level >= _level);
            _;
          }

          function withdraw() external onlyOwner {
            owner.transfer(this.balance);
          }

          function setLevelUpFee(uint _fee) external onlyOwner {
            levelUpFee = _fee;
          }

          function levelUp(uint _zombieId) external payable {
            require(msg.value == levelUpFee);
            zombies[_zombieId].level++;
          }

          function changeName(uint _zombieId, string _newName) external aboveLevel(2, _zombieId) ownerOf(_zombieId) {
            zombies[_zombieId].name = _newName;
          }

          function changeDna(uint _zombieId, uint _newDna) external aboveLevel(20, _zombieId) ownerOf(_zombieId) {
            zombies[_zombieId].dna = _newDna;
          }

          function getZombiesByOwner(address _owner) external view returns(uint[]) {
            uint[] memory result = new uint[](ownerZombieCount[_owner]);
            uint counter = 0;
            for (uint i = 0; i < zombies.length; i++) {
              if (zombieToOwner[i] == _owner) {
                result[counter] = i;
                counter++;
              }
            }
            return result;
          }

        }
      "zombiefeeding.sol": |
        pragma solidity ^0.4.19;

        import "./zombiefactory.sol";

        contract KittyInterface {
          function getKitty(uint256 _id) external view returns (
            bool isGestating,
            bool isReady,
            uint256 cooldownIndex,
            uint256 nextActionAt,
            uint256 siringWithId,
            uint256 birthTime,
            uint256 matronId,
            uint256 sireId,
            uint256 generation,
            uint256 genes
          );
        }

        contract ZombieFeeding is ZombieFactory {

          KittyInterface kittyContract;

          modifier ownerOf(uint _zombieId) {
            require(msg.sender == zombieToOwner[_zombieId]);
            _;
          }

          function setKittyContractAddress(address _address) external onlyOwner {
            kittyContract = KittyInterface(_address);
          }

          function _triggerCooldown(Zombie storage _zombie) internal {
            _zombie.readyTime = uint32(now + cooldownTime);
          }

          function _isReady(Zombie storage _zombie) internal view returns (bool) {
              return (_zombie.readyTime <= now);
          }

          function feedAndMultiply(uint _zombieId, uint _targetDna, string _species) internal ownerOf(_zombieId) {
            Zombie storage myZombie = zombies[_zombieId];
            require(_isReady(myZombie));
            _targetDna = _targetDna % dnaModulus;
            uint newDna = (myZombie.dna + _targetDna) / 2;
            if (keccak256(_species) == keccak256("kitty")) {
              newDna = newDna - newDna % 100 + 99;
            }
            _createZombie("NoName", newDna);
            _triggerCooldown(myZombie);
          }

          function feedOnKitty(uint _zombieId, uint _kittyId) public {
            uint kittyDna;
            (,,,,,,,,,kittyDna) = kittyContract.getKitty(_kittyId);
            feedAndMultiply(_zombieId, kittyDna, "kitty");
          }
        }
      "zombiefactory.sol": |
        pragma solidity ^0.4.19;

        import "./ownable.sol";

        contract ZombieFactory is Ownable {

            event NewZombie(uint zombieId, string name, uint dna);

            uint dnaDigits = 16;
            uint dnaModulus = 10 ** dnaDigits;
            uint cooldownTime = 1 days;

            struct Zombie {
              string name;
              uint dna;
              uint32 level;
              uint32 readyTime;
              uint16 winCount;
              uint16 lossCount;
            }

            Zombie[] public zombies;

            mapping (uint => address) public zombieToOwner;
            mapping (address => uint) ownerZombieCount;

            function _createZombie(string _name, uint _dna) internal {
                uint id = zombies.push(Zombie(_name, _dna, 1, uint32(now + cooldownTime), 0, 0)) - 1;
                zombieToOwner[id] = msg.sender;
                ownerZombieCount[msg.sender]++;
                NewZombie(id, _name, _dna);
            }

            function _generateRandomDna(string _str) private view returns (uint) {
                uint rand = uint(keccak256(_str));
                return rand % dnaModulus;
            }

            function createRandomZombie(string _name) public {
                require(ownerZombieCount[msg.sender] == 0);
                uint randDna = _generateRandomDna(_name);
                randDna = randDna - randDna % 100;
                _createZombie(_name, randDna);
            }

        }
      "ownable.sol": |
        /**
         * @title Ownable
         * @dev The Ownable contract has an owner address, and provides basic authorization control
         * functions, this simplifies the implementation of "user permissions".
         */
        contract Ownable {
          address public owner;

          event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

          /**
           * @dev The Ownable constructor sets the original `owner` of the contract to the sender
           * account.
           */
          function Ownable() public {
            owner = msg.sender;
          }


          /**
           * @dev Throws if called by any account other than the owner.
           */
          modifier onlyOwner() {
            require(msg.sender == owner);
            _;
          }


          /**
           * @dev Allows the current owner to transfer control of the contract to a newOwner.
           * @param newOwner The address to transfer ownership to.
           */
          function transferOwnership(address newOwner) public onlyOwner {
            require(newOwner != address(0));
            OwnershipTransferred(owner, newOwner);
            owner = newOwner;
          }

        }
    answer: |
      pragma solidity ^0.4.19;

      import "./zombieattack.sol";
      
      contract ZombieOwnership is ZombieAttack {

      }
---

มากล่าวถึง ***tokens*** กันบ้าง

ถ้าหากว่าคุ้นเคยกับการใช้ Ethereum space มาสักพัก เราอาจจะเคยได้ยินเกี่ยวกับ tokens กันมาบ้าง — หากเจาะลึกลงมาอีกหน่อย มันก็คือ ***ERC20 tokens*** นั่นเอง

***token*** บน Ethereum จริงๆ แล้วก็คือ smart contract ที่เป็นไปตามกฏทั่วๆ ไป — เป็นการ implement ชุดฟังก์ชั่นพื้นฐานที่รับมาจาก token contracts อื่นๆ เช่น `transfer(address _to, uint256 _value)` และ `balanceOf(address _owner)` เป็นต้น

ภายใน smart contract มักจะมีการ mapping `mapping(address => uint256) balances` ที่จะเป็นต้วเก็บข้อมูลว่าแต่ละ address มียอดคงเหลืออยู่เท่าไหร่

ดังนั้นกล่าวโดยทั่วไปแล้ว token นั้นก็คือ contract ที่เก็บข้อมูลว่าผู้ใช้แต่ละท่านมี that มากน้อยเท่าไหร่ และยังเก็บฟังก์ชั่นบางอย่างให้ผู้ใช้สามารถโอน tokens ไปยัง address อื่นๆ ได้อีกด้วย

### แล้วมันสำคัญยังไงกันนะ?

เมื่อทุก ERC20 token ได้แบ่งปันชุดฟังก์ชั่นเดียวกันโดยใช้ชื่อเดียวกัน มันจึงสามารถมีรูปแบบของ interaction ในรูปแบบเดียวกันได้

นั่นแปลว่าหากเราได้ทำการสร้างแอพพลิเคชั่นที่สามารถมี interaction กับ ERC20 token เพียงอันเดียวได้แล้วล่ะก็ มันก็จะสามารถ interact กับ ERC20 token ใด ๆ ก็ได้เช่นกัน วิธีนี้จะทำให้เราไม่จำเป็นต้องเปลี่ยนแปลงโค้ดใดๆ หากเราอยากจะเพิ่ม token เข้าไปยังแอพพลิเคชั่นของเราในอนาคต สิ่งที่ต้องทำคือเพียงแค่ plug in ส่วนของ token contract address เข้าไปเท่านั้น แอพพลิเคชั่นของเราก็จะมีอีก token หนึ่งให้ใช้แล้ว

ตัวอย่างที่เห็นได้ชัดก็คือเรื่องของการแลกเปลี่ยน หรือ exchange กล่าวคือเมื่อ exchange ได้เพิ่ม ERC20 token ใหม่เข้ามา สิ่งที่ต้องทำหลังจากนั้นคือการแค่เพิ่ม smart contract อีกอันที่มันต้องการสื่อสารด้วยเข้ามา โดยผู้ใช้สามารถบอก contract ดังกล่าวให้ส่ง token ไปยัง wallet address ของผู้ที่ต้องการจะแลกเปลี่ยนด้วย และเมื่อใดก็ตามที่ผู้ใช้ของเราต้องการถอน token ออกมา ผู้ที่ทำการแลกเปลี่ยนก็ยังจะสามารถบอก contract ให้ส่ง token กลับมายังผู้ใช้ได้อีกเช่นกัน

ผู้ที่ทำการแลกเปลี่ยนเพียงต้องทำการโค้ด transfer logic นี้เข้าไปแค่รอบเดียว จากนั้นหากต้องการเพิ่ม ERC20 token ใหม่ สิ่งที่ต้องมาจัดการจึงเป็นเพียงแค่เรื่องของการเพิ่ม contract address ใหม่ไปยัง database เท่านั้น

### มาตรฐานของ token อื่นๆ

ERC20 token นั้นเจ๋งมากๆ ในหมวดหมู่ของ token ที่ทำหน้าที่ในเรื่องของการเป็น currency ได้ อย่างไรก็ตาม ERC20 token ก็ยังมีส่วนที่ไม่มีประโยชน์มากนักในแง่ของการเป็นตัวแทนของซอมบี้ในเกมนี้

เหตุผลแรกก็คือเจ้าซอมบี้นั้นไม่ได้แยกร่างเป็นหน่วยได้เหมือนเงินเสียหน่อย — เช่น เราสามารถโอนเงินจำนวน 0.237 ETH ได้แน่นอน แต่หากบอกว่าจะโอนซอมบี้ไป 0.237 ตัวนั้นก็คงฟังดูพิลึกๆ ใช่มั้ยล่ะ

เหตุผลอย่างที่สองก็คือ จำนวนซอมบี้นั้นไม่ได้ถูกสร้างขึ้นมาอย่างเท่าเทียมกันทั้งหมด เช่น เจ้าซอมบี้เลเวล 2 ชื่อว่า "**Steve**" จะไม่ได้มีมูลค่าเท่ากับซอมบี้เลเวล 732 ที่ชื่อ "**H4XF13LD MORRIS 💯💯😎💯💯**" แน่นอน (ไม่ใกล้เคียงเลยด้วยซ้ำนะเจ้า *Steve*)

ยังมี token standard อื่นๆ ที่เหมาะสมมากกว่าทีจะใช้กับ crypto-collectibles ดังเช่น CryptoZombies ของเรา — token standard เหล่านั้นก็คือ ***ERC721 tokens.***

แต่ละ ***ERC721 tokens*** นั้น **ไม่สามารถ** นำมาใช้แทนกันได้ เพราะแต่ละองค์ประกอบของมันล้วนแล้วแต่มีเอกลักษณ์ของตัวเอง นอกจากนี้ยังไม่สามารถแยกร่างได้อีกด้วย ซึ่งเราจะต้องแลกเปลี่ยนมันทั้งหมดเท่านั้น และแต่ละ token ก็จะมี ID ที่ไม่ซ้ำกันเลย นี่จึงเป็นเหตุผลที่ _ERC721 token เหมาะจะถูกนำมาใช้ในการสร้าง zombie ที่สามารถ trade ได้นั่นเอง

> จำไว้ว่าการใช้ ERC721 มีข้อดีในเรื่องของการที่เราไม่ต้องโค้ด auction หรือ escrow logic ลงภายใน contract ในการคิดวิธี trade / sell ซอมบี้เลยแม้แต่น้อย โดยหากเราสามารถยืนยันสเปคได้ ผู้อื่นก็จะสามารถสร้าง exchange platform ขึ้นมาสำหรับ crypto-tradable ERC721 assets และ ERC721 zombies ก็จะสามารถใช้การได้ใน platform นั้นทันที ซึี่งนี่ก็เป็นข้อดีที่ชัดเจนมากๆ ในการใช้ token standard แทนที่จะมานั่งคิด trading logic ของเราเอง

## มาทดสอบกันเลยจ้า

สัญญาว่าเราจะมาโค้ด ERC721 กันในบทถัดไปแน่นอน แต่ก่อนอื่นมาสร้าง file structure สำหรับบทนี้กันก่อนดีกว่า

ซึ่ง ERC721 logic จะต้องถูกเก็บลงใน contract ที่ชื่อ `ZombieOwnership`

1. ประกาศเวอร์ชั่น `pragma` ตรงด้านบนสุดของไฟล์ก่อนเป็นอันดับแรก  (ให้กลับไปดูในเรื่อง file ของบทก่อนหน้าเกี่ยวกับเรื่องของ syntax ต่างๆ)

2. ไฟล์นี้จะต้องมีการ `import` จาก `zombieattack.sol`

3. ประกาศ contract ใหม่ที่ชื่อ `ZombieOwnership` ที่ inherit มาจาก `ZombieAttack` ปล่อยว่างส่วน body ของ contract เอาไว้ก่อน
