---
title: –ö–æ–º–º–µ–Ω—Ç–∞—Ä–∏–∏
actions: ['checkAnswer', 'hints']
requireLogin: true
material:
  editor:
    language: sol
    startingCode:
      "zombieownership.sol": |
        pragma solidity ^0.4.25;

        import "./zombieattack.sol";
        import "./erc721.sol";
        import "./safemath.sol";

        /// TODO: –ó–∞–º–µ–Ω–∏—Ç–µ —ç—Ç–æ –Ω–∞ natspec –æ–ø–∏—Å–∞–Ω–∏—è
        contract ZombieOwnership is ZombieAttack, ERC721 {

          using SafeMath for uint256;

          mapping (uint => address) zombieApprovals;

          function balanceOf(address _owner) external view returns (uint256) {
            return ownerZombieCount[_owner];
          }

          function ownerOf(uint256 _tokenId) external view returns (address) {
            return zombieToOwner[_tokenId];
          }

          function _transfer(address _from, address _to, uint256 _tokenId) private {
            ownerZombieCount[_to] = ownerZombieCount[_to].add(1);
            ownerZombieCount[msg.sender] = ownerZombieCount[msg.sender].sub(1);
            zombieToOwner[_tokenId] = _to;
            emit Transfer(_from, _to, _tokenId);
          }

          function transferFrom(address _from, address _to, uint256 _tokenId) external payable {
            require (zombieToOwner[_tokenId] == msg.sender || zombieApprovals[_tokenId] == msg.sender);
            _transfer(_from, _to, _tokenId);
          }

          function approve(address _approved, uint256 _tokenId) external payable onlyOwnerOf(_tokenId) {
            zombieApprovals[_tokenId] = _approved;
            emit Approval(msg.sender, _approved, _tokenId);
          }

        }
      "zombieattack.sol": |
        pragma solidity ^0.4.25;

        import "./zombiehelper.sol";

        contract ZombieAttack is ZombieHelper {
          uint randNonce = 0;
          uint attackVictoryProbability = 70;

          function randMod(uint _modulus) internal returns(uint) {
            randNonce = randNonce.add(1);
            return uint(keccak256(abi.encodePacked(now, msg.sender, randNonce))) % _modulus;
          }

          function attack(uint _zombieId, uint _targetId) external onlyOwnerOf(_zombieId) {
            Zombie storage myZombie = zombies[_zombieId];
            Zombie storage enemyZombie = zombies[_targetId];
            uint rand = randMod(100);
            if (rand <= attackVictoryProbability) {
              myZombie.winCount = myZombie.winCount.add(1);
              myZombie.level = myZombie.level.add(1);
              enemyZombie.lossCount = enemyZombie.lossCount.add(1);
              feedAndMultiply(_zombieId, enemyZombie.dna, "zombie");
            } else {
              myZombie.lossCount = myZombie.lossCount.add(1);
              enemyZombie.winCount = enemyZombie.winCount.add(1);
              _triggerCooldown(myZombie);
            }
          }
        }
      "zombiehelper.sol": |
        pragma solidity ^0.4.25;

        import "./zombiefeeding.sol";

        contract ZombieHelper is ZombieFeeding {

          uint levelUpFee = 0.001 ether;

          modifier aboveLevel(uint _level, uint _zombieId) {
            require(zombies[_zombieId].level >= _level);
            _;
          }

          function withdraw() external onlyOwner {
            address _owner = owner();
            _owner.transfer(address(this).balance);
          }

          function setLevelUpFee(uint _fee) external onlyOwner {
            levelUpFee = _fee;
          }

          function levelUp(uint _zombieId) external payable {
            require(msg.value == levelUpFee);
            zombies[_zombieId].level = zombies[_zombieId].level.add(1);
          }

          function changeName(uint _zombieId, string _newName) external aboveLevel(2, _zombieId) onlyOwnerOf(_zombieId) {
            zombies[_zombieId].name = _newName;
          }

          function changeDna(uint _zombieId, uint _newDna) external aboveLevel(20, _zombieId) onlyOwnerOf(_zombieId) {
            zombies[_zombieId].dna = _newDna;
          }

          function getZombiesByOwner(address _owner) external view returns(uint[]) {
            uint[] memory result = new uint[](ownerZombieCount[_owner]);
            uint counter = 0;
            for (uint i = 0; i < zombies.length; i++) {
              if (zombieToOwner[i] == _owner) {
                result[counter] = i;
                counter++;
              }
            }
            return result;
          }

        }
      "zombiefeeding.sol": |
        pragma solidity ^0.4.25;

        import "./zombiefactory.sol";

        contract KittyInterface {
          function getKitty(uint256 _id) external view returns (
            bool isGestating,
            bool isReady,
            uint256 cooldownIndex,
            uint256 nextActionAt,
            uint256 siringWithId,
            uint256 birthTime,
            uint256 matronId,
            uint256 sireId,
            uint256 generation,
            uint256 genes
          );
        }

        contract ZombieFeeding is ZombieFactory {

          KittyInterface kittyContract;

          modifier onlyOwnerOf(uint _zombieId) {
            require(msg.sender == zombieToOwner[_zombieId]);
            _;
          }

          function setKittyContractAddress(address _address) external onlyOwner {
            kittyContract = KittyInterface(_address);
          }

          function _triggerCooldown(Zombie storage _zombie) internal {
            _zombie.readyTime = uint32(now + cooldownTime);
          }

          function _isReady(Zombie storage _zombie) internal view returns (bool) {
              return (_zombie.readyTime <= now);
          }

          function feedAndMultiply(uint _zombieId, uint _targetDna, string _species) internal onlyOwnerOf(_zombieId) {
            Zombie storage myZombie = zombies[_zombieId];
            require(_isReady(myZombie));
            _targetDna = _targetDna % dnaModulus;
            uint newDna = (myZombie.dna + _targetDna) / 2;
            if (keccak256(abi.encodePacked(_species)) == keccak256(abi.encodePacked("kitty"))) {
              newDna = newDna - newDna % 100 + 99;
            }
            _createZombie("NoName", newDna);
            _triggerCooldown(myZombie);
          }

          function feedOnKitty(uint _zombieId, uint _kittyId) public {
            uint kittyDna;
            (,,,,,,,,,kittyDna) = kittyContract.getKitty(_kittyId);
            feedAndMultiply(_zombieId, kittyDna, "kitty");
          }
        }
      "zombiefactory.sol": |
        pragma solidity ^0.4.25;

        import "./ownable.sol";
        import "./safemath.sol";

        contract ZombieFactory is Ownable {

          using SafeMath for uint256;
          using SafeMath32 for uint32;
          using SafeMath16 for uint16;

          event NewZombie(uint zombieId, string name, uint dna);

          uint dnaDigits = 16;
          uint dnaModulus = 10 ** dnaDigits;
          uint cooldownTime = 1 days;

          struct Zombie {
            string name;
            uint dna;
            uint32 level;
            uint32 readyTime;
            uint16 winCount;
            uint16 lossCount;
          }

          Zombie[] public zombies;

          mapping (uint => address) public zombieToOwner;
          mapping (address => uint) ownerZombieCount;

          function _createZombie(string _name, uint _dna) internal {
            uint id = zombies.push(Zombie(_name, _dna, 1, uint32(now + cooldownTime), 0, 0)) - 1;
            zombieToOwner[id] = msg.sender;
            ownerZombieCount[msg.sender] = ownerZombieCount[msg.sender].add(1);
            emit NewZombie(id, _name, _dna);
          }

          function _generatePseudoRandomDna(string _str) private view returns (uint) {
            uint rand = uint(keccak256(abi.encodePacked(_str)));
            return rand % dnaModulus;
          }

          function createPseudoRandomZombie(string _name) public {
            require(ownerZombieCount[msg.sender] == 0);
            uint randDna = _generatePseudoRandomDna(_name);
            randDna = randDna - randDna % 100;
            _createZombie(_name, randDna);
          }

        }
      "ownable.sol": |
        pragma solidity ^0.4.25;

        /**
        * @title Ownable
        * @dev The Ownable contract has an owner address, and provides basic authorization control
        * functions, this simplifies the implementation of "user permissions".
        */
        contract Ownable {
          address private _owner;

          event OwnershipTransferred(
            address indexed previousOwner,
            address indexed newOwner
          );

          /**
          * @dev The Ownable constructor sets the original `owner` of the contract to the sender
          * account.
          */
          constructor() internal {
            _owner = msg.sender;
            emit OwnershipTransferred(address(0), _owner);
          }

          /**
          * @return the address of the owner.
          */
          function owner() public view returns(address) {
            return _owner;
          }

          /**
          * @dev Throws if called by any account other than the owner.
          */
          modifier onlyOwner() {
            require(isOwner());
            _;
          }

          /**
          * @return true if `msg.sender` is the owner of the contract.
          */
          function isOwner() public view returns(bool) {
            return msg.sender == _owner;
          }

          /**
          * @dev Allows the current owner to relinquish control of the contract.
          * @notice Renouncing to ownership will leave the contract without an owner.
          * It will not be possible to call the functions with the `onlyOwner`
          * modifier anymore.
          */
          function renounceOwnership() public onlyOwner {
            emit OwnershipTransferred(_owner, address(0));
            _owner = address(0);
          }

          /**
          * @dev Allows the current owner to transfer control of the contract to a newOwner.
          * @param newOwner The address to transfer ownership to.
          */
          function transferOwnership(address newOwner) public onlyOwner {
            _transferOwnership(newOwner);
          }

          /**
          * @dev Transfers control of the contract to a newOwner.
          * @param newOwner The address to transfer ownership to.
          */
          function _transferOwnership(address newOwner) internal {
            require(newOwner != address(0));
            emit OwnershipTransferred(_owner, newOwner);
            _owner = newOwner;
          }
        }
      "safemath.sol": |
        pragma solidity ^0.4.25;

        /**
         * @title SafeMath
         * @dev Math operations with safety checks that throw on error
         */
        library SafeMath {

          /**
          * @dev Multiplies two numbers, throws on overflow.
          */
          function mul(uint256 a, uint256 b) internal pure returns (uint256) {
            if (a == 0) {
              return 0;
            }
            uint256 c = a * b;
            assert(c / a == b);
            return c;
          }

          /**
          * @dev Integer division of two numbers, truncating the quotient.
          */
          function div(uint256 a, uint256 b) internal pure returns (uint256) {
            // assert(b > 0); // Solidity automatically throws when dividing by 0
            uint256 c = a / b;
            // assert(a == b * c + a % b); // There is no case in which this doesn't hold
            return c;
          }

          /**
          * @dev Subtracts two numbers, throws on overflow (i.e. if subtrahend is greater than minuend).
          */
          function sub(uint256 a, uint256 b) internal pure returns (uint256) {
            assert(b <= a);
            return a - b;
          }

          /**
          * @dev Adds two numbers, throws on overflow.
          */
          function add(uint256 a, uint256 b) internal pure returns (uint256) {
            uint256 c = a + b;
            assert(c >= a);
            return c;
          }
        }

        /**
         * @title SafeMath32
         * @dev SafeMath library implemented for uint32
         */
        library SafeMath32 {

          function mul(uint32 a, uint32 b) internal pure returns (uint32) {
            if (a == 0) {
              return 0;
            }
            uint32 c = a * b;
            assert(c / a == b);
            return c;
          }

          function div(uint32 a, uint32 b) internal pure returns (uint32) {
            // assert(b > 0); // Solidity automatically throws when dividing by 0
            uint32 c = a / b;
            // assert(a == b * c + a % b); // There is no case in which this doesn't hold
            return c;
          }

          function sub(uint32 a, uint32 b) internal pure returns (uint32) {
            assert(b <= a);
            return a - b;
          }

          function add(uint32 a, uint32 b) internal pure returns (uint32) {
            uint32 c = a + b;
            assert(c >= a);
            return c;
          }
        }

        /**
         * @title SafeMath16
         * @dev SafeMath library implemented for uint16
         */
        library SafeMath16 {

          function mul(uint16 a, uint16 b) internal pure returns (uint16) {
            if (a == 0) {
              return 0;
            }
            uint16 c = a * b;
            assert(c / a == b);
            return c;
          }

          function div(uint16 a, uint16 b) internal pure returns (uint16) {
            // assert(b > 0); // Solidity automatically throws when dividing by 0
            uint16 c = a / b;
            // assert(a == b * c + a % b); // There is no case in which this doesn't hold
            return c;
          }

          function sub(uint16 a, uint16 b) internal pure returns (uint16) {
            assert(b <= a);
            return a - b;
          }

          function add(uint16 a, uint16 b) internal pure returns (uint16) {
            uint16 c = a + b;
            assert(c >= a);
            return c;
          }
        }
      "erc721.sol": |
        pragma solidity ^0.4.25;

        contract ERC721 {
          event Transfer(address indexed _from, address indexed _to, uint256 indexed _tokenId);
          event Approval(address indexed _owner, address indexed _approved, uint256 indexed _tokenId);

          function balanceOf(address _owner) external view returns (uint256);
          function ownerOf(uint256 _tokenId) external view returns (address);
          function transferFrom(address _from, address _to, uint256 _tokenId) external payable;
          function approve(address _approved, uint256 _tokenId) external payable;
        }
    answer: |
      pragma solidity ^0.4.25;

      import "./zombieattack.sol";
      import "./erc721.sol";
      import "./safemath.sol";

      contract ZombieOwnership is ZombieAttack, ERC721 {

        using SafeMath for uint256;

        mapping (uint => address) zombieApprovals;

        function balanceOf(address _owner) external view returns (uint256) {
          return ownerZombieCount[_owner];
        }

        function ownerOf(uint256 _tokenId) external view returns (address) {
          return zombieToOwner[_tokenId];
        }

        function _transfer(address _from, address _to, uint256 _tokenId) private {
          ownerZombieCount[_to] = ownerZombieCount[_to].add(1);
          ownerZombieCount[msg.sender] = ownerZombieCount[msg.sender].sub(1);
          zombieToOwner[_tokenId] = _to;
          emit Transfer(_from, _to, _tokenId);
        }

        function transferFrom(address _from, address _to, uint256 _tokenId) external payable {
            require (zombieToOwner[_tokenId] == msg.sender || zombieApprovals[_tokenId] == msg.sender);
            _transfer(_from, _to, _tokenId);
          }

        function approve(address _approved, uint256 _tokenId) external payable onlyOwnerOf(_tokenId) {
            zombieApprovals[_tokenId] = _approved;
            emit Approval(msg.sender, _approved, _tokenId);
          }

      }
---

–ö–æ–¥ Solidity –¥–ª—è –Ω–∞—à–µ–π –∏–≥—Ä—ã –ø—Ä–æ –∑–æ–º–±–∏ –Ω–∞–∫–æ–Ω–µ—Ü-—Ç–æ –∑–∞–∫–æ–Ω—á–µ–Ω!

–í —Å–ª–µ–¥—É—é—â–∏—Ö —É—Ä–æ–∫–∞—Ö –º—ã —Ä–∞—Å—Å–º–æ—Ç—Ä–∏–º, –∫–∞–∫ —Ä–∞–∑–≤–µ—Ä–Ω—É—Ç—å –∫–æ–¥ –≤ Ethereum –∏ –∫–∞–∫ –≤–∑–∞–∏–º–æ–¥–µ–π—Å—Ç–≤–æ–≤–∞—Ç—å —Å –Ω–∏–º —Å –ø–æ–º–æ—â—å—é Web3.js.

–ù–∞–ø–æ—Å–ª–µ–¥–æ–∫, –ø—Ä–µ–∂–¥–µ —á–µ–º –º—ã –æ—Ç–ø—É—Å—Ç–∏–º –≤–∞—Å –Ω–∞ —É—Ä–æ–∫–µ 5: –¥–∞–≤–∞–π—Ç–µ –ø–æ–≥–æ–≤–æ—Ä–∏–º –æ **–∫–æ–º–º–µ–Ω—Ç–∏—Ä–æ–≤–∞–Ω–∏–∏ –≤–∞—à–µ–≥–æ –∫–æ–¥–∞**.

## –°–∏–Ω—Ç–∞–∫—Å–∏—Å –∫–æ–º–º–µ–Ω—Ç–∞—Ä–∏–µ–≤

–ö–æ–º–º–µ–Ω—Ç–∏—Ä–æ–≤–∞–Ω–∏–µ –≤ Solidity –ø–æ—Ö–æ–∂–µ –Ω–∞ JavaScript. –í—ã —É–∂–µ –≤–∏–¥–µ–ª–∏ –Ω–µ—Å–∫–æ–ª—å–∫–æ –ø—Ä–∏–º–µ—Ä–æ–≤ –æ–¥–Ω–æ—Å—Ç—Ä–æ—á–Ω—ã—Ö –∫–æ–º–º–µ–Ω—Ç–∞—Ä–∏–µ–≤ –Ω–∞ —É—Ä–æ–∫–∞—Ö CryptoZombies:

```
// –≠—Ç–æ –æ–¥–Ω–æ—Å—Ç—Ä–æ—á–Ω—ã–π –∫–æ–º–º–µ–Ω—Ç–∞—Ä–∏–π. –≠—Ç–æ –∫–∞–∫ –∑–∞–º–µ—Ç–∫–∞ –¥–ª—è —Å–µ–±—è (–∏–ª–∏ –¥–ª—è –¥—Ä—É–≥–∏—Ö)
```

–ü—Ä–æ—Å—Ç–æ –¥–æ–±–∞–≤—å—Ç–µ –¥–≤–æ–π–Ω–æ–π `//` –≥–¥–µ —É–≥–æ–¥–Ω–æ, –∏ –≤—ã –∫–æ–º–º–µ–Ω—Ç–∏—Ä—É–µ—Ç–µ. –≠—Ç–æ —Ç–∞–∫ –ª–µ–≥–∫–æ, —á—Ç–æ –í—ã –¥–æ–ª–∂–Ω—ã –¥–µ–ª–∞—Ç—å —ç—Ç–æ –≤—Å–µ –≤—Ä–µ–º—è.

–ù–æ —è –≤–∞—Å —Å–ª—ã—à—É ‚Äî –∏–Ω–æ–≥–¥–∞ –æ–¥–Ω–æ–π —Å—Ç—Ä–æ–∫–∏ –Ω–µ–¥–æ—Å—Ç–∞—Ç–æ—á–Ω–æ. –í—ã –ø—Ä–∏—Ä–æ–∂–¥–µ–Ω–Ω—ã–π –ø–∏—Å–∞—Ç–µ–ª—å, –≤ –∫–æ–Ω—Ü–µ –∫–æ–Ω—Ü–æ–≤!

–ü–æ—ç—Ç–æ–º—É, —É –Ω–∞—Å —Ç–∞–∫–∂–µ –µ—Å—Ç—å –º–Ω–æ–≥–æ—Å—Ç—Ä–æ—á–Ω—ã–µ –∫–æ–º–º–µ–Ω—Ç–∞—Ä–∏–∏:

```
contract CryptoZombies {
  /* –≠—Ç–æ –º–Ω–æ–≥–æ—Å—Ç—Ä–æ—á–Ω—ã–π –∫–æ–º–º–µ–Ω—Ç–∞—Ä–∏–π. –Ø —Ö–æ—Ç–µ–ª –±—ã –ø–æ–±–ª–∞–≥–æ–¥–∞—Ä–∏—Ç—å –≤—Å–µ—Ö –≤–∞—Å,
¬†¬†¬†¬† –∫—Ç–æ –Ω–∞—à–µ–ª –≤—Ä–µ–º—è, —á—Ç–æ–±—ã –ø–æ–ø—Ä–æ–±–æ–≤–∞—Ç—å —ç—Ç–æ—Ç –∫—É—Ä—Å –ø—Ä–æ–≥—Ä–∞–º–º–∏—Ä–æ–≤–∞–Ω–∏—è.
¬†¬†¬†¬† –Ø –∑–Ω–∞—é, —á—Ç–æ —ç—Ç–æ –±–µ—Å–ø–ª–∞—Ç–Ω–æ –¥–ª—è –≤—Å–µ—Ö –≤–∞—Å, –∏ —Ç–∞–∫ –æ—Å—Ç–∞–Ω–µ—Ç—Å—è –±–µ—Å–ø–ª–∞—Ç–Ω—ã–º
¬†¬†¬†¬† –Ω–∞–≤—Å–µ–≥–¥–∞, –Ω–æ –º—ã –≤—Å–µ –µ—â–µ –≤–∫–ª–∞–¥—ã–≤–∞–µ–º –Ω–∞—à–µ —Å–µ—Ä–¥—Ü–µ –∏ –¥—É—à—É, –¥–µ–ª–∞—è —ç—Ç–æ –Ω–∞—Å—Ç–æ–ª—å–∫–æ —Ö–æ—Ä–æ—à–æ, –Ω–∞—Å–∫–æ–ª—å–∫–æ —ç—Ç–æ –≤–æ–∑–º–æ–∂–Ω–æ.

¬†¬†¬†¬† –ó–Ω–∞–π—Ç–µ, —á—Ç–æ —ç—Ç–æ –≤—Å–µ –µ—â–µ –Ω–∞—á–∞–ª–æ —Ä–∞–∑–≤–∏—Ç–∏—è Blockchain.
¬†¬†¬†¬† –ú—ã –ø—Ä–æ–¥–≤–∏–Ω—É–ª–∏—Å—å –æ—á–µ–Ω—å –¥–∞–ª–µ–∫–æ, –Ω–æ –µ—Å—Ç—å —Ç–∞–∫ –º–Ω–æ–≥–æ —Å–ø–æ—Å–æ–±–æ–≤ —Å–¥–µ–ª–∞—Ç—å —ç—Ç–æ
¬†¬†¬†¬† —Å–æ–æ–±—â–µ—Å—Ç–≤–æ –ª—É—á—à–µ. –ï—Å–ª–∏ –º—ã –≥–¥–µ-—Ç–æ –¥–æ–ø—É—Å—Ç–∏–ª–∏ –æ—à–∏–±–∫—É, –≤—ã –º–æ–∂–µ—Ç–µ
¬†¬†¬†¬† –ø–æ–º–æ—á—å –Ω–∞–º –∏ –æ—Ç–∫—Ä—ã—Ç—å –∑–∞–ø—Ä–æ—Å –Ω–∞ –ø—Ä–∏–Ω—è—Ç–∏–µ –∏–∑–º–µ–Ω–µ–Ω–∏–π (Pull Request):
     https://github.com/loomnetwork/cryptozombie-lessons

     –ò–ª–∏ –µ—Å–ª–∏ —É –≤–∞—Å –µ—Å—Ç—å –∏–¥–µ–∏, –∫–æ–º–º–µ–Ω—Ç–∞—Ä–∏–∏ –∏–ª–∏ –≤—ã –ø—Ä–æ—Å—Ç–æ —Ö–æ—Ç–∏—Ç–µ —Å–∫–∞–∑–∞—Ç—å
     "–ü—Ä–∏–≤–µ—Ç" - –∑–∞—Ö–æ–¥–∏—Ç–µ –≤ —Å–æ–æ–±—â–µ—Å—Ç–≤–æ Telegram –ø–æ –∞–¥—Ä–µ—Å—É https://t.me/loomnetwork
  */
}
```

–í —á–∞—Å—Ç–Ω–æ—Å—Ç–∏, —Ä–µ–∫–æ–º–µ–Ω–¥—É–µ—Ç—Å—è –∫–æ–º–º–µ–Ω—Ç–∏—Ä–æ–≤–∞—Ç—å –≤–∞—à –∫–æ–¥, —á—Ç–æ–±—ã –æ–±—ä—è—Å–Ω–∏—Ç—å –æ–∂–∏–¥–∞–µ–º–æ–µ –ø–æ–≤–µ–¥–µ–Ω–∏–µ –∫–∞–∂–¥–æ–π —Ñ—É–Ω–∫—Ü–∏–∏ –≤ –≤–∞—à–µ–º –∫–æ–Ω—Ç—Ä–∞–∫—Ç–µ. –¢–∞–∫–∏–º –æ–±—Ä–∞–∑–æ–º, –¥—Ä—É–≥–æ–π —Ä–∞–∑—Ä–∞–±–æ—Ç—á–∏–∫ (–∏–ª–∏ –≤—ã, –ø–æ—Å–ª–µ 6-–º–µ—Å—è—á–Ω–æ–≥–æ –ø–µ—Ä–µ—Ä—ã–≤–∞ –≤ –ø—Ä–æ–µ–∫—Ç–µ!) –º–æ–∂–µ—Ç –±—ã—Å—Ç—Ä–æ –ø—Ä–æ—Å–º–æ—Ç—Ä–µ—Ç—å –∏ –ø–æ–Ω—è—Ç—å –≤ –æ–±—â–∏—Ö —á–µ—Ä—Ç–∞—Ö, —á—Ç–æ –¥–µ–ª–∞–µ—Ç –≤–∞—à –∫–æ–¥, –Ω–µ —á–∏—Ç–∞—è —Å–∞–º –∫–æ–¥.

–°—Ç–∞–Ω–¥–∞—Ä—Ç –≤ —Å–æ–æ–±—â–µ—Å—Ç–≤–µ Solidity - –∏—Å–ø–æ–ª—å–∑–æ–≤–∞—Ç—å —Ñ–æ—Ä–º–∞—Ç **_natspec_**, –∫–æ—Ç–æ—Ä—ã–π –≤—ã–≥–ª—è–¥–∏—Ç —Å–ª–µ–¥—É—é—â–∏–º –æ–±—Ä–∞–∑–æ–º:

```
/// @title –ö–æ–Ω—Ç—Ä–∞–∫—Ç –¥–ª—è –æ—Å–Ω–æ–≤–Ω—ã—Ö –º–∞—Ç–µ–º–∞—Ç–∏—á–µ—Å–∫–∏—Ö –æ–ø–µ—Ä–∞—Ü–∏–π
/// @author H4XF13LD MORRIS üíØüíØüòéüíØüíØ
/// @notice –ù–∞ –¥–∞–Ω–Ω—ã–π –º–æ–º–µ–Ω—Ç —ç—Ç–æ—Ç –∫–æ–Ω—Ç—Ä–∞–∫—Ç –ø—Ä–æ—Å—Ç–æ –¥–æ–±–∞–≤–ª—è–µ—Ç —Ñ—É–Ω–∫—Ü–∏—é —É–º–Ω–æ–∂–µ–Ω–∏—è
contract Math {
  /// @notice –ü–µ—Ä–µ–º–Ω–æ–∂–µ–Ω–∏–µ –¥–≤—É—Ö —á–∏—Å–µ–ª
  /// @param x –ø–µ—Ä–≤—ã–π uint.
  /// @param y –≤—Ç–æ—Ä–æ–π uint.
  /// @return z —Ä–µ–∑—É–ª—å—Ç–∞—Ç (x * y)
  /// @dev –≠—Ç–∞ —Ñ—É–Ω–∫—Ü–∏—è —Å–µ–π—á–∞—Å –Ω–µ –ø—Ä–æ–≤–µ—Ä—è–µ—Ç –Ω–∞ –ø–µ—Ä–µ–ø–æ–ª–Ω–µ–Ω–∏—è
  function multiply(uint x, uint y) returns (uint z) {
    // –≠—Ç–æ –ø—Ä–æ—Å—Ç–æ –æ–±—ã—á–Ω—ã–π –∫–æ–º–º–µ–Ω—Ç–∞—Ä–∏–π, –∏ –µ–≥–æ –Ω–µ —Ä–∞—Å–ø–æ–∑–Ω–∞–µ—Ç natspec
    z = x * y;
  }
}
```

`@title` –∏ `@author` –ø–æ–Ω—è—Ç–Ω—ã.

`@notice` –æ–±—ä—è—Å–Ω—è–µ—Ç **–ø–æ–ª—å–∑–æ–≤–∞—Ç–µ–ª—é**, —á—Ç–æ –¥–µ–ª–∞–µ—Ç –∫–æ–Ω—Ç—Ä–∞–∫—Ç / —Ñ—É–Ω–∫—Ü–∏—è. `@dev` –¥–ª—è –ø–æ—è—Å–Ω–µ–Ω–∏—è –¥–æ–ø–æ–ª–Ω–∏—Ç–µ–ª—å–Ω—ã—Ö –¥–µ—Ç–∞–ª–µ–π —Ä–∞–∑—Ä–∞–±–æ—Ç—á–∏–∫–∞–º.

`@param` –∏ `@return` –¥–ª—è –æ–ø–∏—Å–∞–Ω–∏—è —Ç–æ–≥–æ, –¥–ª—è —á–µ–≥–æ –ø—Ä–µ–¥–Ω–∞–∑–Ω–∞—á–µ–Ω –∫–∞–∂–¥—ã–π –ø–∞—Ä–∞–º–µ—Ç—Ä –∏ –≤–æ–∑–≤—Ä–∞—â–∞–µ–º–æ–µ –∑–Ω–∞—á–µ–Ω–∏–µ —Ñ—É–Ω–∫—Ü–∏–∏.

–û–±—Ä–∞—Ç–∏—Ç–µ –≤–Ω–∏–º–∞–Ω–∏–µ, —á—Ç–æ –≤–∞–º –Ω–µ –≤—Å–µ–≥–¥–∞ –Ω—É–∂–Ω–æ –∏—Å–ø–æ–ª—å–∑–æ–≤–∞—Ç—å –≤—Å–µ —ç—Ç–∏ —Ç–µ–≥–∏ –¥–ª—è –∫–∞–∂–¥–æ–π —Ñ—É–Ω–∫—Ü–∏–∏ - –≤—Å–µ —Ç–µ–≥–∏ —è–≤–ª—è—é—Ç—Å—è –Ω–µ–æ–±—è–∑–∞—Ç–µ–ª—å–Ω—ã–º–∏. –ù–æ, –ø–æ –∫—Ä–∞–π–Ω–µ–π –º–µ—Ä–µ, –æ—Å—Ç–∞–≤—å—Ç–µ `@dev` –∑–∞–º–µ—Ç–∫—É, –æ–±—ä—è—Å–Ω—è—é—â—É—é, —á—Ç–æ –¥–µ–ª–∞–µ—Ç –∫–∞–∂–¥–∞—è —Ñ—É–Ω–∫—Ü–∏—è.

# –ü—Ä–æ–≤–µ—Ä—å —Å–µ–±—è

–ï—Å–ª–∏ –≤—ã –µ—â–µ –Ω–µ –∑–∞–º–µ—Ç–∏–ª–∏, —Å—Ä–µ–¥—Å—Ç–≤–æ –ø—Ä–æ–≤–µ—Ä–∫–∏ –æ—Ç–≤–µ—Ç–æ–≤ CryptoZombies –∏–≥–Ω–æ—Ä–∏—Ä—É–µ—Ç –∫–æ–º–º–µ–Ω—Ç–∞—Ä–∏–∏, –∫–æ–≥–¥–∞ –ø—Ä–æ–≤–µ—Ä—è–µ—Ç –≤–∞—à–∏ –æ—Ç–≤–µ—Ç—ã. –¢–∞–∫ —á—Ç–æ –º—ã –Ω–µ –º–æ–∂–µ–º –ø—Ä–æ–≤–µ—Ä–∏—Ç—å –≤–∞—à –∫–æ–¥ natspec –¥–ª—è —ç—Ç–æ–π –≥–ª–∞–≤—ã ;)

–¢–µ–º –Ω–µ –º–µ–Ω–µ–µ, –∫ —ç—Ç–æ–º—É –≤—Ä–µ–º–µ–Ω–∏ –≤—ã Solidity –≤–∞–ª—à–µ–±–Ω–∏–∫ ‚Äî¬†–º—ã –ø—Ä–æ—Å—Ç–æ –ø—Ä–µ–¥–ø–æ–ª–æ–∂–∏–º, —á—Ç–æ –≤—ã —É—Å–≤–æ–∏–ª–∏ —ç—Ç–æ!

–í –ª—é–±–æ–º —Å–ª—É—á–∞–µ, –ø–æ—Å—Ç–∞—Ä–∞–π—Ç–µ—Å—å, –∏ –ø–æ–ø—Ä–æ–±—É–π—Ç–µ –¥–æ–±–∞–≤–∏—Ç—å –Ω–µ–∫–æ—Ç–æ—Ä—ã–µ —Ç–µ–≥–∏ natspec –≤ `ZombieOwnership`:

1. `@title` ‚Äî¬†–ù–∞–ø—Ä–∏–º–µ—Ä: –ö–æ–Ω—Ç—Ä–∞–∫—Ç, –∫–æ—Ç–æ—Ä—ã–π —É–ø—Ä–∞–≤–ª—è–µ—Ç –ø–µ—Ä–µ–¥–∞—á–µ–π –ø—Ä–∞–≤ —Å–æ–±—Å—Ç–≤–µ–Ω–Ω–æ—Å—Ç–∏ –Ω–∞ –∑–æ–º–±–∏

2. `@author` ‚Äî¬†–í–∞—à–µ –∏–º—è!

3. `@dev` ‚Äî –ù–∞–ø—Ä–∏–º–µ—Ä: –°–æ–≤–º–µ—Å—Ç–∏–º —Å —Ä–µ–∞–ª–∏–∑–∞—Ü–∏–µ–π OpenZeppelin —á–µ—Ä–Ω–æ–≤–æ–π —Å–ø–µ—Ü–∏—Ñ–∏–∫–∞—Ü–∏–∏ ERC721
