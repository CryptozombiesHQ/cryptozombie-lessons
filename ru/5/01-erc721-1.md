---
title: Токены на Ethereum
actions: ['checkAnswer', 'hints']
requireLogin: true
material:
  editor:
    language: sol
    startingCode:
      "zombieownership.sol": |
        //  Начало здесь
      "zombieattack.sol": |
        pragma solidity ^0.4.25;

        import "./zombiehelper.sol";

        contract ZombieAttack is ZombieHelper {
          uint randNonce = 0;
          uint attackVictoryProbability = 70;

          function randMod(uint _modulus) internal returns(uint) {
            randNonce++;
            return uint(keccak256(abi.encodePacked(now, msg.sender, randNonce))) % _modulus;
          }

          function attack(uint _zombieId, uint _targetId) external ownerOf(_zombieId) {
            Zombie storage myZombie = zombies[_zombieId];
            Zombie storage enemyZombie = zombies[_targetId];
            uint rand = randMod(100);
            if (rand <= attackVictoryProbability) {
              myZombie.winCount++;
              myZombie.level++;
              enemyZombie.lossCount++;
              feedAndMultiply(_zombieId, enemyZombie.dna, "zombie");
            } else {
              myZombie.lossCount++;
              enemyZombie.winCount++;
              _triggerCooldown(myZombie);
            }
          }
        }

      "zombiehelper.sol": |
        pragma solidity ^0.4.25;

        import "./zombiefeeding.sol";

        contract ZombieHelper is ZombieFeeding {

          uint levelUpFee = 0.001 ether;

          modifier aboveLevel(uint _level, uint _zombieId) {
            require(zombies[_zombieId].level >= _level);
            _;
          }

          function withdraw() external onlyOwner {
            address _owner = owner();
            _owner.transfer(address(this).balance);
          }

          function setLevelUpFee(uint _fee) external onlyOwner {
            levelUpFee = _fee;
          }

          function levelUp(uint _zombieId) external payable {
            require(msg.value == levelUpFee);
            zombies[_zombieId].level++;
          }

          function changeName(uint _zombieId, string _newName) external aboveLevel(2, _zombieId) ownerOf(_zombieId) {
            zombies[_zombieId].name = _newName;
          }

          function changeDna(uint _zombieId, uint _newDna) external aboveLevel(20, _zombieId) ownerOf(_zombieId) {
            zombies[_zombieId].dna = _newDna;
          }

          function getZombiesByOwner(address _owner) external view returns(uint[]) {
            uint[] memory result = new uint[](ownerZombieCount[_owner]);
            uint counter = 0;
            for (uint i = 0; i < zombies.length; i++) {
              if (zombieToOwner[i] == _owner) {
                result[counter] = i;
                counter++;
              }
            }
            return result;
          }

        }

      "zombiefeeding.sol": |
        pragma solidity ^0.4.25;

        import "./zombiefactory.sol";

        contract KittyInterface {
          function getKitty(uint256 _id) external view returns (
            bool isGestating,
            bool isReady,
            uint256 cooldownIndex,
            uint256 nextActionAt,
            uint256 siringWithId,
            uint256 birthTime,
            uint256 matronId,
            uint256 sireId,
            uint256 generation,
            uint256 genes
          );
        }

        contract ZombieFeeding is ZombieFactory {

          KittyInterface kittyContract;

          modifier ownerOf(uint _zombieId) {
            require(msg.sender == zombieToOwner[_zombieId]);
            _;
          }

          function setKittyContractAddress(address _address) external onlyOwner {
            kittyContract = KittyInterface(_address);
          }

          function _triggerCooldown(Zombie storage _zombie) internal {
            _zombie.readyTime = uint32(now + cooldownTime);
          }

          function _isReady(Zombie storage _zombie) internal view returns (bool) {
              return (_zombie.readyTime <= now);
          }

          function feedAndMultiply(uint _zombieId, uint _targetDna, string _species) internal ownerOf(_zombieId) {
            Zombie storage myZombie = zombies[_zombieId];
            require(_isReady(myZombie));
            _targetDna = _targetDna % dnaModulus;
            uint newDna = (myZombie.dna + _targetDna) / 2;
            if (keccak256(abi.encodePacked(_species)) == keccak256(abi.encodePacked("kitty"))) {
              newDna = newDna - newDna % 100 + 99;
            }
            _createZombie("NoName", newDna);
            _triggerCooldown(myZombie);
          }

          function feedOnKitty(uint _zombieId, uint _kittyId) public {
            uint kittyDna;
            (,,,,,,,,,kittyDna) = kittyContract.getKitty(_kittyId);
            feedAndMultiply(_zombieId, kittyDna, "kitty");
          }
        }

      "zombiefactory.sol": |
        pragma solidity ^0.4.25;

        import "./ownable.sol";

        contract ZombieFactory is Ownable {

            event NewZombie(uint zombieId, string name, uint dna);

            uint dnaDigits = 16;
            uint dnaModulus = 10 ** dnaDigits;
            uint cooldownTime = 1 days;

            struct Zombie {
              string name;
              uint dna;
              uint32 level;
              uint32 readyTime;
              uint16 winCount;
              uint16 lossCount;
            }

            Zombie[] public zombies;

            mapping (uint => address) public zombieToOwner;
            mapping (address => uint) ownerZombieCount;

            function _createZombie(string _name, uint _dna) internal {
                uint id = zombies.push(Zombie(_name, _dna, 1, uint32(now + cooldownTime), 0, 0)) - 1;
                zombieToOwner[id] = msg.sender;
                ownerZombieCount[msg.sender]++;
                emit NewZombie(id, _name, _dna);
            }

            function _generateRandomDna(string _str) private view returns (uint) {
                uint rand = uint(keccak256(abi.encodePacked(_str)));
                return rand % dnaModulus;
            }

            function createRandomZombie(string _name) public {
                require(ownerZombieCount[msg.sender] == 0);
                uint randDna = _generateRandomDna(_name);
                randDna = randDna - randDna % 100;
                _createZombie(_name, randDna);
            }

        }

      "ownable.sol": |
        pragma solidity ^0.4.25;

        /**
        * @title Ownable
        * @dev The Ownable contract has an owner address, and provides basic authorization control
        * functions, this simplifies the implementation of "user permissions".
        */
        contract Ownable {
          address private _owner;

          event OwnershipTransferred(
            address indexed previousOwner,
            address indexed newOwner
          );

          /**
          * @dev The Ownable constructor sets the original `owner` of the contract to the sender
          * account.
          */
          constructor() internal {
            _owner = msg.sender;
            emit OwnershipTransferred(address(0), _owner);
          }

          /**
          * @return the address of the owner.
          */
          function owner() public view returns(address) {
            return _owner;
          }

          /**
          * @dev Throws if called by any account other than the owner.
          */
          modifier onlyOwner() {
            require(isOwner());
            _;
          }

          /**
          * @return true if `msg.sender` is the owner of the contract.
          */
          function isOwner() public view returns(bool) {
            return msg.sender == _owner;
          }

          /**
          * @dev Allows the current owner to relinquish control of the contract.
          * @notice Renouncing to ownership will leave the contract without an owner.
          * It will not be possible to call the functions with the `onlyOwner`
          * modifier anymore.
          */
          function renounceOwnership() public onlyOwner {
            emit OwnershipTransferred(_owner, address(0));
            _owner = address(0);
          }

          /**
          * @dev Allows the current owner to transfer control of the contract to a newOwner.
          * @param newOwner The address to transfer ownership to.
          */
          function transferOwnership(address newOwner) public onlyOwner {
            _transferOwnership(newOwner);
          }

          /**
          * @dev Transfers control of the contract to a newOwner.
          * @param newOwner The address to transfer ownership to.
          */
          function _transferOwnership(address newOwner) internal {
            require(newOwner != address(0));
            emit OwnershipTransferred(_owner, newOwner);
            _owner = newOwner;
          }
        }

    answer: |
      pragma solidity ^0.4.25;

      import "./zombieattack.sol";

      contract ZombieOwnership is ZombieAttack {

      }
---

Давайте поговорим про **_токены_**.

Если вы были в окружении Ethereum какое-то время, вы, вероятно, слышали, как люди говорят о токенах — особенно **_ERC20 токенах_**.

**_Токены_** на Ethereum это просто умный контракт, который следует некоторым общим правилам, а именно реализует стандартный набор функций, которые наследуют все остальные контракты токенов, такие как `transferFrom(address _from, address _to, uint256 _tokenId)` и `balanceOf(address _owner)`.

Внутри умный контракт обычно содержит соответствие, `mapping(address => uint256) balances`, которое отслеживает баланс каждого адреса.

Таким образом, по сути токен - это просто контракт, который отслеживает, у кого сколько токенов, и несколько функций, позволяющие этим пользователям переводить свои токены на другие адреса.

### Почему это так важно?

Поскольку все токены ERC20 имеют один и тот же набор функций с одинаковыми именами, с ними можно взаимодействовать одинаково.

Это означает, что если вы создаете приложение, способное взаимодействовать с одним токеном ERC20, оно также может взаимодействовать с любым токеном ERC20. Таким образом, в будущем в ваше приложение можно будет легко добавить больше токенов без необходимости индивидуальной доработки. Вы можете просто добавить новый адрес контракта токена, и, бум, в вашем приложении есть еще токен, который можно использовать.

Одним из примеров этого может быть биржа. Когда биржа добавляет новый токен ERC20, на самом деле ей просто нужно добавить еще один смарт-контракт, с которым она взаимодействует. Пользователи могут сказать этому контракту отправить токены на адрес кошелька биржи, а биржа может сказать контракту отправить токены обратно пользователям, когда они запрашивают вывод.

Бирже нужно реализовать эту логику перевода только один раз, а затем, когда он хочет добавить новый токен ERC20, это просто вопрос добавления нового адреса контракта в ее базу данных.

### Другие стандарты токенов

ERC20 токены действительно хороши для токенов, которые ведут себя как валюты. Но они не особенно полезны для представления зомби в нашей игре про зомби.

Во-первых, зомби не делимы, как валюты — я могу отправить Вам 0.237 ETH, но отправка Вам 0.237 зомби не имеет смысла.

Во-вторых, не все зомби созданы равными. Ваш зомби 2 уровня "**Стив**" совершенно не равен моему зомби 732 уровня "**H4XF13LD Моррис 💯💯😎💯💯**". (Даже не близко, *Стив*).

Существует еще один стандарт токенов, который гораздо лучше подходит для крипто-коллекционирования, таких как КриптоЗомби — и они называются **_ERC721 токены._**

**_ERC721 токены** **не** взаимозаменяемы, поскольку каждый из них считается уникальным и не делимым. Вы можете обменять их только как одно целое, и каждый из них имеет уникальный идентификатор. Так что они идеально подходят для того, чтобы сделать наших зомби удобными для обмена.

> Обратите внимание, что использование такого стандарта, как ERC721, имеет то преимущество, что нам не нужно реализовывать логику аукциона или условного депонирования (escrow) в нашем контракте, определяющую, как игроки могут обменивать / продавать наших зомби. Если мы будем соответствовать спецификации, кто-то другой может создать платформу обмена для крипто-обменных активов ERC721, и наши зомби ERC721 будут использоваться на этой платформе. Таким образом, есть явные преимущества использования стандарта токенов вместо реализации собственной логики для торговли.

## Проверь себя

Мы собираемся углубиться в реализацию ERC721 в следующей главе. Но сначала давайте настроим структуру файлов для этого урока.

Мы собираемся сохранить всю логику ERC721 в контракте под названием `ZombieOwnership`.

1. Объявите `pragma` версию в верху файла (проверьте синтаксис в файлах предыдущих уроков).

2. Этот файл должен импортировать (`import`) контракт `zombieattack.sol`.

3. Объявите новый контракт, `ZombieOwnership`, который наследуется от `ZombieAttack`. Оставьте тело контракта пока пустым.
