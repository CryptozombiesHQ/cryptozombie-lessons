---
title: 배경 지식 & 시작하기
actions: ['checkAnswer', 'hints']
requireLogin: true
material:
  editor:
    language: sol
    startingCode:
        "ZombieToken.sol": |
          // 여기서 시작하게

        "ERC721XToken.sol": |

          // Full implementation with all included files at https://github.com/loomnetwork/erc721x

          pragma solidity ^0.4.25;

          import "./../../Interfaces/ERC721X.sol";

          import "./../../Interfaces/ERC721XReceiver.sol";
          import "./ERC721XTokenNFT.sol";

          import "openzeppelin-solidity/contracts/AddressUtils.sol";
          import "./../../Libraries/ObjectsLib.sol";


          // Additional features over NFT token that is compatible with batch transfers
          contract ERC721XToken is ERC721X, ERC721XTokenNFT {

              using ObjectLib for ObjectLib.Operations;
              using AddressUtils for address;

              bytes4 internal constant ERC721X_RECEIVED = 0x660b3370;
              bytes4 internal constant ERC721X_BATCH_RECEIVE_SIG = 0xe9e5be6a;

              event BatchTransfer(address from, address to, uint256[] tokenTypes, uint256[] amounts);


              modifier isOperatorOrOwner(address _from) {
                  require((msg.sender == _from) || operators[_from][msg.sender], "msg.sender is neither _from nor operator");
                  _;
              }

              function implementsERC721X() public pure returns (bool) {
                  return true;
              }

              /**
               * @dev transfer objects from different tokenIds to specified address
               * @param _from The address to BatchTransfer objects from.
               * @param _to The address to batchTransfer objects to.
               * @param _tokenIds Array of tokenIds to update balance of
               * @param _amounts Array of amount of object per type to be transferred.
               * Note:  Arrays should be sorted so that all tokenIds in a same bin are adjacent (more efficient).
               */
              function _batchTransferFrom(address _from, address _to, uint256[] _tokenIds, uint256[] _amounts)
                  internal
                  isOperatorOrOwner(_from)
              {

                  // Requirements
                  require(_tokenIds.length == _amounts.length, "Inconsistent array length between args");
                  require(_to != address(0), "Invalid recipient");

                  if (tokenType[_tokenIds[0]] == NFT) {
                      tokenOwner[_tokenIds[0]] = _to;
                      emit Transfer(_from, _to, _tokenIds[0]);
                  }

                  // Load first bin and index where the object balance exists
                  (uint256 bin, uint256 index) = ObjectLib.getTokenBinIndex(_tokenIds[0]);

                  // Balance for current bin in memory (initialized with first transfer)
                  // Written with bad library syntax instead of as below to bypass stack limit error
                  uint256 balFrom = ObjectLib.updateTokenBalance(
                      packedTokenBalance[_from][bin], index, _amounts[0], ObjectLib.Operations.SUB
                  );
                  uint256 balTo = ObjectLib.updateTokenBalance(
                      packedTokenBalance[_to][bin], index, _amounts[0], ObjectLib.Operations.ADD
                  );

                  // Number of transfers to execute
                  uint256 nTransfer = _tokenIds.length;

                  // Last bin updated
                  uint256 lastBin = bin;

                  for (uint256 i = 1; i < nTransfer; i++) {
                      // If we're transferring an NFT we additionally should update the tokenOwner and emit the corresponding event
                      if (tokenType[_tokenIds[i]] == NFT) {
                          tokenOwner[_tokenIds[i]] = _to;
                          emit Transfer(_from, _to, _tokenIds[i]);
                      }
                      (bin, index) = _tokenIds[i].getTokenBinIndex();

                      // If new bin
                      if (bin != lastBin) {
                          // Update storage balance of previous bin
                          packedTokenBalance[_from][lastBin] = balFrom;
                          packedTokenBalance[_to][lastBin] = balTo;

                          // Load current bin balance in memory
                          balFrom = packedTokenBalance[_from][bin];
                          balTo = packedTokenBalance[_to][bin];

                          // Bin will be the most recent bin
                          lastBin = bin;
                      }

                      // Update memory balance
                      balFrom = balFrom.updateTokenBalance(index, _amounts[i], ObjectLib.Operations.SUB);
                      balTo = balTo.updateTokenBalance(index, _amounts[i], ObjectLib.Operations.ADD);
                  }

                  // Update storage of the last bin visited
                  packedTokenBalance[_from][bin] = balFrom;
                  packedTokenBalance[_to][bin] = balTo;

                  // Emit batchTransfer event
                  emit BatchTransfer(_from, _to, _tokenIds, _amounts);
              }

              function batchTransferFrom(address _from, address _to, uint256[] _tokenIds, uint256[] _amounts) public {
                  // Batch Transfering
                  _batchTransferFrom(_from, _to, _tokenIds, _amounts);
              }

              /**
               * @dev transfer objects from different tokenIds to specified address
               * @param _from The address to BatchTransfer objects from.
               * @param _to The address to batchTransfer objects to.
               * @param _tokenIds Array of tokenIds to update balance of
               * @param _amounts Array of amount of object per type to be transferred.
               * @param _data Data to pass to onERC721XReceived() function if recipient is contract
               * Note:  Arrays should be sorted so that all tokenIds in a same bin are adjacent (more efficient).
               */
              function safeBatchTransferFrom(
                  address _from,
                  address _to,
                  uint256[] _tokenIds,
                  uint256[] _amounts,
                  bytes _data
              )
                  public
              {

                  // Batch Transfering
                  _batchTransferFrom(_from, _to, _tokenIds, _amounts);

                  // Pass data if recipient is contract
                  if (_to.isContract()) {
                      bytes4 retval = ERC721XReceiver(_to).onERC721XBatchReceived(
                          msg.sender, _from, _tokenIds, _amounts, _data
                      );
                      require(retval == ERC721X_BATCH_RECEIVE_SIG);
                  }
              }

              function transfer(address _to, uint256 _tokenId, uint256 _amount) public {
                  _transferFrom(msg.sender, _to, _tokenId, _amount);
              }

              function transferFrom(address _from, address _to, uint256 _tokenId, uint256 _amount) public {
                  _transferFrom(_from, _to, _tokenId, _amount);
              }

              function _transferFrom(address _from, address _to, uint256 _tokenId, uint256 _amount)
                  internal
                  isOperatorOrOwner(_from)
              {
                  require(tokenType[_tokenId] == FT);
                  require(_amount <= balanceOf(_from, _tokenId), "Quantity greater than from balance");
                  require(_to != address(0), "Invalid to address");

                  _updateTokenBalance(_from, _tokenId, _amount, ObjectLib.Operations.SUB);
                  _updateTokenBalance(_to, _tokenId, _amount, ObjectLib.Operations.ADD);
                  emit TransferWithQuantity(_from, _to, _tokenId, _amount);
              }

              function safeTransferFrom(address _from, address _to, uint256 _tokenId, uint256 _amount) public {
                  safeTransferFrom(_from, _to, _tokenId, _amount, "");
              }

              function safeTransferFrom(address _from, address _to, uint256 _tokenId, uint256 _amount, bytes _data) public {
                  _transferFrom(_from, _to, _tokenId, _amount);
                  require(
                      checkAndCallSafeTransfer(_from, _to, _tokenId, _amount, _data),
                      "Sent to a contract which is not an ERC721X receiver"
                  );
              }

              function _mint(uint256 _tokenId, address _to, uint256 _supply) internal {
                  // If the token doesn't exist, add it to the tokens array
                  if (!exists(_tokenId)) {
                      tokenType[_tokenId] = FT;
                      allTokens.push(_tokenId);
                  } else {
                      // if the token exists, it must be a FT
                      require(tokenType[_tokenId] == FT, "Not a FT");
                  }

                  _updateTokenBalance(_to, _tokenId, _supply, ObjectLib.Operations.ADD);
                  emit TransferWithQuantity(address(this), _to, _tokenId, _supply);
              }


              function checkAndCallSafeTransfer(
                  address _from,
                  address _to,
                  uint256 _tokenId,
                  uint256 _amount,
                  bytes _data
              )
                  internal
                  returns (bool)
              {
                  if (!_to.isContract()) {
                      return true;
                  }

                  bytes4 retval = ERC721XReceiver(_to).onERC721XReceived(
                      msg.sender, _from, _tokenId, _amount, _data);
                  return(retval == ERC721X_RECEIVED);
              }

          }
    answer: >
      pragma solidity ^0.4.25;

      import "./ERC721XToken.sol";

      contract ZombieCard is ERC721XToken {

      }
---

이 레슨에서는 예제 블록체인 게임을 위한 토큰 컨트랙트를 만들어갈 것이네.

이 컨트랙트는 우리 Loom Network에서 만든 게임인 <a href="https://loom.games/" target=_blank>좀비 배틀그라운드</a>에서 ERC721x를 구현한 방식을 기반으로 하네 - 좀비 배틀그라운드는 매직:더 개더링이나 하스스톤과 비슷한 블록체인 기반의 카드 수집형 카드 게임이지.

이렇게 해서 자네는 실제 세상에서 쓰이는 예시들이 어떻게 생겼는지 알 수 있을 것이고, 이 레슨을 완료하고 나면 자네만의 게임에 이 구현을 적용할 준비가 끝날 것이네.

첫 번째 챕터에서는, 앞으로 나올 내용들을 잘 이해할 수 있도록 기반 지식을 먼저 확인해보도록 하지...

## 배경 지식: 그래서 토큰이 뭐야?

게임 내 아이템들을 "토큰"이라 말하는 것이 조금 헷갈릴 수도 있네. "토큰"은 화폐처럼 쓰이는 무언가를 암시하기 때문이지.

하지만 이더리움에서 **_토큰_**이라는 단어는 그저 똑같은 공통 함수들을 구현하도록 하는 스마트 컨트랙트의 표준을 의미할 뿐이네. `transfer(address _to, uint256 _value)`이나 `balanceOf(address _owner)` 같은 함수들 말이지. 

그래, 자네는 화폐를 전송하거나 화폐의 잔액을 가지고 있을 수 있네 - 하지만 어떤 게임에서 아이템을 전송하거나 아이템 인벤토리를 가질 수도 있지. 검이나 카드 아이템 같은 것들 말이네.

게임 내에서 검이 화폐처럼 소모할 수 있는 것이 아니라 해도 - 플레이어가 소유할 수 있는 것이라면 어떤 것이든 이런 똑같은 기본 기능들이 *정말로* 필요하네.

그러니 "토큰"의 개념은 공통 메소드들을 추상화하여 우리의 게임에 넣을 모든 아이템들에 적용하기에 적절하네 - 이런 방식으로 이더리움상의 모든 게임 아이템들이 공통 인터페이스를 공유하고, 동일한 토큰 표준을 따르는 어떤 지갑이나 마켓플레이스에도 쓰일 수 있는 것이지.

## 다른 토큰 표준들: ERC20과 ERC721

가장 널리 알려진 토큰 표준은 **_ERC20 토큰_**이네. 이 토큰들은 화폐처럼 쓰이지. 하지만 이들은 디지털 아이템들을 표현하는 데에는 딱 적합하지는 않네. 트레이딩 카드 게임에서의 카드 같은 것들 말이지.

첫째로, 카드들은 화폐처럼 분할이 되지 않네 - 한 카드의 2/5를 소유한다거나 할 순 없지.

둘쨰로, 모든 카드가 똑같이 만들어지지 않기에, 상호 교환할 수 없네. 자네의 **Z-Virus** 카드를 내 **Ozmoziz** 카드와 교환하고 싶지는 않겠지.

그래서 크립토 수집품들에 더 잘 맞는 다른 토큰 표준이 있네. 이 표준은 **_ERC721_**이라 불리지.

**_ERC721 토큰들은_** 상호 교환이 되지 **않네**. 각 아이템은 고유하게 다뤄지고, 고유 ID 값을 가지네. 또 나눠지지도 않지 - 이 토큰들은 하나의 전체 단위로만 거래를 할 수 있네.

그러니 이것이 우리의 트레이딩 카드 게임을 만들 때 더 잘 맞겠지.

자네, 내가 **더 잘** 맞는다고 한 거 알아차렸나? 완벽하다는 건 아니야, 그렇지? 😉

## ERC721x - 두 세계의 장점만을 사용하기

게임 내 아이템에 대해 ERC721이 가지는 문제는, 대부분의 게임들에서는 **실제로** 동일한 아이템들이 꽤 있다는 것이네.

예를 들어, 우리의 카드 게임에 서로 다른 300가지 종류의 카드들이 있다고 해보지. 하지만 이 카드들은 각각 하나 이상의 복사본이 있어야 할 것이네 - 그렇지 않으면 많은 사람들이 우리 게임을 플레이할 수는 없겠지!

동일한 카드의 다중 복사본은 (대부분) 상호 교환이 되네. 그러니 이를 고려한 새로운 토큰 표준을 만들 필요가 있겠지.

ERC721x는 다양한 토큰들을 정의할 수 있게 하고(카드 템플릿 같은 것을 생각해보게), 각 카드마다 해당 카드의 전체 개수 등을 정의할 수 있게 하여 이러한 사용 사례를 처리하고 있네.

어떻게 하고 있는지는 자네도 보게 될 것이네. 우리가 다음 챕터들에서 자네에게 하나하나 설명해줄 것이니 말이네. 👨‍🏫

## 시작하기: 프로젝트에서 ERC721x 사용하기

새로운 ERC721x 토큰을 만들 때, 바닥부터 시작할 필요는 없네. ERC20 토큰을 만들 때 <a href="https://github.com/OpenZeppelin/openzeppelin-solidity" target=_blank>Open Zeppelin</a> 같은 곳에서 템플릿을 복사하고 필요한 변경을 하면서 만들기 시작하는 것처럼, ERC721x도 자네 프로젝트에 우리의 구현을 포함하면서 만들기 시작할 수 있네.

향후 프로젝트에 이를 포함하려면, <a href="https://github.com/loomnetwork/erc721x" target=_blank>여기</a> 우리 Github에 있는 설명대로 따라하면 되네. 하지만 이번 레슨에서는, 우리가 먼저 자네 프로젝트에 `ERC721XToken.sol`를 넣어놓았네.

모든 코드를 이해하는 것에 대해서는 걱정할 필요 없네 - 자네가 이해할 필요가 있는 부분들에 대해서는 우리가 하나하나 설명해줄 것이니 말이네.

# 직접 해보기

기초부터 시작하도록 하지: "ZombieCard"라는 이름으로 `ERC721XToken`의 로직을 상속받는 새로운 컨트랙트를 만들 것이네.

1. 먼저, 맨 위에 우리가 `pragma solidity ^0.4.25`를 사용한다고 선언하게.

2. pragma를 선언한 후에, 다음 파일을 `import`하게: `./ERC721XToken.sol`

3. 다음으로, 새로운 `contract`를 "ZombieCard"라는 이름으로 선언하게. 이 컨트랙트는 `is` 키워드를 사용해서 `ERC721XToken`을 상속해야 하네. 지금은 컨트랙트의 내용은 비워두게.

> 참고: 기초 내용들이 잘 기억이 나지 않아 헷갈린다면, <a href="https://cryptozombies.io/en/lesson/1/chapter/2">레슨1 챕터2</a>와 <a href="https://cryptozombies.io/en/lesson/2/chapter/5">레슨2 챕터5</a>를 복습하도록 하게.
