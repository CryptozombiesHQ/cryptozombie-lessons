---
title: Usando a Interface
actions: ['verificarResposta', 'dicas']
material:
  editor:
    language: sol
    startingCode:
      "zombiefeeding.sol": |
        pragma solidity ^0.4.19;

        import "./zombiefactory.sol";

        contract KittyInterface {
          function getKitty(uint256 _id) external view returns (
            bool isGestating,
            bool isReady,
            uint256 cooldownIndex,
            uint256 nextActionAt,
            uint256 siringWithId,
            uint256 birthTime,
            uint256 matronId,
            uint256 sireId,
            uint256 generation,
            uint256 genes
          );
        }

        contract ZombieFeeding is ZombieFactory {

          address ckAddress = 0x06012c8cf97BEaD5deAe237070F9587f8E7A266d;
          // Inicialize o kittyContract aqui usando `ckAddress` acima

          function feedAndMultiply(uint _zombieId, uint _targetDna) public {
            require(msg.sender == zombieToOwner[_zombieId]);
            Zombie storage myZombie = zombies[_zombieId];
            _targetDna = _targetDna % dnaModulus;
            uint newDna = (myZombie.dna + _targetDna) / 2;
            _createZombie("NoName", newDna);
          }

        }
      "zombiefactory.sol": |
        pragma solidity ^0.4.19;

        contract ZombieFactory {

            event NewZombie(uint zombieId, string name, uint dna);

            uint dnaDigits = 16;
            uint dnaModulus = 10 ** dnaDigits;

            struct Zombie {
                string name;
                uint dna;
            }

            Zombie[] public zombies;

            mapping (uint => address) public zombieToOwner;
            mapping (address => uint) ownerZombieCount;

            function _createZombie(string _name, uint _dna) internal {
                uint id = zombies.push(Zombie(_name, _dna)) - 1;
                zombieToOwner[id] = msg.sender;
                ownerZombieCount[msg.sender]++;
                NewZombie(id, _name, _dna);
            }

            function _generatePseudoRandomDna(string _str) private view returns (uint) {
                uint rand = uint(keccak256(_str));
                return rand % dnaModulus;
            }

            function createPseudoRandomZombie(string _name) public {
                require(ownerZombieCount[msg.sender] == 0);
                uint randDna = _generatePseudoRandomDna(_name);
                _createZombie(_name, randDna);
            }

        }
    answer: >
      pragma solidity ^0.4.19;

      import "./zombiefactory.sol";

      contract KittyInterface {
        function getKitty(uint256 _id) external view returns (
          bool isGestating,
          bool isReady,
          uint256 cooldownIndex,
          uint256 nextActionAt,
          uint256 siringWithId,
          uint256 birthTime,
          uint256 matronId,
          uint256 sireId,
          uint256 generation,
          uint256 genes
        );
      }

      contract ZombieFeeding is ZombieFactory {

        address ckAddress = 0x06012c8cf97BEaD5deAe237070F9587f8E7A266d;
        KittyInterface kittyContract = KittyInterface(ckAddress);

        function feedAndMultiply(uint _zombieId, uint _targetDna) public {
          require(msg.sender == zombieToOwner[_zombieId]);
          Zombie storage myZombie = zombies[_zombieId];
          _targetDna = _targetDna % dnaModulus;
          uint newDna = (myZombie.dna + _targetDna) / 2;
          _createZombie("NoName", newDna);
        }

      }
---

Continuando o nosso próximo exemplo com a `NumberInterface`, uma vez que definimos a interface como:

```
contract NumberInterface {
  function getNum(address _myAddress) public view returns (uint);
}
```

Nós podemos usá-lo em um contrato conforme segue:

```
contract MyContract {
  address NumberInterfaceAddress = 0xab38...;
  // ^ O endereço do contrato FavoriteNumber no Ethereum

  NumberInterface numberContract = NumberInterface(NumberInterfaceAddress);
  // Agora `numberContract` esta apontando para outro contrato

  function someFunction() public {
    // Agora nós podemos chamar `getNum` deste contrato:
    uint num = numberContract.getNum(msg.sender);
    // ...e fazer algo com `num` aqui
  }
}
```

Desta forma, seus contratos podem interagir com qualquer outro contrato no blockchain do Ethereum, contanto que tais contratos exponham as suas funções como `public` ou `external`.

# Vamos testar

Vamos configurar o nosso contrato para ler o smart contrato do CryptoKitties!

1. Salvamos o endereço do contrato do CryptoKitties no código para você, em uma variável chamada `ckAddress`. Na próxima linha, crie uma variável de estado do tipo `KittyInterface` chamada `kittyContract`, e inicialize-a com a variável `ckAddress` - do mesmo jeito que fizemos acima no `numberContract`.
