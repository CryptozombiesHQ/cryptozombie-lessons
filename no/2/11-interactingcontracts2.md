---
title: Bruk interfacet
actions: ['checkAnswer', 'hints']
material:
  editor:
    language: sol
    startingCode:
      "zombiefeeding.sol": |
        pragma solidity ^0.4.19;

        import "./zombiefactory.sol";

        contract KittyInterface {
          function getKitty(uint256 _id) external view returns (
            bool isGestating,
            bool isReady,
            uint256 cooldownIndex,
            uint256 nextActionAt,
            uint256 siringWithId,
            uint256 birthTime,
            uint256 matronId,
            uint256 sireId,
            uint256 generation,
            uint256 genes
          );
        }

        contract ZombieFeeding is ZombieFactory {

          address ckAddress = 0x06012c8cf97BEaD5deAe237070F9587f8E7A266d;
          // Initialiser kittyContract her ved bruk av `ckAddress` ovenfor

          function feedAndMultiply(uint _zombieId, uint _targetDna) public {
            require(msg.sender == zombieToOwner[_zombieId]);
            Zombie storage myZombie = zombies[_zombieId];
            _targetDna = _targetDna % dnaModulus;
            uint newDna = (myZombie.dna + _targetDna) / 2;
            _createZombie("NoName", newDna);
          }

        }
      "zombiefactory.sol": |
        pragma solidity ^0.4.19;

        contract ZombieFactory {

            event NewZombie(uint zombieId, string name, uint dna);

            uint dnaDigits = 16;
            uint dnaModulus = 10 ** dnaDigits;

            struct Zombie {
                string name;
                uint dna;
            }

            Zombie[] public zombies;

            mapping (uint => address) public zombieToOwner;
            mapping (address => uint) ownerZombieCount;

            function _createZombie(string _name, uint _dna) internal {
                uint id = zombies.push(Zombie(_name, _dna)) - 1;
                zombieToOwner[id] = msg.sender;
                ownerZombieCount[msg.sender]++;
                NewZombie(id, _name, _dna);
            }

            function _generatePseudoRandomDna(string _str) private view returns (uint) {
                uint rand = uint(keccak256(_str));
                return rand % dnaModulus;
            }

            function createPseudoRandomZombie(string _name) public {
                require(ownerZombieCount[msg.sender] == 0);
                uint randDna = _generatePseudoRandomDna(_name);
                _createZombie(_name, randDna);
            }

        }
    answer: >
      pragma solidity ^0.4.19;

      import "./zombiefactory.sol";

      contract KittyInterface {
        function getKitty(uint256 _id) external view returns (
          bool isGestating,
          bool isReady,
          uint256 cooldownIndex,
          uint256 nextActionAt,
          uint256 siringWithId,
          uint256 birthTime,
          uint256 matronId,
          uint256 sireId,
          uint256 generation,
          uint256 genes
        );
      }

      contract ZombieFeeding is ZombieFactory {

        address ckAddress = 0x06012c8cf97BEaD5deAe237070F9587f8E7A266d;
        KittyInterface kittyContract = KittyInterface(ckAddress);

        function feedAndMultiply(uint _zombieId, uint _targetDna) public {
          require(msg.sender == zombieToOwner[_zombieId]);
          Zombie storage myZombie = zombies[_zombieId];
          _targetDna = _targetDna % dnaModulus;
          uint newDna = (myZombie.dna + _targetDna) / 2;
          _createZombie("NoName", newDna);
        }

      }
---

Fortsetter vårt forrige eksempel med `NumberInterface`, når vi har definert interface som:

```
contract NumberInterface {
  function getNum(address _myAddress) public view returns (uint);
}
```

Vi kan bruke det i en kontrakt som følger:

```
contract MyContract {
  address NumberInterfaceAddress = 0xab38... 
  // ^ Addressen til FavoriteNumber kontrakten på Ethereum
  NumberInterface numberContract = NumberInterface(NumberInterfaceAddress)
  // Nå peker `numberContract` til den andre kontrakten

  function someFunction() public {
    // Nå kan vi kjøre `getNum` fra den kontrakten:
    uint num = numberContract.getNum(msg.sender);
    // ...og gjør noe med `num` her
  }
}
```

På denne måten kan kontrakten din samhandle med enhver annen kontrakt på Ethereum blockchain, så lenge de setter disse funksjonene som `public` eller `external`.

# Test det

La oss sette opp kontrakten vår for å lese fra CryptoKitties smart-kontrakt!

1. Jeg har lagret adressen til CryptoKitties-kontrakten i koden for deg, under en variabel kalt `ckAddress`. I neste linje oppretter du en `KittyInterface` kalt `kittyContract`, og initialiserer den med `ckAddress` - akkurat som vi gjorde med `numberContract` ovenfor.
