---
title: Tokens Ethereum
actions: ['v√©rifierLaR√©ponse', 'indice']
requireLogin: true
material:
  editor:
    language: sol
    startingCode:
      "zombieownership.sol": |
        // Commencez ici
      "zombieattack.sol": |
        pragma solidity ^0.4.19;

        import "./zombiehelper.sol";

        contract ZombieBattle is ZombieHelper {
          uint randNonce = 0;
          uint attackVictoryProbability = 70;

          function randMod(uint _modulus) internal returns(uint) {
            randNonce++;
            return uint(keccak256(now, msg.sender, randNonce)) % _modulus;
          }

          function attack(uint _zombieId, uint _targetId) external ownerOf(_zombieId) {
            Zombie storage myZombie = zombies[_zombieId];
            Zombie storage enemyZombie = zombies[_targetId];
            uint rand = randMod(100);
            if (rand <= attackVictoryProbability) {
              myZombie.winCount++;
              myZombie.level++;
              enemyZombie.lossCount++;
              feedAndMultiply(_zombieId, enemyZombie.dna, "zombie");
            } else {
              myZombie.lossCount++;
              enemyZombie.winCount++;
              _triggerCooldown(myZombie);
            }
          }
        }
      "zombiehelper.sol": |
        pragma solidity ^0.4.19;

        import "./zombiefeeding.sol";

        contract ZombieHelper is ZombieFeeding {

          uint levelUpFee = 0.001 ether;

          modifier aboveLevel(uint _level, uint _zombieId) {
            require(zombies[_zombieId].level >= _level);
            _;
          }

          function withdraw() external onlyOwner {
            owner.transfer(this.balance);
          }

          function setLevelUpFee(uint _fee) external onlyOwner {
            levelUpFee = _fee;
          }

          function levelUp(uint _zombieId) external payable {
            require(msg.value == levelUpFee);
            zombies[_zombieId].level++;
          }

          function changeName(uint _zombieId, string _newName) external aboveLevel(2, _zombieId) ownerOf(_zombieId) {
            zombies[_zombieId].name = _newName;
          }

          function changeDna(uint _zombieId, uint _newDna) external aboveLevel(20, _zombieId) ownerOf(_zombieId) {
            zombies[_zombieId].dna = _newDna;
          }

          function getZombiesByOwner(address _owner) external view returns(uint[]) {
            uint[] memory result = new uint[](ownerZombieCount[_owner]);
            uint counter = 0;
            for (uint i = 0; i < zombies.length; i++) {
              if (zombieToOwner[i] == _owner) {
                result[counter] = i;
                counter++;
              }
            }
            return result;
          }

        }
      "zombiefeeding.sol": |
        pragma solidity ^0.4.19;

        import "./zombiefactory.sol";

        contract KittyInterface {
          function getKitty(uint256 _id) external view returns (
            bool isGestating,
            bool isReady,
            uint256 cooldownIndex,
            uint256 nextActionAt,
            uint256 siringWithId,
            uint256 birthTime,
            uint256 matronId,
            uint256 sireId,
            uint256 generation,
            uint256 genes
          );
        }

        contract ZombieFeeding is ZombieFactory {

          KittyInterface kittyContract;

          modifier ownerOf(uint _zombieId) {
            require(msg.sender == zombieToOwner[_zombieId]);
            _;
          }

          function setKittyContractAddress(address _address) external onlyOwner {
            kittyContract = KittyInterface(_address);
          }

          function _triggerCooldown(Zombie storage _zombie) internal {
            _zombie.readyTime = uint32(now + cooldownTime);
          }

          function _isReady(Zombie storage _zombie) internal view returns (bool) {
              return (_zombie.readyTime <= now);
          }

          function feedAndMultiply(uint _zombieId, uint _targetDna, string _species) internal ownerOf(_zombieId) {
            Zombie storage myZombie = zombies[_zombieId];
            require(_isReady(myZombie));
            _targetDna = _targetDna % dnaModulus;
            uint newDna = (myZombie.dna + _targetDna) / 2;
            if (keccak256(_species) == keccak256("kitty")) {
              newDna = newDna - newDna % 100 + 99;
            }
            _createZombie("NoName", newDna);
            _triggerCooldown(myZombie);
          }

          function feedOnKitty(uint _zombieId, uint _kittyId) public {
            uint kittyDna;
            (,,,,,,,,,kittyDna) = kittyContract.getKitty(_kittyId);
            feedAndMultiply(_zombieId, kittyDna, "kitty");
          }
        }
      "zombiefactory.sol": |
        pragma solidity ^0.4.19;

        import "./ownable.sol";

        contract ZombieFactory is Ownable {

            event NewZombie(uint zombieId, string name, uint dna);

            uint dnaDigits = 16;
            uint dnaModulus = 10 ** dnaDigits;
            uint cooldownTime = 1 days;

            struct Zombie {
              string name;
              uint dna;
              uint32 level;
              uint32 readyTime;
              uint16 winCount;
              uint16 lossCount;
            }

            Zombie[] public zombies;

            mapping (uint => address) public zombieToOwner;
            mapping (address => uint) ownerZombieCount;

            function _createZombie(string _name, uint _dna) internal {
                uint id = zombies.push(Zombie(_name, _dna, 1, uint32(now + cooldownTime), 0, 0)) - 1;
                zombieToOwner[id] = msg.sender;
                ownerZombieCount[msg.sender]++;
                NewZombie(id, _name, _dna);
            }

            function _generatePseudoRandomDna(string _str) private view returns (uint) {
                uint rand = uint(keccak256(_str));
                return rand % dnaModulus;
            }

            function createPseudoRandomZombie(string _name) public {
                require(ownerZombieCount[msg.sender] == 0);
                uint randDna = _generatePseudoRandomDna(_name);
                randDna = randDna - randDna % 100;
                _createZombie(_name, randDna);
            }

        }
      "ownable.sol": |
        /**
         * @title Ownable
         * @dev The Ownable contract has an owner address, and provides basic authorization control
         * functions, this simplifies the implementation of "user permissions".
         */
        contract Ownable {
          address public owner;

          event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

          /**
           * @dev The Ownable constructor sets the original `owner` of the contract to the sender
           * account.
           */
          function Ownable() public {
            owner = msg.sender;
          }


          /**
           * @dev Throws if called by any account other than the owner.
           */
          modifier onlyOwner() {
            require(msg.sender == owner);
            _;
          }


          /**
           * @dev Allows the current owner to transfer control of the contract to a newOwner.
           * @param newOwner The address to transfer ownership to.
           */
          function transferOwnership(address newOwner) public onlyOwner {
            require(newOwner != address(0));
            OwnershipTransferred(owner, newOwner);
            owner = newOwner;
          }

        }
    answer: |
      pragma solidity ^0.4.19;

      import "./zombieattack.sol";

      contract ZombieOwnership is ZombieAttack {

      }
---

Commen√ßons par les **_tokens_**.

Si vous √™tes dans la sph√®re Ethereum depuis un moment, vous avez s√ªrement entendu parler des tokens - en particulier des **tokens ERC20**.

un **_token_** Ethereum est un smart contract qui suit un ensemble de r√®gles - √† savoir, il impl√©mente un ensemble de fonctions standards que tous les autres contrats de tokens partagent, comme `transfer(address _to, uint256 _value)` et `balanceOf(address _owner)`.

Le smart contract a habituellement un mappage interne, `mapping(address => uint256) balances`, qui permet de conna√Ætre le solde de chaque adresse.

Un token est simplement un contrat qui permet de conna√Ætre combien de ce token chaque personne poss√®de, et qui a certaines fonctions pour permettre aux utilisateurs de transf√©rer leurs tokens √† d'autres adresses.

### Pourquoi c'est important ?

Puisque tous les tokens ERC20 partagent le m√™me ensemble de fonctions avec les m√™mes noms, ils peuvent tous √™tre manipul√©s de la m√™me mani√®re.

Cela veut dire que si vous construisez une application qui est capable d'interagir avec un token ERC20, elle sera aussi capable d'interagir avec n'importe quel token ERC20. De cette mani√®re, d'autres tokens pourrons facilement √™tre ajout√©s √† votre application sans avoir besoin de personnaliser le code. Vous pourrez simplement rajouter la nouvelle adresse du contrat du token, et boom, votre application pourra utiliser un nouveau token.

On pourrait prendre comme exemple un √©change. Quand un √©change ajoute un nouveau token ERC20, en v√©rit√© il a juste besoin d'ajouter un nouveau smart contract. Les utilisateurs pourront utiliser ce contrat pour envoyer des tokens sur l'adresse de l'√©change, et l'√©change pourra utiliser ce contrat pour renvoyer des tokens aux utilisateurs quand ils voudront retirer.

L'√©change a simplement besoin d'impl√©menter une fois la logique de transfert, et quand il veut ajouter un nouveau token ERC20, il suffit simplement d'ajouter l'adresse du nouveau contrat √† sa base de donn√©es.

### Autres standards des tokens

Les tokens ERC20 sont vraiment pratiques pour servir en temps que monnaie. Mais ils ne sont pas vraiment utiles pour repr√©senter des zombies dans notre jeu de zombie.

Premi√®rement, les zombies ne sont pas divisibles comme les monnaies - je peux vous envoyer 0.237 ETH, mais je ne peux pas vous envoyer 0.237 d'un zombie.

Deuxi√®mement, tous les zombies ne sont pas √©gaux. Votre zombie "**Pierre**" de niveau 2 n'est pas du tout √©gal √† mon zombie de niveau 732 "**H4XF13LD MORRIS üíØüíØüòéüíØüíØ**". (Tu peux pas test, *Pierre*).

Il existe un autre standard de token qui est beaucoup plus adapt√© pour les crypto-collectibles comme CryptoZombies ‚Äî ce sont les **_tokens ERC721._**

Les **_tokens ERC721_** **ne** sont **pas** interchangeable puisqu'ils sont suppos√©s √™tre uniques, et ne sont pas divisibles. Vous pouvez seulement les √©changer en entier, et ils ont chacun un ID unique. C'est exactement cela que l'on veut pour rendre nos zombies √©changeables.

> Remarque : En utilisant un standard comme ERC721, nous n'avons pas besoin d'impl√©menter les logiques qui d√©finissent comment les joueurs vont √©changer / vendre les zombies. Si on respecte les sp√©cifications, quelqu'un d'autre pourrait construire une plateforme d'√©change pour les actifs crypto-√©changeables, et nos zombies ERC721 seraient compatibles avec cette plateforme. C'est un avantage √©vident d'utiliser un standard de token au lieu d'impl√©menter sa propre logique d'√©change.

## A votre tour

Nous allons nous pencher sur l'impl√©mentation ERC721 dans le prochain chapitre. Mais pour l'instant, nous allons mettre en place la structure de nos fichiers pour notre le√ßon.

Nous allons stocker toute la logique ERC721 dans un contrat appel√© `ZombieOwnership`.

1. D√©clarez notre version `pragma` en haut du fichier (regardez les fichiers des le√ßons pr√©c√©dentes pour la syntaxe).

2. Le fichier devra importer, avec `import`, `zombieattack.sol`.

 3. D√©clarez un nouveau contrat, `ZombieOwnership`, qui h√©ritera de `ZombieAttack`. Laissez le corps du contrat vide pour l'instant.
