---
title: 背景知识及如何开始
actions: ['checkAnswer', 'hints']
requireLogin: true
material:
  editor:
    language: sol
    startingCode:
        "ZombieToken.sol": |
          //start here

        "ERC721XToken.sol": |

          // Full implementation with all included files at https://github.com/loomnetwork/erc721x

          pragma solidity ^0.4.25;

          import "./../../Interfaces/ERC721X.sol";

          import "./../../Interfaces/ERC721XReceiver.sol";
          import "./ERC721XTokenNFT.sol";

          import "openzeppelin-solidity/contracts/AddressUtils.sol";
          import "./../../Libraries/ObjectsLib.sol";


          // Additional features over NFT token that is compatible with batch transfers
          contract ERC721XToken is ERC721X, ERC721XTokenNFT {

              using ObjectLib for ObjectLib.Operations;
              using AddressUtils for address;

              bytes4 internal constant ERC721X_RECEIVED = 0x660b3370;
              bytes4 internal constant ERC721X_BATCH_RECEIVE_SIG = 0xe9e5be6a;

              event BatchTransfer(address from, address to, uint256[] tokenTypes, uint256[] amounts);


              modifier isOperatorOrOwner(address _from) {
                  require((msg.sender == _from) || operators[_from][msg.sender], "msg.sender is neither _from nor operator");
                  _;
              }

              function implementsERC721X() public pure returns (bool) {
                  return true;
              }

              /**
               * @dev transfer objects from different tokenIds to specified address
               * @param _from The address to BatchTransfer objects from.
               * @param _to The address to batchTransfer objects to.
               * @param _tokenIds Array of tokenIds to update balance of
               * @param _amounts Array of amount of object per type to be transferred.
               * Note:  Arrays should be sorted so that all tokenIds in a same bin are adjacent (more efficient).
               */
              function _batchTransferFrom(address _from, address _to, uint256[] _tokenIds, uint256[] _amounts)
                  internal
                  isOperatorOrOwner(_from)
              {

                  // Requirements
                  require(_tokenIds.length == _amounts.length, "Inconsistent array length between args");
                  require(_to != address(0), "Invalid recipient");

                  if (tokenType[_tokenIds[0]] == NFT) {
                      tokenOwner[_tokenIds[0]] = _to;
                      emit Transfer(_from, _to, _tokenIds[0]);
                  }

                  // Load first bin and index where the object balance exists
                  (uint256 bin, uint256 index) = ObjectLib.getTokenBinIndex(_tokenIds[0]);

                  // Balance for current bin in memory (initialized with first transfer)
                  // Written with bad library syntax instead of as below to bypass stack limit error
                  uint256 balFrom = ObjectLib.updateTokenBalance(
                      packedTokenBalance[_from][bin], index, _amounts[0], ObjectLib.Operations.SUB
                  );
                  uint256 balTo = ObjectLib.updateTokenBalance(
                      packedTokenBalance[_to][bin], index, _amounts[0], ObjectLib.Operations.ADD
                  );

                  // Number of transfers to execute
                  uint256 nTransfer = _tokenIds.length;

                  // Last bin updated
                  uint256 lastBin = bin;

                  for (uint256 i = 1; i < nTransfer; i++) {
                      // If we're transferring an NFT we additionally should update the tokenOwner and emit the corresponding event
                      if (tokenType[_tokenIds[i]] == NFT) {
                          tokenOwner[_tokenIds[i]] = _to;
                          emit Transfer(_from, _to, _tokenIds[i]);
                      }
                      (bin, index) = _tokenIds[i].getTokenBinIndex();

                      // If new bin
                      if (bin != lastBin) {
                          // Update storage balance of previous bin
                          packedTokenBalance[_from][lastBin] = balFrom;
                          packedTokenBalance[_to][lastBin] = balTo;

                          // Load current bin balance in memory
                          balFrom = packedTokenBalance[_from][bin];
                          balTo = packedTokenBalance[_to][bin];

                          // Bin will be the most recent bin
                          lastBin = bin;
                      }

                      // Update memory balance
                      balFrom = balFrom.updateTokenBalance(index, _amounts[i], ObjectLib.Operations.SUB);
                      balTo = balTo.updateTokenBalance(index, _amounts[i], ObjectLib.Operations.ADD);
                  }

                  // Update storage of the last bin visited
                  packedTokenBalance[_from][bin] = balFrom;
                  packedTokenBalance[_to][bin] = balTo;

                  // Emit batchTransfer event
                  emit BatchTransfer(_from, _to, _tokenIds, _amounts);
              }

              function batchTransferFrom(address _from, address _to, uint256[] _tokenIds, uint256[] _amounts) public {
                  // Batch Transfering
                  _batchTransferFrom(_from, _to, _tokenIds, _amounts);
              }

              /**
               * @dev transfer objects from different tokenIds to specified address
               * @param _from The address to BatchTransfer objects from.
               * @param _to The address to batchTransfer objects to.
               * @param _tokenIds Array of tokenIds to update balance of
               * @param _amounts Array of amount of object per type to be transferred.
               * @param _data Data to pass to onERC721XReceived() function if recipient is contract
               * Note:  Arrays should be sorted so that all tokenIds in a same bin are adjacent (more efficient).
               */
              function safeBatchTransferFrom(
                  address _from,
                  address _to,
                  uint256[] _tokenIds,
                  uint256[] _amounts,
                  bytes _data
              )
                  public
              {

                  // Batch Transfering
                  _batchTransferFrom(_from, _to, _tokenIds, _amounts);

                  // Pass data if recipient is contract
                  if (_to.isContract()) {
                      bytes4 retval = ERC721XReceiver(_to).onERC721XBatchReceived(
                          msg.sender, _from, _tokenIds, _amounts, _data
                      );
                      require(retval == ERC721X_BATCH_RECEIVE_SIG);
                  }
              }

              function transfer(address _to, uint256 _tokenId, uint256 _amount) public {
                  _transferFrom(msg.sender, _to, _tokenId, _amount);
              }

              function transferFrom(address _from, address _to, uint256 _tokenId, uint256 _amount) public {
                  _transferFrom(_from, _to, _tokenId, _amount);
              }

              function _transferFrom(address _from, address _to, uint256 _tokenId, uint256 _amount)
                  internal
                  isOperatorOrOwner(_from)
              {
                  require(tokenType[_tokenId] == FT);
                  require(_amount <= balanceOf(_from, _tokenId), "Quantity greater than from balance");
                  require(_to != address(0), "Invalid to address");

                  _updateTokenBalance(_from, _tokenId, _amount, ObjectLib.Operations.SUB);
                  _updateTokenBalance(_to, _tokenId, _amount, ObjectLib.Operations.ADD);
                  emit TransferWithQuantity(_from, _to, _tokenId, _amount);
              }

              function safeTransferFrom(address _from, address _to, uint256 _tokenId, uint256 _amount) public {
                  safeTransferFrom(_from, _to, _tokenId, _amount, "");
              }

              function safeTransferFrom(address _from, address _to, uint256 _tokenId, uint256 _amount, bytes _data) public {
                  _transferFrom(_from, _to, _tokenId, _amount);
                  require(
                      checkAndCallSafeTransfer(_from, _to, _tokenId, _amount, _data),
                      "Sent to a contract which is not an ERC721X receiver"
                  );
              }

              function _mint(uint256 _tokenId, address _to, uint256 _supply) internal {
                  // If the token doesn't exist, add it to the tokens array
                  if (!exists(_tokenId)) {
                      tokenType[_tokenId] = FT;
                      allTokens.push(_tokenId);
                  } else {
                      // if the token exists, it must be a FT
                      require(tokenType[_tokenId] == FT, "Not a FT");
                  }

                  _updateTokenBalance(_to, _tokenId, _supply, ObjectLib.Operations.ADD);
                  emit TransferWithQuantity(address(this), _to, _tokenId, _supply);
              }


              function checkAndCallSafeTransfer(
                  address _from,
                  address _to,
                  uint256 _tokenId,
                  uint256 _amount,
                  bytes _data
              )
                  internal
                  returns (bool)
              {
                  if (!_to.isContract()) {
                      return true;
                  }

                  bytes4 retval = ERC721XReceiver(_to).onERC721XReceived(
                      msg.sender, _from, _tokenId, _amount, _data);
                  return(retval == ERC721X_RECEIVED);
              }

          }
    answer: >
      pragma solidity ^0.4.25;

      import "./ERC721XToken.sol";

      contract ZombieCard is ERC721XToken {

      }
---

在本课中，我们将构建一个示例区块链游戏的代币合约。

它大致基于Loom Network我们自己的游戏“僵尸战场”中实现ERC721x的方式 —— “僵尸战场”一款类似"万智牌"或“炉石”的基于区块链的集换式卡牌游戏。

通过这种方式，你可以了解真实世界的用例，并且在完成本课程后，将能更好地将该实现应用到你自己的游戏中。

在第一章，我们先从一些背景知识开始，确保我们获得一致的信息......

## 背景知识：代币到底是什么?

将游戏中的物品称为“代币”可能会让人产生误解，因为“代币”一词暗指像货币一样花费的东西。

但是，以太坊上的**_token_**一词只是指智能合约的标准，它们都实现相同的通用函数，例如`transfer(address _to, uint256 _value)` 和`balanceOf(address _owner)`。

是的，你可以转移并拥有货币余额 —— 但也可以转移并拥有一些游戏中的物品，如剑或扑克牌。

尽管游戏中的剑不像货币一样可花费 —— 但我们*的确*希望玩家拥有的任何东西都具有相同的基本功能。

因此，“代币”的概念对于抽象出这些常用方法非常有用，它们可以应用到我们游戏的所有物品中 —— 这样，以太坊上的所有游戏物品都有一个共同的界面，并且可以受任何遵守同一代币标准的钱包和交易市场所支持。

## 不同的代币标准: ERC20 和 ERC721

最常见的代币标准是**_ERC20代币_**。 这些代币的作用类似于货币。但它们在表示数字物品(如集换式卡牌游戏中的卡牌)方面并不是特别有用。

首先，卡牌不像货币一样是可分割的 —— 你不可能拥有一张卡牌的五分之二。

其次，并非所有卡牌都是平等的，所以它们不可互换。 你不会想将你的Z-Virus卡换成我的Ozmoziz。

有另一种代币标准更适合加密收藏品 —— 它们被称为**_ERC721代币_**。

**_ERC721代币_**是**不可**互换的。每个物品都是唯一的，并且有唯一的ID。它们也不能被分割 —— 你只能将它们作为整体来交易。

所以，这些代币更适合制作我们的集换式卡牌游戏。

你有注意到我说的是它们**更合适** —— 而不是完美，对吧?😉

## ERC721x —— 两全其美的选择

ERC721对于戏内物品的问题在于，大多数游戏**都有**一些相同的物品。

例如，假设我们的卡牌游戏有300张不同的卡牌。 但是每一张卡牌都需要不止一个副本 —— 否则没有多少人可以玩我们的游戏！

同一张卡牌的多个副本（大部分）可互换。 因此，有一个考虑到这一点的代币标准很重要。

ERC721x允许你定义多个代币（将它们视为卡牌模板）来处理此用例，并且对于每张卡牌，你可以定义诸如将总共存在多少张之类的内容。

你会看到如何操作，因为在接下来的章节中我们将逐步引导你。👨‍🏫

## 开始吧: 在你的项目中使用ERC721x

实现新的ERC721x代币时，没必要从头开始。 就像你通过从<a href="https://github.com/OpenZeppelin/openzeppelin-solidity" target=_blank>Open Zeppelin</a>等地方复制模板并进行相关更改来开始ERC20代币一样，对于ERC721x，你可以先将我们的实现包含在你的项目中。

要将其包含在你未来的项目中，可以按照我们<a href="https://github.com/loomnetwork/erc721x" target=_blank>GitHub中的说明</a>进行操作。 但是对于本课，我们已经将`ERC721XToken.sol`包含在你的项目中了。

不要担心理解不了所有代码 —— 我们将逐步引导你完成你需要了解的部分。

# 实战演习

让我们从基础开始：我们将创建一个名为“ZombieCard”的新合约，它继承了 `ERC721XToken`的逻辑。

1. 首先，在顶部声明我们正在使用`pragma solidity ^0.4.25`。

2. 声明pragma之后，`导入` 文件`./ERC721XToken.sol`。

3. 接下来，声明一个名为“ZombieCard”的新`合约`。 它应该是使用关键字`is`继承自`ERC721XToken`。 暂时将合约正文留空。

> 注意：如果你不记得基础知识、感到困惑了，可以复习下<a href="https://cryptozombies.io/en/lesson/1/chapter/2">第1课第2章</a>和<a href="https://cryptozombies.io/en/lesson/2/chapter/5">第2课第5章</a>。